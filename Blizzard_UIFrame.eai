#IFDEF GLOBAL
  #DEFINE VER $VER$
  #IFDEF REFORGED
  boolean W3CIcons = true
  minimapicon array miniicons
  //framehandle array iconFrame
  integer miniicons_length = 0
  boolean W3CClock = true
  framehandle clock = null
  integer GameTimeSec = 0
  timer ClockTimer = null
  #ELSE

  #ENDIF
  #UNDEF VER
#ELSE
  #DEFINE VER $VER$
  #IFDEF REFORGED

//===========================================================================
// W3C UI Frame (Some ICON is custom)
// use Frame need adapter 1.36 new Observer mode minimap changing position , here no adapter
// use miniicon always have some slight deviation , like lua have innate advantage
//===========================================================================
function SetW3CMiniMapIcons takes nothing returns nothing
  local integer i = 0
  if GetTriggerPlayer() != null then
    if W3CIcons == false then
      set W3CIcons = true
      call DisplayTimedTextToForce(GetPlayersAll(), 7.00, "MiniMapIcon |cff00ff00[on]|r")
    else
      set W3CIcons = false
      call DisplayTimedTextToForce(GetPlayersAll(), 7.00, "MiniMapIcon |cfffd0000[off]|r")
    endif
  endif
  loop
    exitwhen i >= miniicons_length
    //call BlzFrameSetEnable(iconFrame[i], not BlzFrameGetEnable(iconFrame[i]))
    //call BlzDestroyFrame(iconFrame[i])
    //set iconFrame[i] = null
    call SetMinimapIconVisible(miniicons[i], W3CIcons)
    //call DestroyMinimapIcon(miniicons[i])
    //set miniicons[i] = null
    set i = i + 1
  endloop
endfunction

function W3CMiniMapIcons takes nothing returns nothing
  local trigger t = CreateTrigger()
  local integer i = 0
  loop
    exitwhen i >= GetBJMaxPlayers()
    if GetPlayerController(Player(i)) != MAP_CONTROL_COMPUTER then
      call TriggerRegisterPlayerChatEvent(t, Player(i), "-minimap", false)
    endif
    set i = i + 1
  endloop
  call TriggerAddAction(t, function SetW3CMiniMapIcons)
endfunction

function CreateW3CMiniMapIcons takes nothing returns nothing
  local unit u = null
  local group g = null
  local string t = null
  local integer i = 0
  local integer id = 0
  local real x = 0
  local real y = 0
  local real xR
  local real yR
  local real maxMinimapDist = RMaxBJ(GetCameraBoundMaxX() - GetCameraBoundMinX() + 768 , GetCameraBoundMaxY() - GetCameraBoundMinY() + 512) / 2
  local real xCenter = (GetCameraBoundMaxX() + GetCameraBoundMinX()) / 2
  local real yCenter = (GetCameraBoundMaxY() + GetCameraBoundMinY()) / 2
  if W3CIcons == false or not IsMapFlagSet(MAP_FOG_HIDE_TERRAIN) then
    return
  endif
  set g = CreateGroup()
  call GroupEnumUnitsOfPlayer(g, Player(PLAYER_NEUTRAL_PASSIVE), null)
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    set t = null
    set id = GetUnitTypeId(u)
    if false then
      #INCLUDETABLE <NeutralIcon.txt> #EFR
    elseif id == '%1' then
      set t = "%2"
      #ENDINCLUDE
    endif
    if t != null and GetUnitState(u, UNIT_STATE_LIFE) > 0 then
      // try fix deviation
      set xR = R2I((GetUnitX(u) - xCenter) / maxMinimapDist)
      set yR = R2I((GetUnitY(u) - yCenter) / maxMinimapDist)
      // set iconFrame[i] = BlzCreateFrameByType("BACKDROP", "Icon", BlzGetOriginFrame(ORIGIN_FRAME_GAME_UI, 0), "", 0)
      // call BlzFrameSetSize(iconFrame[i], 0.0166, 0.0166)
      // call BlzFrameSetAbsPoint(iconFrame[i], FRAMEPOINT_CENTER, 0.077 + 0.069 * xR, 0.074 + 0.069 * yR)
      // call BlzFrameSetTexture(iconFrame[i], t, 0, true)
      // call BlzFrameSetVisible(iconFrame[i], true)
      // call BlzFrameSetEnable(iconFrame[i], false)
      set x = GetUnitX(u) + 69 * xR - 74
      set y = GetUnitY(u) + 69 * yR - 74
      set miniicons[i] = CreateMinimapIcon(x, y, 255, 255, 255, t, FOG_OF_WAR_FOGGED)
      set i = i + 1
    endif
    call GroupRemoveUnit(g, u)
  endloop
  set miniicons_length = i
  call DestroyGroup(g)
  set g = null
  call W3CMiniMapIcons()
endfunction

function SetClockText takes string s, integer i returns string
  local integer timeMin = i / 60
  local integer timeSec = i - (timeMin * 60)
  if timeMin < 100 then
    set s = s + " "
  endif
  if timeMin < 10 then
    set s = s + "0"
  endif
  set s = s + I2S(timeMin) + " : "
  if timeSec < 10 then
    set s = s + "0"
  endif
  set s = s + I2S(timeSec)
  return s
endfunction

function UpdateClock takes nothing returns nothing
  local string timeString = "Time: "
  set GameTimeSec = GameTimeSec + 1
  if GameTimeSec >= 60000 then  // 1000 minutes in seconds
    set GameTimeSec = 59999  // Cap at 999:59
  endif
  set timeString = SetClockText(timeString, GameTimeSec)
  call BlzFrameSetText(clock, timeString)
  set timeString = null
endfunction

function SetW3CClock takes nothing returns nothing
  if GetTriggerPlayer() != null then
    if W3CClock == false then
      set W3CClock = true
      call DisplayTimedTextToForce(GetPlayersAll(), 7.00, "Game time clock |cff00ff00[on]|r")
    else
      set W3CClock = false
      call DisplayTimedTextToForce(GetPlayersAll(), 7.00, "Game time clock |cfffd0000[off]|r")
    endif
    call BlzFrameSetVisible(clock, W3CClock)
  endif
endfunction

function CreateW3CClock takes nothing returns nothing
  local trigger t = CreateTrigger()
  local integer i = 0
  if clock == null then
    set clock = BlzCreateFrameByType("TEXT", "GameTime", BlzGetOriginFrame(ORIGIN_FRAME_GAME_UI, 0), "", 0)
    call BlzFrameSetAbsPoint(clock, FRAMEPOINT_CENTER, 0.475, 0.561)  //0.465, 0.563
    call BlzFrameSetScale(clock, 1.2)
    call BlzFrameSetText(clock, "Time:  00:00")
    call BlzFrameSetTextColor(clock, BlzConvertColor(255, 205, 205, 50))
    set ClockTimer = CreateTimer()
    call TimerStart(ClockTimer, 1, true, function UpdateClock)
  endif
  loop
    exitwhen i >= GetBJMaxPlayers()
    if GetPlayerController(Player(i)) != MAP_CONTROL_COMPUTER then
      call BlzFrameSetVisible(clock, W3CClock)
      call TriggerRegisterPlayerChatEvent(t, Player(i), "-clock", true)
    endif
    set i = i + 1
  endloop
  call TriggerAddAction(t, function SetW3CClock)
endfunction

  #ELSE
  // old war3 cannot use
function CreateW3CMiniMapIcons takes nothing returns nothing

endfunction

function CreateW3CClock takes nothing returns nothing

endfunction

  #ENDIF
  #UNDEF VER
#ENDIF