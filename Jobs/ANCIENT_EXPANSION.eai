#IFDEF GLOBAL
    integer ancient_exp_state = 0
    integer ancient_exp_count = 0
    location ancient_exp_loc = null
    unit ancient_exp_wisp = null
    unit ancient_exp_unit = null
    group ancient_exp_group = CreateGroup()
    boolean ancient_attack_running = false
    boolean ancient_exp_maintree = false

#ELSE
function BuildAncient takes unit u, location loc returns nothing
	local unit temp = null
	local location l = null
	call RemoveGuardPosition(u)
	call GroupAddUnit(ancient_exp_group, u)
	if loc == null then
		set temp = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[racial_expansion], GetUnitX(current_expansion), GetUnitY(current_expansion), 270.0)
		set l = GetUnitLoc(temp)
		call RemoveUnit(temp)
		set temp = null
	else
		set l = Location(GetLocationX(loc),GetLocationY(loc))
	endif
	call IssuePointOrderByIdLoc(u,old_id[racial_expansion], l)
	call RemoveLocation(l)
	set l = null
endfunction

function AncientExpansionJob takes nothing returns nothing
local group g = null
local unit u = null
local location l = null
local boolean isstrong = false
call DisplayToAllJobDebug("ANCIENT_EXPANSION JOB STARTED")

if current_expansion == null then
	//set current_expansion = ExpNearestMin()
	call ChooseExpansion()
endif

if current_expansion == null then
	// Found out so no point trying ancient expansion again
	set ancient_exp_state = 0
	set ancient_exp_unit = null
	call SetCaptainHome(BOTH_CAPTAINS,GetLocationX(front_loc[0]),GetLocationY(front_loc[0]))
	call GroupRecycleGuardPositionInstant(CopyGroup(ancient_exp_group,g))
	set gold_buffer = 0
	set wood_buffer = 0
	set ancient_exp_maintree = false
	set g = null
	set not_taken_expansion = null
	call Trace("Ancient Expansion state : no mine or mine take by other player , cannot expansion")
	return
endif
if ancient_exp_state > 1 and ancient_exp_state != 5 and ancient_exp_unit != null and GetUnitState(ancient_exp_unit, UNIT_STATE_LIFE) <= 0 then
	call Trace("Ancient Expansion state : tree die , get new tree")
	set ancient_exp_state = 0
	set ancient_exp_unit = null
	call GroupRecycleGuardPositionInstant(CopyGroup(ancient_exp_group,g))
	set g = null
endif
set exp_time_count = ai_time
set not_taken_expansion = current_expansion
set u = GetExpFoe(u)
if ancient_exp_state == 0 then
	//if current_expansion == null then
		//call TQAddJob(5, ANCIENT_EXPANSION, 0)
		//set u = null
		//return
	//endif
	call Trace("Expansion Distance: " + Int2Str(R2I(GetExpansionDistance())))
	call Trace("Expansion state 0: get the Wisp and Location")
	set gold_buffer = GetUnitGoldCost2(racial_expansion)
	set wood_buffer = GetUnitWoodCost2(racial_expansion)
	if GetGold() < gold_buffer or GetWood() < wood_buffer then
		set ancient_exp_count = 0
		call Trace("Expansion State 0 - no resources expansion , wait check mine")
		set u = null
		if TownCountDone(racial_hall[1]) > 0 and ((race_manual_loading and TownCount(race_manual_loading_mine) == 0) or GetMinesOwned() == 0) then
			call Trace("Expansion State 0 - no resources expansion and no mine , wait move tree")
			set ancient_exp_state = 1
			call TQAddJob(1, ANCIENT_EXPANSION, 0)
			return
		endif
		call Trace("Expansion State 0 - no resources expansion and no tree , end job")
		set gold_buffer = 0
		set wood_buffer = 0
		set not_taken_expansion = null
		set ancient_expanding = false
		return
	elseif u == null and (current_expansion == not_taken_expansion or not CheckExpansionTaken(current_expansion)) then
		if ancient_exp_wisp == null or not UnitAlive(ancient_exp_wisp) then
			set ancient_exp_wisp = GetExpansionPeon()
			if ancient_exp_wisp == null then
				set ancient_exp_wisp = GetExpansionPeon2(ancient_exp_wisp)
			endif
		endif
		if ancient_exp_wisp != null then
			call CreateDebugTag("Expansion State 0 - prior build racial_expansion", 10, ancient_exp_wisp, 3.00, 1.50)
			call BuildAncient(ancient_exp_wisp, ancient_exp_loc)
			set ancient_exp_state = 5
			set ancient_exp_unit = null
		else
			call Trace("Expansion State 0 - no peon expansion , wait" + Int2Str(3 - ancient_exp_count) + "times to tree expansion")
			set ancient_exp_count = ancient_exp_count + 1
			if ancient_exp_count > 3 then
				set ancient_exp_state = 1
				set ancient_exp_count = 0
			endif
		endif
		call TQAddJob(5, ANCIENT_EXPANSION, 0)
	endif
elseif ancient_exp_state == 1 then
	if ancient_exp_unit == null or not UnitAlive(ancient_exp_unit) then  // no mine , check all hall
		set l = GetUnitLoc(current_expansion)
		set g = CreateGroup()
		call GroupEnumUnitsOfPlayer(g, ai_player, null)
		set g = SelectUnittype(g, UNIT_TYPE_TOWNHALL, true)
		set g = SelectByAlive(g, true)
		set g = SelectByHidden(g, false)
		set g = GetNearestSubGroupOfGroup(g, l, 1)
		set ancient_exp_unit = FirstOfGroup(g)
		call DestroyGroup(g)
		set g = null
		call RemoveLocation(l)
		set l = null
	endif
	if ancient_exp_unit == null then
		call Trace("Expansion State 1 - cannot expansion , no tree")
		set ancient_exp_maintree = false
		set ancient_expanding = false
		set gold_buffer = 0
		set wood_buffer = 0
		set ancient_exp_state = 0
		set u = null
		set not_taken_expansion = null
		return
	elseif GetLocationNonCreepStrength(GetUnitX(ancient_exp_unit), GetUnitY(ancient_exp_unit), 1500) <= 0 then
		call CreateDebugTag("Expansion State 1 - move tree", 10, ancient_exp_unit, 3.00, 1.50)
		set ancient_exp_state = 2
		call RemoveGuardPosition(ancient_exp_unit)
		call IssueImmediateOrderById(ancient_exp_unit, OrderIdCancel)
		call IssueImmediateOrder(ancient_exp_unit, "unroot")
		call GroupAddUnit(ancient_exp_group, ancient_exp_unit)
	else
		call Trace("Expansion State 1 - cannot move tree , wait next run job")
	endif
	call TQAddJob(1, ANCIENT_EXPANSION, 0)
elseif ancient_exp_state == 2 then
	call CreateDebugTag("Expansion State 2: Uproot Tree", 10, ancient_exp_unit, 3.00, 1.50)
	call IssueImmediateOrderById(ancient_exp_unit, OrderIdCancel)
	call IssueImmediateOrder(ancient_exp_unit, "unroot")
	//if hero_built[1] then
		//call RemoveGuardPosition(hero_unit[1])
		//call IssuePointOrder(hero_unit[1], "move", GetUnitX(ancient_exp_unit), GetUnitY(ancient_exp_unit))
		//call GroupClear(ancient_exp_group)
		//call GroupEnumUnitsOfPlayer(ancient_exp_group, ai_player, null)
		//set ancient_exp_group = SelectByAlive(ancient_exp_group, true)
		//set ancient_exp_group = SelectUnittype(ancient_exp_group, UNIT_TYPE_STRUCTURE, false)
		//set ancient_exp_group = SelectUnittype(ancient_exp_group, UNIT_TYPE_PEON, false)
		//set ancient_exp_group = SelectByUnitStandard(ancient_exp_group, true)
		//call GroupRemoveGuardPositionInstant(ancient_exp_group)
		//call GroupPointOrder(ancient_exp_group, "smart", GetUnitX(ancient_exp_unit), GetUnitY(ancient_exp_unit))
	set l = GetUnitLoc(ancient_exp_unit)
	if FoodUsed() < 20 or DistanceBetweenPoints(l, home_location) < 800 then  // take other tree , no unit need guardians, hall in home need make way
		set g = CreateGroup()
		call GroupEnumUnitsInRangeOfLoc(g, l, 2000, null)
		call GroupRemoveUnit(g, ancient_exp_unit)
		set g = SelectByPlayer(g, ai_player, true)
		call SelectByHidden(g, false)
		call SelectByAlive(g, true)
		loop
			set u = FirstOfGroup(g)
			exitwhen u == null
			if IsUnitType(u, UNIT_TYPE_ANCIENT) then
				call GroupAddUnit(ancient_exp_group, u)
				call IssueImmediateOrder(u, "unroot")
			endif
			call GroupRemoveUnit(g, u)
		endloop
		call DestroyGroup(g)
		set g = null
		set ancient_exp_maintree = true
	endif
	call RemoveLocation(l)
	set l = null
	//endif
	set ancient_exp_state = 3
	call TQAddJob(3, ANCIENT_EXPANSION, 0)
elseif ancient_exp_state == 3 then
	call CreateDebugTag("Expansion State 3: Awaiting Hero", 10, ancient_exp_unit, 3.00, 1.50)
	set isstrong = GetOwnStrength() > GetExpansionStrength()
	call SetCaptainHome(BOTH_CAPTAINS,R2I(GetUnitX(ancient_exp_unit)),R2I(GetUnitY(ancient_exp_unit)))
	if DistanceBetweenUnits(ancient_exp_unit, hero_unit[1]) <= 750 or u == null or isstrong or ancient_exp_maintree then
		if ancient_exp_count > 0 then
			set g = CopyGroup(ancient_exp_group, g)
			call GroupClear(ancient_exp_group)
			call GroupEnumUnitsInRange(ancient_exp_group, GetUnitX(ancient_exp_unit), GetUnitY(ancient_exp_unit), 1000, null)
			set ancient_exp_group = SelectByPlayer(ancient_exp_group, ai_player, true)
			set ancient_exp_group = SelectByAlive(ancient_exp_group, true)
			set ancient_exp_group = SelectUnittype(ancient_exp_group, UNIT_TYPE_STRUCTURE, false)
			set ancient_exp_group = SelectUnittype(ancient_exp_group, UNIT_TYPE_PEON, false)
			set ancient_exp_group = SelectByUnitStandard(ancient_exp_group, true)
			if not isstrong then
				call GroupAddUnit(ancient_exp_group, ancient_exp_unit)
				call IssueTargetOrder(ancient_exp_unit, "move", current_expansion)
			endif
			if ancient_exp_maintree then
				set g = SelectUnittype(g, UNIT_TYPE_ANCIENT, true)
				set g = SelectByAlive(g, true)
				call GroupAddGroup(g, ancient_exp_group)  //Ensure that the ancient tree can still be operated in the next loop
				call GroupPointOrder(ancient_exp_group, "attack", GetUnitX(current_expansion), GetUnitY(current_expansion))
			endif
			call DestroyGroup(g)
			set g = null
			////call GroupAddTypeAndRemoveGuard(ancient_exp_group, race_ancient_expansion_help_id, ai_player)
			//call GroupPointOrder(ancient_exp_group, "smart", GetUnitX(u), GetUnitY(u))
			set ancient_exp_state = 4
		else
			set ancient_exp_count = ancient_exp_count + 1
		endif
	else
		if ancient_exp_count > 0 then
			set ancient_exp_count = ancient_exp_count - 1
		endif
		//call GroupEnumUnitsOfPlayer(ancient_exp_group, ai_player, null)
		//set ancient_exp_group = SelectByAlive(ancient_exp_group, true)
		//set ancient_exp_group = SelectUnittype(ancient_exp_group, UNIT_TYPE_STRUCTURE, false)
		//set ancient_exp_group = SelectUnittype(ancient_exp_group, UNIT_TYPE_PEON, false)
		//set ancient_exp_group = SelectByUnitStandard(ancient_exp_group, true)
		//call GroupRemoveGuardPositionInstant(ancient_exp_group)
		//call GroupPointOrder(ancient_exp_group, "smart", GetUnitX(ancient_exp_unit), GetUnitY(ancient_exp_unit))
	endif
	call TQAddJob(5, ANCIENT_EXPANSION, 0)
elseif ancient_exp_state == 4 then
	call CreateDebugTag("Expansion State 4: Attacking and taking expansion", 10, ancient_exp_unit, 3.00, 1.50)
	call SetCaptainHome(BOTH_CAPTAINS,R2I(GetUnitX(ancient_exp_unit)),R2I(GetUnitY(ancient_exp_unit)))
	if u == null then
		call CreateDebugTag("Expansion State 4: expansion done", 10, ancient_exp_unit, 3.00, 1.50)
		call GroupRemoveUnit(ancient_exp_group, ancient_exp_unit)
		call IssueTargetOrder(ancient_exp_unit, "entangle", current_expansion)
		call TQAddUnitJob(R2I(GetTimeToReachUnit(ancient_exp_unit, current_expansion)*2 + 5), RESET_GUARD_POSITION, 0, ancient_exp_unit)
		call GroupRecycleGuardPositionInstant(CopyGroup(ancient_exp_group,g))
		set ancient_exp_state = 0
		//set no_attack_allowed = false
		set not_taken_expansion = null
		set ancient_expanding = false
		set ancient_attack_running = false
		set ancient_exp_maintree = false
		set ancient_exp_unit = null
		call SetCaptainHome(BOTH_CAPTAINS,GetLocationX(front_loc[0]),GetLocationY(front_loc[0]))
		set g = null
		return
	else
		//call GroupAddTypeAndRemoveGuard(ancient_exp_group, race_ancient_expansion_help_id, ai_player)
		if GetUnitState(ancient_exp_unit, UNIT_STATE_LIFE) <= GetUnitState(ancient_exp_unit, UNIT_STATE_MAX_LIFE) * 0.40 then
			call IssueImmediateOrderById(ancient_exp_unit, OrderId("eattree"))
		elseif ancient_attack_running then
			if DistanceBetweenUnits(ancient_exp_unit, u) <= 750 then
				call GroupTargetOrder(ancient_exp_group, "attack", u)
			else
				call GroupPointOrder(ancient_exp_group, "attack", GetUnitX(u), GetUnitY(u))
			endif
		endif
		call TQAddJob(2, ANCIENT_EXPANSION, 0)
	endif
elseif ancient_exp_state == 5 then
	if ancient_exp_wisp != null and GetUnitState(ancient_exp_wisp, UNIT_STATE_LIFE) > 0 and (current_expansion == not_taken_expansion or not CheckExpansionTaken(current_expansion)) then
		if GetUnitCurrentOrder(ancient_exp_wisp) != old_id[racial_expansion] then
			call CreateDebugTag("Expansion State 5: Fast Expand as camp clear", 10, ancient_exp_wisp, 3.00, 1.50)
			call BuildAncient(ancient_exp_wisp, ancient_exp_loc)
		endif
		call TQAddJob(5, ANCIENT_EXPANSION, 0)
	else
		call Trace("Expansion State 5: end")
		set gold_buffer = 0
		set wood_buffer = 0
		set ancient_exp_state = 0
		set ancient_exp_wisp = null
		set not_taken_expansion = null
		set ancient_expanding = false
		set ancient_exp_maintree = false
		call GroupRecycleGuardPositionInstant(CopyGroup(ancient_exp_group,g))
		set g = null
	endif
endif
set u = null
endfunction
#ENDIF