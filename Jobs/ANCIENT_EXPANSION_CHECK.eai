#IFDEF GLOBAL
   boolean ancient_expanding = false
   boolean homemineentangle = true
   integer OrderIdCancel = 851976
#ELSE

// ######################## Ancient Expansion Check #################
// #  Created by Strategy Master 13/07/05. Conditions that check that
// #  an ancient expansion can start.
// ##################################################################

function AncientExpansionGetExp takes nothing returns nothing
	local integer i = 0
	local real dmax = 1000000
	local real d = 0
	local unit u = null
	if homemineentangle and first_town_mine < 2 and (own_town_mine[0] == null or GetUnitTypeId(own_town_mine[0]) != 'egol') then  //  home location mine disappear
		set u = GetOneOfIdNearLoc(u, ai_player, 'ngol', home_location, 1000)
		if u != null and GetResourceAmount(u) > 0 then
			set i = expansion_list_length // entangle
		else
			set u = null
			set homemineentangle = false  // no ngol , stop
		endif
	endif
	loop
		exitwhen i >= expansion_list_length
		if GetResourceAmount(expansion_list[i]) > 0 then
			set d = DistanceBetweenPoints_kd(home_location,GetUnitLoc(expansion_list[i]))
			if d < dmax and not CheckExpansionTaken(expansion_list[i]) then
				set u = expansion_list[i]
				set dmax = d
			endif
		endif
		set i = i + 1
	endloop
	if u != null then
		set current_expansion = u
	endif
	set u = null
endfunction

function AncientExpansionCheck takes nothing returns nothing
	local unit u = null
	call DisplayToAllJobDebug("ANCIENT_EXPANSION_CHECK JOB START")
	if not ancient_expanding then
		if ((race_manual_loading and TownCountDone(racial_expansion) > TownCount(race_manual_loading_mine)) or (not race_manual_loading and TownCountDone(racial_expansion) > GetMinesOwned())) and (current_expansion == null or CheckExpansionTaken(current_expansion)) then
			call AncientExpansionGetExp()  // ELF can move tree , no need check gold resource
		endif
		if current_expansion != null and not CheckExpansionTaken(current_expansion) and not IsUnitInGroup(current_expansion,water_expansion) and not_taken_expansion == null then
			set u = GetExpFoe(u)
			call Trace("expansion free")
			if ((u != null and GetExpansionStrength() < race_ancient_expansion_strength) or u == null) and (hero_rush_bonus[hero[1]] + hero_rush_bonus[hero[2]] + hero_rush_bonus[hero[3]]) >= race_ancient_expansion_hero_rush_level then
				call Trace("Ancient expansion - Begin Job")
				set not_taken_expansion = current_expansion
				if ancient_exp_loc != null then
					call RemoveLocation(ancient_exp_loc)
				endif
				set ancient_exp_loc = GetBuildLocationInDistanceFromLoc_d(old_id[racial_expansion], GetUnitLoc(current_expansion), ver_creeps_attack_buildings_dist + 500, true)
				set ancient_expanding = true
				call TQAddJob(1, ANCIENT_EXPANSION, 0)
			elseif (hero_rush_bonus[hero[1]] + hero_rush_bonus[hero[2]] + hero_rush_bonus[hero[3]]) < race_ancient_expansion_hero_rush_level then
				call Trace("Ancient Expansion not possible with hero level")
			endif
			set u = null
		else
			call Trace("Ancient expansion - No expansion available")
		endif
	endif
	call TQAddJob(50, ANCIENT_EXPANSION_CHECK, 0)
endfunction

#ENDIF