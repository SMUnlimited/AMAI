#IFDEF GLOBAL
#ELSE


// Extra prevention against AMAI building two townhalls at the same location
// GetotherExpansionNearby dosn't yet detect haunted mines so they are still the exception
function BuildExpansionJob takes unit u returns nothing
	local unit v = null
	local real x = 0
	local real y = 0
	local real d = 0
	if u == null or not UnitAlive(u) or current_expansion == null then
		return
	endif
	set d = DistanceBetweenUnits(u, current_expansion)
	if GetUnitCurrentOrder(u) == old_id[racial_expansion] then
		if CheckDoubleExpansionsClaimedInArea(GetUnitX(current_expansion), GetUnitY(current_expansion), true) then
			if double_expansion_ally_present then
				call Trace("Cancelling Expansion - Enemy ally expansion detected")
				call IssueImmediateOrder(u, "stop")
				return
			endif
			if double_expansion_enemy_present then
				call Trace("Cancelling Expansion - Enemy double expansion detected")
				if d > (190 * 30) then // movement speed of peasant * seconds away. If way too far give up as enemy will be built first.
					call IssueImmediateOrder(u, "stop")
					return
				endif
			endif
		endif
		call TQAddUnitJob(2, BUILD_EXPANSION, 0, u)
		call Trace("Build Expansion JOB")
	elseif not race_uses_mine_expansion and d < race_max_expa_mine_distance then
		if CheckDoubleExpansionsClaimedInArea(GetUnitX(current_expansion), GetUnitY(current_expansion), true) then
			if not double_own_present and not double_expansion_ally_present and double_expansion_enemy_present and GetLocationNonCreepStrength(GetUnitX(current_expansion), GetUnitY(current_expansion), race_max_expa_mine_distance) == 0 then
				call IssueImmediateOrder(u, "stop")
				set v = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[racial_expansion], GetUnitX(current_expansion), GetUnitY(current_expansion), 270.0)
				set x = GetUnitX(v)
				set y = GetUnitY(v)
				call RemoveUnit(v)
				set v = null
				if DistanceBetweenPoints_dd(GetUnitLoc(v), GetUnitLoc(current_expansion)) <= race_max_expa_mine_distance then
					call IssuePointOrderById(u, old_id[racial_expansion], x, y)
					call TQAddUnitJob(2, BUILD_EXPANSION, 0, u)
					call Trace("Threat Expansion JOB")
				endif
			endif
		endif
	endif
endfunction

#ENDIF