#IFDEF GLOBAL
boolean retreat_home_n = false
#ELSE
function ResetBuyNeutralUnit takes integer nn returns nothing
  local unit u = neutral_sent[nn]
  if u != null then
    call GroupRemoveUnit(unit_buying_merc, u)
    if UnitAlive(u) and not IsUnitInGroup(u, unit_buying_item) and not IsUnitInGroup(u, unit_buying_tavern) and IsStandardUnit(u) then
      call RecycleGuardPosition(u)
      call AddAssault(1, GetUnitTypeId(u))
    endif
  endif
  set neutral_sent[nn] = null
  set u = null
  set neutral_ordered[nn] = false
  set retreat_home_n = false
  set buy_time_large[nn] = -1
  set buy_time_small[nn] = -1
endfunction

function BuyNeutral takes integer id returns nothing
  local real timetotravel = 0
  local real daytime = GetFloatGameState(GAME_STATE_TIME_OF_DAY)
  local real current_time = TimerGetElapsed(tq_timer)
  local real time_next_try = 0
  local real dist = 0
  local integer nn = GetNeutralNumber(id)
  local unit buy_place = nearest_neutral[nn]
  local boolean place_guarded = neutral_guarded[nn]
  //local integer detectedenemies = 0

  call DisplayToAllJobDebug("BUY_NEUTRAL JOB START")
  // moved this check here to prevent a job crash if placed lower down
  if TownCountDone(id) >= neutral_wanted[nn] then
    call ResetBuyNeutralUnit(nn)
    set buy_place = null
    return
  endif
  if neutral_sent[nn] == null or not UnitAlive(neutral_sent[nn]) then
    if neutral_sent[nn] != null then
      call GroupRemoveUnit(unit_buying_merc, neutral_sent[nn])
      set neutral_sent[nn] = null
    endif
    if retreat_home_n then
      set retreat_home_n = false
      set neutral_ordered[nn] = false
      set buy_time_large[nn] = -1
      set buy_time_small[nn] = -1
      set buy_place = null
      return
    endif
    set neutral_sent[nn] = GetUnitToBuy(neutral_sent[nn], buy_place)
    if neutral_sent[nn] == null then
      set neutral_ordered[nn] = false
      set buy_place = null
      return
    endif
    call GroupAddUnit(unit_buying_merc, neutral_sent[nn])
  endif
  if retreat_home_n then  // buy item use retreat_home_n , avoid conflict
    if DistanceBetweenPoints_dk(GetUnitLoc(neutral_sent[nn]), home_location) >= 1100 then
      call CreateDebugTag("BUY_ITEM: Retreat to Home", 10, neutral_sent[nn], 3.00, 1.50)
      call RemoveGuardPosition(neutral_sent[nn])
      call IssuePointOrder(neutral_sent[nn], "move", GetLocationX(home_location), GetLocationY(home_location))
      call TQAddUnitJob(GetTimeToReachLoc(neutral_sent[nn], home_location) + 1, RESET_GUARD_POSITION, 1, neutral_sent[nn])
      call TQAddJob(5, BUY_NEUTRAL, id)
    else
      call ResetBuyNeutralUnit(nn)
    endif
    set buy_place = null
    return
  endif
  set timetotravel = GetTimeToReachUnit(neutral_sent[nn], buy_place) + 1
  set dist = DistanceBetweenUnits(neutral_sent[nn], buy_place)
  //  call DisplayToAll("Buy Neutral Started")
  if (buy_time_large[nn] >= 0 and buy_time_large[nn] + buy_timeout_large < current_time) or (buy_time_small[nn] >= 0 and buy_time_small[nn] + buy_timeout_small < current_time) then
    set available_time[id] = ai_time + regenerate_time[id] / 2
    call ResetBuyNeutralUnit(nn)
    set buy_place = null
    return
  endif
  if place_guarded and daytime >= 5 and daytime < 18 then
    set time_next_try = (18 - daytime)*20
  else
    call RemoveGuardPosition(neutral_sent[nn])
    //call SetUnitUserData(neutral_sent[nn], UNIT_BUYING)
    if buy_time_large[nn] < 0 then
      set buy_time_large[nn] = current_time
    endif
    if dist < buy_distance then
      call CreateDebugTag("buy unit", 10, neutral_sent[nn], 3.00, 1.50)
      if GetUnitCurrentOrder(neutral_sent[nn]) == OrderId("move") then
        call IssueImmediateOrder(neutral_sent[nn], "holdposition")  // stop will let panda cancel element clone
      endif
      call IssueNeutralImmediateOrderById(ai_player, buy_place, old_id[id])
      if buy_time_small[nn] < 0 then
        set buy_time_small[nn] = current_time
      endif
      set time_next_try = 0.5
    else
      // Enemies are present so get home before they harm us
      if GetLocationNonCreepStrength(GetUnitX(buy_place), GetUnitY(buy_place), 1000) >= GetUnitStrength(neutral_sent[nn]) then
        call RemoveGuardPosition(neutral_sent[nn])
        call IssuePointOrder(neutral_sent[nn], "move", GetLocationX(home_location), GetLocationY(home_location))
        //call TQAddUnitJob(GetTimeToReachLoc(neutral_sent[nn], home_location), RESET_GUARD_POSITION, 1, neutral_sent[nn])
        set retreat_home_n = true
        call TQAddJob(1, BUY_NEUTRAL, id)
        set buy_place = null
        return
      endif
      //call CreateDebugTag("move to merc camp", 10, neutral_sent[nn], 3.00, 1.50)
      call CreateDebugTag("move to merc camp", 10, neutral_sent[nn], 3.00, 1.50)
      call IssuePointOrder(neutral_sent[nn], "move", GetUnitX(buy_place), GetUnitY(buy_place))
      set time_next_try = RMin(4, RMax(timetotravel,1))
    endif
  endif
  call TQAddJob(time_next_try, BUY_NEUTRAL, id)
  set buy_place = null
endfunction

#ENDIF