#IFDEF GLOBAL
#ELSE

function ResetBuyNeutralHero takes integer nn, integer id returns unit
  if neutral_sent[nn] != null then
    call GroupRemoveUnit(unit_buying_tavern, neutral_sent[nn])
    if UnitAlive(neutral_sent[nn]) and not IsUnitInGroup(neutral_sent[nn], unit_buying_item) and not IsUnitInGroup(neutral_sent[nn], unit_buying_merc) and IsStandardUnit(neutral_sent[nn]) then
      call RecycleGuardPosition(neutral_sent[nn])
      call AddAssault(1, GetUnitTypeId(neutral_sent[nn]))
    endif
  endif
  if not hero_built[1] and id == hero[1] then
    set recalculate_heros = true
  endif
  set neutral_sent[nn] = null
  set neutral_ordered[nn] = false
  set TavernResurrect = false
  set buy_time_large[nn] = -1
  set buy_time_small[nn] = -1
  return null
endfunction

function BuyNeutralHero takes integer id returns nothing
  local real timetotravel = 0
  local real daytime = GetFloatGameState(GAME_STATE_TIME_OF_DAY)
  local real current_time = TimerGetElapsed(tq_timer)
  local real time_next_try = 0
  local real distance = 0
  local integer nn = GetNeutralNumber(id)
  //local unit buying_unit = neutral_sent[nn]
  local unit tavern = nearest_neutral[nn]
  local boolean place_guarded = neutral_guarded[nn]

  call DisplayToAllJobDebug("BUY_NEUTRAL_HERO JOB START")

  //call Trace("Get Tavern Hero Job")
  if buy_type[id] == BT_HERO then
    set nn = NEUTRAL_TAVERN
    set tavern = nearest_neutral[nn]
    set place_guarded = neutral_guarded[nn]
  endif
  if TownCount(id) >= neutral_wanted[nn] or id != hero[tier] then
    set tavern = ResetBuyNeutralHero(nn, id)
    return
  endif

  if place_guarded and daytime >= 5 and daytime < 18 then
    set time_next_try = (18 - daytime)*20
  else

    if neutral_sent[nn] == null or GetUnitState(neutral_sent[nn], UNIT_STATE_LIFE) <= 0 or not IsStandardUnit(neutral_sent[nn]) then
      if neutral_sent[nn] != null then
        call GroupRemoveUnit(unit_buying_tavern, neutral_sent[nn])
      endif
      set neutral_sent[nn] = GetUnitToBuy(neutral_sent[nn], tavern)
      if neutral_sent[nn] == null then
        set tavern = ResetBuyNeutralHero(nn, id)
        call Trace("No unit to get neutral hero")
        return
      endif
      call Trace("unit for neutral hero obtained")
      call GroupAddUnit(unit_buying_tavern, neutral_sent[nn])
    endif
    set timetotravel = GetTimeToReachUnit(neutral_sent[nn], tavern)

    if current_time < (neutral_hero_time*5) - timetotravel then
      set time_next_try = RMax((neutral_hero_time*5) - timetotravel - current_time, 0.5)
    else
      set distance = DistanceBetweenUnits(neutral_sent[nn], tavern)
      call RemoveGuardPosition(neutral_sent[nn])
      if distance >= buy_distance then
        call CreateDebugTag("Move to Tavern", 10, neutral_sent[nn], 3.00, 1.50)
        call IssuePointOrder(neutral_sent[nn], "move", GetUnitX(tavern), GetUnitY(tavern))
        set time_next_try = RMin(4, RMax(timetotravel + 1,1))
      else
        call CreateDebugTag("buy tavern hero", 10, neutral_sent[nn], 3.00, 1.50)
        call IssueImmediateOrder(neutral_sent[nn], "holdposition")  // stop will let panda cancel element clone
        if TavernResurrect then  // try Resurrect all hero
          if hero_built[1] and TownCount(hero[1]) < 1 then
            call IssueNeutralTargetOrder(ai_player, tavern, "awaken", hero_unit[1])
          endif
          if hero_built[2] and TownCount(hero[2]) < 1 then
            call IssueNeutralTargetOrder(ai_player, tavern, "awaken", hero_unit[2])
          endif
          if hero_built[3] and TownCount(hero[3]) < 1 then
            call IssueNeutralTargetOrder(ai_player, tavern, "awaken", hero_unit[3])
          endif
          set tavern = ResetBuyNeutralHero(nn, id)
          return  // end job
        endif
        call IssueNeutralImmediateOrderById(ai_player, tavern, old_id[id])
        set time_next_try = 0.5
      endif
    endif
  endif
  call TQAddJob(time_next_try, BUY_NEUTRAL_HERO, id)
  set tavern = null
endfunction

#ENDIF