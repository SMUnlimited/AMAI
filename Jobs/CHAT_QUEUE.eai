#IFDEF GLOBAL
  boolean i_locked_chat = false
#ELSE
function FireChat takes nothing returns nothing
    local integer lockplayer = -1
    local integer c = 0
    local player p = null
    local string chat = Int2Str(CHAT_LOCK)
    if HaveStoredInteger(amaiCache, chat, "lock") then
      set lockplayer = GetStoredInteger(amaiCache, chat, "lock")
    endif
    //call Trace("Chat Queue: Player Lock: " + Int2Str(lockplayer) + " AI Player:" + Int2Str(GetAiPlayer()))
    if chat_queue_size >= 0 or chat_strategy != null then
      call Trace("Chat Queue: Want to chat")
      if lockplayer <= -1 then
         call Trace("Chat Queue: Taking Lock")
         call StoreInteger(amaiCache, chat, "lock", GetAiPlayer())
         call TQAddJob(GetRandomInt(1, 3), CHAT_QUEUE, 0)
         set chat = null
         return
      elseif lockplayer == GetAiPlayer() then
        set i_locked_chat = true
        if chat_strategy != null then
          loop
            exitwhen c >= GetBJMaxPlayers()
            set p = Player(c)
            if p != null and ((GetPlayerSlotState(p) == PLAYER_SLOT_STATE_PLAYING and IsPlayerAlly(ai_player,p)) or IsPlayerObserver(p)) then
              call DisplayToPlayer(chat_strategy, p, true)
            endif
            set c = c + 1
          endloop
          set chat_strategy = null
          set p = null
        else
          call DisplayToPlayer(chat_queue[0], chat_queue_player[0], chat_queue_importance[0])
          call RemoveOldMessage()
        endif
      endif
    elseif lockplayer == GetAiPlayer() then
       set i_locked_chat = false
       call Trace("Chat Queue: Reset Lock")
       call FlushStoredInteger(amaiCache, chat, "lock")
       call TQAddJob(GetRandomInt(21, 25), CHAT_QUEUE, 0)
       set chat = null
       return
    endif
    if i_locked_chat then
      call TQAddJob(GetRandomInt(5, 8), CHAT_QUEUE, 0)
    else
      call TQAddJob(GetRandomInt(16, 20), CHAT_QUEUE, 0)
    endif
    set chat = null
endfunction
#ENDIF