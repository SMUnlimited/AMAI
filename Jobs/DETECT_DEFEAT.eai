#IFDEF GLOBAL
    integer force_number	= 0
    player array own_force
    integer gold_problem_level = 0
#ELSE
//============================================================================
// Self destruction
//============================================================================
function RemoveFromOwnForce takes player p returns nothing
  local integer i = 0
  loop
    if i >= force_number then
      return
    endif
    exitwhen own_force[i] == p
    set i = i + 1
  endloop
  set force_number = force_number - 1
  loop
    exitwhen i >= force_number
    set own_force[i] = own_force[i+1]
    set i = i + 1
  endloop
endfunction

function KillYourself takes boolean end_ai returns nothing
    local group g = null
    local unit u = null
    local integer i = 0
    if not player_defeated then
      call Chat(C_AGG)
      call Chat(C_EGG)
      call Sleep(3)
      set g = CreateGroup()
      call GroupEnumUnitsOfPlayer(g, ai_player, null)
      loop
        set u = FirstOfGroup(g)
        exitwhen u == null
        if destroy_buildings_on_defeat then
          call KillUnit(u)
        else
          call SetUnitOwner(u, Player(13), true)
        endif
        call GroupRemoveUnit(g, u )
      endloop
      call DestroyGroup(g)
      set g = null
      if not no_ai_ally then
        set wood_exchange_level = 0
        set gold_exchange_level = 0
        call RemoveFromOwnForce(ai_player)
        call ExchangeOnce()
      endif
      if not destroy_buildings_on_defeat then
        call SetPlayerAlliance(ai_player, Player(PLAYER_NEUTRAL_AGGRESSIVE), ALLIANCE_PASSIVE, true)  // trigger the victory/defeat recognition triggers
        call SetPlayerAlliance(ai_player, Player(PLAYER_NEUTRAL_AGGRESSIVE), ALLIANCE_PASSIVE, false)
      endif
      set player_defeated = true
    endif
//    if end_ai then
//      call PauseCompAI(ai_player,true)
//    endif
endfunction

function Defeated takes nothing returns nothing
    if c_ally_total > 1 then
      call Chat(C_Goldproblem)
      call Sleep(10)
      if GetGold() >= gold_problem_level then
          call Chat(C_Thanks)
          return
      endif
    endif
    call KillYourself(no_ai_ally)
endfunction

function CheckDefeated takes nothing returns nothing
  local integer hall_count = TownCount(racial_hall[1])
  local integer hall_cost = GetUnitGoldCost2(racial_hall[1])
  local integer expa_cost = GetUnitGoldCost2(racial_expansion)
  local integer expa_count = TownCount(racial_expansion)
  local integer expa_done_count = TownCountDone(racial_expansion)
  local integer worker_cost = GetUnitGoldCost2(racial_peon)
  local integer worker_count = TownCountDone(racial_peon)
  local integer farm_food = GetFoodMade(old_id[racial_farm])
  local integer farm_count = TownCountDone(racial_farm)
  local integer farm_cost = GetUnitGoldCost2(racial_farm)
  local integer mines = GetMinesOwned()
  local integer agg = p_surrender[profile]
  local group g = CreateGroup()
  local unit u = null
  local real enemy_sum = 0
  local real player_sum = GetPlayerStrength(ai_player)
  local real ally_sum = 0
  local integer i = 0

  if disable_surrender then
	set agg = 0
  endif
  
  if town_threatened then
	  set enemy_sum = army_strength[town_threat_army[most_threatened_town]]
  else
	  call GroupEnumUnitsInRange(g,GetLocationX(home_location),GetLocationY(home_location),front_base_distance,null)
	  loop
	      set u = FirstOfGroup(g)
	      exitwhen u == null
		if UnitAlive(u) and GetOwningPlayer(u) != Player(PLAYER_NEUTRAL_AGGRESSIVE) and GetOwningPlayer(u) != Player(PLAYER_NEUTRAL_PASSIVE) and IsPlayerEnemy(ai_player, GetOwningPlayer(u)) then
	      	set enemy_sum = enemy_sum + GetUnitStrength(u)
		endif
		call GroupRemoveUnit(g,u)
	  endloop
  endif
  call DestroyGroup(g)
  set g = null

  if c_ally_total > 1 then
  loop
 	exitwhen i >= 12
	if IsPlayerAlly(ai_player,Player(i)) then
		set ally_sum = ally_sum + GetPlayerStrength(Player(i))
	endif
      set i = i + 1
  endloop
  endif

  if GetPlayerUnitCount(ai_player, true) == 0 and GetPlayerStructureCount(ai_player, true) == 0 then
	call KillYourself(no_ai_ally)	
	return
  endif
  
  if agg <= 0 then
	return
  endif

  if agg >= 1 then
  	if FoodUsed() == 0 and hall_count == 0 then
    		call KillYourself(no_ai_ally)
			return
 	endif
  endif

  if agg >= 2 then
	if FoodUsed() < 6 and hall_count == 0 and worker_count == 0 then
		call KillYourself(no_ai_ally)
		return
	endif 
	if enemy_sum > ((player_sum + ally_sum + 5) * 5) and hall_count <= 1 then
	 	call KillYourself(no_ai_ally)
		return
  	endif
  	if worker_count == 0 and GetGold() < worker_cost and hall_count >= 1 and FoodUsed() < 6 and worker_count == 0 then
      	set gold_problem_level = worker_cost
		call Defeated()
		return
  	endif
  	if GetPlayerStrength(ai_player) == 0 and hall_count == 0 and GetGold() < hall_cost then
    		set gold_problem_level = hall_cost
    		call Defeated()
			return
  	endif
  	if GetPlayerStrength(ai_player) == 0 and mines == 0 and GetGold() < expa_cost and (expa_count - expa_done_count) == 0 then
    		set gold_problem_level = expa_cost
    		call Defeated()
			return
  	endif
  endif

  if agg >= 3 then
	if FoodUsed() < 6 and hall_count == 0 then
		call KillYourself(no_ai_ally)
		return
	endif
	if enemy_sum > ((player_sum + ally_sum + 5) * 4) and hall_count <= 1 then
	 	call KillYourself(no_ai_ally)
		return
  	endif
	if (farm_count * farm_food) < FoodUsed() and mines == 0 and GetGold() < farm_cost and (FoodUsed()) < 10 and worker_count == 0 then
		set gold_problem_level = farm_cost
		call Defeated()
		return
  	endif
	if worker_count == 0 and GetGold() < worker_cost and hall_count >= 1 and FoodUsed() < 15 then
      	set gold_problem_level = worker_cost
		call Defeated()
		return
  	endif
  	if GetPlayerStrength(ai_player) <= 7 and mines == 0 and GetGold() < expa_cost and (expa_count - expa_done_count) == 0 then
    		set gold_problem_level = expa_cost
    		call Defeated()
			return
  	endif
  endif

  if agg >= 4 then
	if enemy_sum > ((player_sum + ally_sum + 5) * 3) and hall_count <= 2 then
	 	call KillYourself(no_ai_ally)
		return
  	endif
  endif
endfunction

function SumForceState takes playerstate ps returns integer
  local integer i = 0
  local integer sum = 0
  loop
    exitwhen i >= force_number
    set sum = sum + GetPlayerState(own_force[i],ps)
    set i = i + 1
  endloop
  return sum
endfunction

function SumForceFood takes nothing returns integer
  return SumForceState(PLAYER_STATE_RESOURCE_FOOD_USED)
endfunction

function SumForceGold takes nothing returns integer
  return SumForceState(PLAYER_STATE_RESOURCE_GOLD)
endfunction

function CheckDefeatedForce takes nothing returns nothing
  if SumForceFood() == 0 and SumForceGold() <= 100 then
    call KillYourself(true)
  endif
endfunction

function DetectDefeatJob takes nothing returns nothing
  call DisplayToAllJobDebug("DETECT_DEFEAT Job Start")
  call CheckDefeated()
  call CheckDefeatedForce()
  if not player_defeated then
    call TQAddJob(13 * sleep_multiplier, DETECT_DEFEAT, 0)
  endif
endfunction
#ENDIF