#IFDEF GLOBAL
    unit double_expansion_target = null
    boolean double_expansion_ally_present = false
    boolean double_expansion_allyhuman_present = false
    boolean double_own_present = false
    boolean double_expansion_enemy_present = false
#ELSE
//============================================================================
// Detect double expansion
//============================================================================

function GetOtherExpansionNearby takes unit ru, real tx, real ty returns unit
  local group g = CreateGroup()
  local unit u = null
  local player p = null
  //local integer id = 0
  set ru = null
  call GroupEnumUnitsInRange(g, tx, ty, expansion_taken_radius, null)
  // set g = SelectByPlayer(g, ai_player, false)
  // set g = SelectByAlive2(g, true)
  // set g = SelectByHidden(g, false)
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    set p = GetOwningPlayer(u)
    if p != Player(PLAYER_NEUTRAL_PASSIVE) and p != Player(PLAYER_NEUTRAL_AGGRESSIVE) and p != ai_player and GetUnitState(u, UNIT_STATE_LIFE) >= 1 and not IsUnitHidden(u) then
      if IsUnitType(u, UNIT_TYPE_TOWNHALL) == true or IsUnitGoldMine(u) then //or id == oUNDEAD_MINE then//id == oTOWN_HALL or id == oKEEP or id == oCASTLE or id == oUNDEAD_MINE or id == oGREAT_HALL or id == oSTRONGHOLD or id == oFORTRESS or id == oTREE_LIFE or id == oTREE_AGES or id == oTREE_ETERNITY then
        call DestroyGroup(g)
        set g = null
        set ru = u
        set u = null
        set p = null
        return ru
      elseif IsPlayerEnemy(p, ai_player) and IsUnitType(u, UNIT_TYPE_STRUCTURE) == true then
        set ru = u
      endif
    endif
    call GroupRemoveUnit(g,u)
  endloop
  call DestroyGroup(g)
  set g = null
  set p = null
  return ru
endfunction

function CheckDoubleExpansionsClaimedInArea takes real tx, real ty, boolean isBuildCommand returns boolean
  local group g = CreateGroup()
  local unit u = null
  local player p = null
  local integer minecount = 0
  local integer claimedMines = 0
  set double_expansion_ally_present = false
  set double_expansion_enemy_present = false
  set double_expansion_allyhuman_present = false
  set double_own_present = false
  call GroupEnumUnitsInRange(g, tx, ty, expansion_taken_radius, null)
  //set g = SelectByAlive2(g, true)
  if double_expansion_target != null and not UnitAlive(double_expansion_target) then
    set double_expansion_target = null
  endif
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    set p = GetOwningPlayer(u)
    if p != Player(PLAYER_NEUTRAL_AGGRESSIVE) and GetUnitState(u, UNIT_STATE_LIFE) >= 1 then
      if IsPlayerEnemy(p, ai_player) and IsUnitType(u, UNIT_TYPE_STRUCTURE) then
        if double_expansion_target == null and not IsUnitHidden(u) then
          set double_expansion_target = u
        endif
        set double_expansion_enemy_present = true
      endif
      if IsUnitGoldMine(u) or IsUnitType(u, UNIT_TYPE_TOWNHALL) then
        if IsValidMineClaimed(u) then
          set claimedMines = claimedMines + 1
        endif
        if p != Player(PLAYER_NEUTRAL_PASSIVE) then
          if p == ai_player then
            set double_own_present = true
          elseif IsPlayerAlly(ai_player, p) then
            set double_expansion_ally_present = true
            if GetPlayerController(p) == MAP_CONTROL_USER then
              set double_expansion_allyhuman_present = true
            endif
          else
            if double_expansion_target == null and not IsUnitHidden(u) then
              set double_expansion_target = u
            endif
            set double_expansion_enemy_present = true
          endif
        endif
        if not IsUnitType(u, UNIT_TYPE_TOWNHALL) then
          set minecount = minecount + 1
        endif
      endif
    endif
    call GroupRemoveUnit(g,u)
  endloop
  call DestroyGroup(g)
  set g = null
  set p = null
  call Trace("Total Mines:" + Int2Str(minecount) + " Claimed Mines:" + Int2Str(claimedMines))
  if isBuildCommand then
    set claimedMines = claimedMines + 1 // I'm on the way to build an expansion so include in count
  endif
  return minecount > 0 and claimedMines > minecount
endfunction

// Beware Does not include own halls and mines in this check
function GetTotalClaimedMinesNearby takes real tx, real ty returns integer
  local group g = CreateGroup()
  local unit u = null
  local player p = null
  local integer count = 0
  call GroupEnumUnitsInRange(g, tx, ty, expansion_taken_radius, null)
  // set g = SelectByPlayer(g, ai_player, false)
  // set g = SelectByPlayer(g, Player(PLAYER_NEUTRAL_PASSIVE), false)
  // set g = SelectByPlayer(g, Player(PLAYER_NEUTRAL_AGGRESSIVE), false)
  // set g = SelectByAlive2(g, true)
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    set p = GetOwningPlayer(u)
    if p != Player(PLAYER_NEUTRAL_PASSIVE) and p != Player(PLAYER_NEUTRAL_AGGRESSIVE) and p != ai_player and GetUnitState(u, UNIT_STATE_LIFE) >= 1 and (IsUnitType(u, UNIT_TYPE_TOWNHALL) == true or IsUnitGoldMine(u)) then //or id == oUNDEAD_MINE then//id == oTOWN_HALL or id == oKEEP or id == oCASTLE or id == oUNDEAD_MINE or id == oGREAT_HALL or id == oSTRONGHOLD or id == oFORTRESS or id == oTREE_LIFE or id == oTREE_AGES or id == oTREE_ETERNITY then
      set count = count + 1
    endif
    call GroupRemoveUnit(g,u)
  endloop
  call DestroyGroup(g)
  set g = null
  set p = null
  call Trace("Total Claimed Mines by other players:" + Int2Str(count))
  return count
endfunction

function GetTotalMinesNearby takes real tx, real ty returns integer
  local group g = CreateGroup()
  local unit u = null
  local player p = null
  local integer count = 0
  call GroupEnumUnitsInRange(g, tx, ty, expansion_taken_radius, null)
  //set g = SelectByAlive2(g, true)
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    set p = GetOwningPlayer(u)
    if p != Player(PLAYER_NEUTRAL_PASSIVE) and p != Player(PLAYER_NEUTRAL_AGGRESSIVE) and GetUnitState(u, UNIT_STATE_LIFE) >= 1 and IsUnitGoldMine(u) then
      set count = count + 1
    endif
    call GroupRemoveUnit(g,u)
  endloop
  call DestroyGroup(g)
  set g = null
  set p = null
  call Trace("Total Mines:" + Int2Str(count))
  return count
endfunction

function CheckDoubleExpansion takes nothing returns nothing
  local group g = CreateGroup()
  local unit u = null
  local boolean b = false
  local integer i = TownCount(racial_expansion)
  local integer id = 0
  call GroupEnumUnitsOfPlayer(g, ai_player, null)
  //set g = SelectNumberOfId(g, 100, old_id[racial_expansion])
  if race_manual_loading then
    set i = TownCount(race_manual_loading_mine)
  endif
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    set id = GetUnitTypeId(u)
    if id == old_id[racial_expansion] and UnitAlive(u) and not IsUnitHidden(u) then
      set b = true
      if CheckDoubleExpansionsClaimedInArea(GetUnitX(u), GetUnitY(u), false) then
        if double_expansion_ally_present and i > 1 and DistanceBetweenPoints_kd(home_location, GetUnitLoc(u)) > 1300 then
          if GetUnitState(u, UNIT_STATE_LIFE) < GetUnitState(u, UNIT_STATE_MAX_LIFE) then
            call IssueImmediateOrderById(u, order_cancel)
            if double_expansion_allyhuman_present then
              // Simulated cancel as real cancel failed as it is actually successfully built at this point.
              // Human probrably wants your expansion gone and attacked it
              call SetPlayerState(ai_player, PLAYER_STATE_RESOURCE_GOLD, GetPlayerState(ai_player, PLAYER_STATE_RESOURCE_GOLD) + GetUnitGoldCost(id))
              call SetPlayerState(ai_player, PLAYER_STATE_RESOURCE_LUMBER, GetPlayerState(ai_player, PLAYER_STATE_RESOURCE_LUMBER) + GetUnitWoodCost(id))
              call KillUnit(u)
            endif
          endif
        endif
      endif
      set i = i - 1
    endif
    call GroupRemoveUnit(g,u)
  endloop
  if b then
    if double_expansion_ally_present then
      call Trace("Ally double expansion detected")
    endif
    if double_expansion_enemy_present then
      call Trace("Enemy double expansion detected")
    endif
  endif
  call DestroyGroup(g)
  set g = null
endfunction

function DetectDoubleExpJob takes nothing returns nothing
  call DisplayToAllJobDebug("DETECT_DOUBLE JOB START")
  call CheckDoubleExpansion()
  call TQAddJob(10 * sleep_multiplier, DETECT_DOUBLE_EXP, 0)
endfunction
#ENDIF