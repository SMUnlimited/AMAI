#IFDEF GLOBAL

#ELSE
function SetPlayerGold takes player p, integer i returns nothing
  call SetPlayerState(p,PLAYER_STATE_RESOURCE_GOLD,i)
endfunction

function SetPlayerWood takes player p, integer i returns nothing
  call SetPlayerState(p,PLAYER_STATE_RESOURCE_LUMBER,i)
endfunction

function IncreasePlayerGold takes player p, integer i returns nothing
//    call Trace(c2s(GetPlayerColor(p))+"gets gold: "+Int2Str(i))
  call SetPlayerGold(p, GetPlayerState(p,PLAYER_STATE_RESOURCE_GOLD) + i)
endfunction

function IncreasePlayerWood takes player p, integer i returns nothing
  call SetPlayerWood(p, GetPlayerState(p,PLAYER_STATE_RESOURCE_LUMBER) + i)
endfunction

// Distribute excess gold/wood to allies below threshold, higher difficulty means 75% more is available to distribute
function DistributeGold takes integer value, integer threshold returns nothing
  local integer amount = value
  local integer p_amount = 0
  local integer i = 0
  local integer exc_amount = 0
  loop
    exitwhen i >= force_number
    if GetPlayerSlotState(own_force[i]) == PLAYER_SLOT_STATE_PLAYING and GetPlayerGold(own_force[i]) < threshold then
      set p_amount = p_amount + 1
    endif
    set i = i + 1
  endloop
  if p_amount > 0 then
    set p_amount = value / p_amount // Only share with players who need gold
    call Trace("EXCHANGE: Distribute Max Gold per player: " + Int2Str(p_amount))
    set i = 0
    loop
      exitwhen i >= force_number
      if GetPlayerSlotState(own_force[i]) == PLAYER_SLOT_STATE_PLAYING and GetPlayerGold(own_force[i]) < threshold then
        set exc_amount = Min(p_amount, threshold - GetPlayerGold(own_force[i]))
        call IncreasePlayerGold(own_force[i], exc_amount)
        set amount = amount - exc_amount
      endif
      set i = i + 1
    endloop
  endif
  call IncreasePlayerGold(ai_player,amount)
endfunction

function DistributeWood takes integer value, integer threshold returns nothing
  local integer amount = value
  local integer p_amount = 0
  local integer i = 0
  local integer exc_amount = 0
  loop
    exitwhen i >= force_number
    if GetPlayerSlotState(own_force[i]) == PLAYER_SLOT_STATE_PLAYING and GetPlayerWood(own_force[i]) < threshold then
      set p_amount = p_amount + 1
    endif
    set i = i + 1
  endloop  
  if p_amount > 0 then
    set p_amount = value / p_amount // Only share with players who need wood
    call Trace("EXCHANGE: Distribute Max Wood per player: " + Int2Str(p_amount))
    set i = 0
    loop
      exitwhen i >= force_number
      if GetPlayerSlotState(own_force[i]) == PLAYER_SLOT_STATE_PLAYING and GetPlayerWood(own_force[i]) < threshold then
        set exc_amount = Min(p_amount, threshold - GetPlayerWood(own_force[i]))
        call IncreasePlayerWood(own_force[i], exc_amount)
        set amount = amount - exc_amount
      endif
      set i = i + 1
    endloop
  endif
  call IncreasePlayerWood(ai_player,amount)
endfunction

function ExchangeOnce takes nothing returns nothing
  local integer amount = 0
  if force_number <= 0 then
    return
  endif
  set amount = GetPlayerGold(ai_player) - R2I(gold_exchange_level * (Max(difficulty - 2, 0) * 0.75))
  if amount > 0 then
    call IncreasePlayerGold(ai_player, -amount)
    call DistributeGold(amount, gold_exchange_level)
  endif
  set amount = GetPlayerWood(ai_player) - R2I(wood_exchange_level * (Max(difficulty - 2, 0) * 0.75))
  if amount > 0 then
    call IncreasePlayerWood(ai_player, -amount)
    call DistributeWood(amount, wood_exchange_level)
  endif
endfunction

function ExchangeAll takes nothing returns nothing
  local integer amount = 0
  if force_number <= 0 then
    return
  endif
  set amount = GetPlayerGold(ai_player)
  call IncreasePlayerGold(ai_player, -amount)
  call DistributeGold(amount, 99999)
  set amount = GetPlayerWood(ai_player)
  call IncreasePlayerWood(ai_player, -amount)
  call DistributeWood(amount, 99999)
endfunction  

function ExchangeJob takes nothing returns nothing
  call DisplayToAllJobDebug("EXCHANGE JOB Start")
  call ExchangeOnce()
  call TQAddJob(30 * sleep_multiplier, EXCHANGE, 0)
endfunction
#ENDIF