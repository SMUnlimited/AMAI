#IFDEF GLOBAL
  boolean unloadpeon = false
#ELSE
function PeonMineCheck takes unit mine returns boolean
  local group g = CreateGroup()
  local group tempg = null
  local location unitloc = GetUnitLoc(mine)
  local unit u = null
  local unit savewisp = null
  local integer i = 0
  local integer peon = TownCount(racial_peon)
  local integer peond = TownCountDone(racial_peon)
  local integer delaywisps = Min(ver_optimal_gold - 1,peond)
  if (race_uses_mine_expansion and peond >= ver_optimal_gold) or peond > ver_optimal_gold then
    set delaywisps = ver_optimal_gold
  endif
  if race_manual_loading then
    set tempg = CreateGroup()
    call GroupEnumUnitsOfPlayer(g, ai_player, null)
    call GroupRemoveUnit(g, expansion_peon)
    loop
      set u = FirstOfGroup(g)
      exitwhen u == null
      if GetUnitTypeId(u) == old_id[race_manual_loading_wisp] and UnitAlive(u) then
        if IsUnitInTransport(u, mine) then
          set savewisp = u
          set i = i + 1
        elseif not IsUnitHidden(u) then
          call GroupAddUnit(tempg, u)
        endif
      endif
      call GroupRemoveUnit(g, u)
    endloop
    if FoodSpace() <= 0 and i >= 3 and unloadpeon == false and savewisp != null and peon <= 4 and peon == peond then  // no foodspace , no peon(at least keep 2 harvest mine) , need one harvest mine peon build Farms
      set delaywisps = 2  // Consistent with peon_assignment
      set unloadpeon = IssueTargetOrder(mine, "unload", savewisp)
    elseif not hero_built[1] and peon < 8 then
      set delaywisps = ver_optimal_gold - 2
      set i = Min(i,delaywisps)
    endif
    if i < delaywisps then
      set tempg = SelectByLoaded(tempg, false)
      set tempg = SelectUnittype(tempg, UNIT_TYPE_STRUCTURE, false)  //  Preventing Tree Transformation
      set tempg = GetNearestSubGroupOfGroup(tempg, unitloc, delaywisps - i)
      //call GroupRemoveGuardPositionInstant(g) // this gets wisps to come off the trees.
      //call GroupTargetOrder(g, "harvest", mine)
      loop  //use accurate quantity control , prevent any wips harvestmine , but mine no vacancies
        set u = FirstOfGroup(tempg)
        exitwhen u == null or i >= delaywisps
        call IssueTargetOrder(u, "harvest", mine)
        set i = i + 1
        call GroupRemoveUnit(tempg, u)
      endloop
    endif
    call DestroyGroup(tempg)
    set tempg = null
    set savewisp = null
  elseif race_uses_mine_expansion then
    set tempg = CreateGroup()
    call GroupEnumUnitsOfPlayer(g, ai_player, null)
    call GroupRemoveUnit(g, expansion_peon)
    loop
      set u = FirstOfGroup(g)
      exitwhen u == null or i >= delaywisps
      if GetUnitTypeId(u) == old_id[racial_peon] and UnitAlive(u) and not IsUnitHidden(u) then
        if GetUnitCurrentOrder(u) == OrderId("harvest") and DistanceBetweenUnits(u,mine) < 300 then  // UD harvest gold max standard distance is 221
          set i = i + 1
        elseif GetUnitCurrentOrder(u) == 0 and not IsUnitBuying(u) then
          call GroupAddUnit(tempg,u)  // prevent the mine harvest peon more than 5
        endif
      endif
      call GroupRemoveUnit(g, u)
    endloop
    loop
      set u = FirstOfGroup(tempg)
      exitwhen u == null or i >= delaywisps
      call IssueTargetOrder(u, "harvest", mine)
      set i = i + 1
      call GroupRemoveUnit(tempg, u)
    endloop
    call DestroyGroup(tempg)
    set tempg = null
  else  //human and orc
    if not hero_built[1] or GetGold() < 300 then
      set peon = 3
    else
      set peon = 2
    endif
    call GroupEnumUnitsOfPlayer(g, ai_player, null)
    call GroupRemoveUnit(g, expansion_peon)
    set g = SelectById(g, old_id[racial_peon], true)
    set g = SelectByHidden(g, false)
    if TownCount(racial_hall[1]) > 1 then
      set g = GetNearestSubGroupOfGroup(g, unitloc, 5)
    endif
    loop
      set u = FirstOfGroup(g)
      exitwhen u == null or i >= peon
      if UnitAlive(u) and (GetUnitCurrentOrder(u) == 0 or GetUnitCurrentOrder(u) == OrderId("harvest")) and not IsUnitBuying(u) then  // no get other order peon , maybe the peon go buy unit or build some
        call IssueTargetOrder(u, "harvest", mine)
        set i = i + 1
      endif
      call GroupRemoveUnit(g, u)
    endloop
    if not hero_built[1] or GetGold() < 300 then
      set i = -1  // At the beginning of the game, pay more attention
    else
      set i = delaywisps  //no need full
    endif
  endif
  call RemoveLocation(unitloc)
  set unitloc = null
  call DestroyGroup(g)
  set g = null
  set u = null
  return i < delaywisps
endfunction

function HarvestCheckJob takes nothing returns nothing
  local group g = null
  local unit u = null
  local integer i = 0
  local integer timeifx = 0
  local boolean mines_not_full = true
  //  call DisplayToAll("Harvest Start")
  if first_town_mine == -1 then
    return  // home_location no more mine , ORC and HUMAN no need run
  endif
  if (GetGold() > 1600 or (GetWood() < 200 and hero_built[1])) and not race_manual_loading and not race_uses_mine_expansion then  // human and orc
    call TQAddJob(32, HARVEST_CHECK, 0)  // Prevent too many peon harvest
    return
  endif
  call DisplayToAllJobDebug("HARVEST CHECK Job Start")
  set unloadpeon = false  // if no food , race_manual_loading need peon unload mine , but just need once unload
  set g = CreateGroup()
  if race_manual_loading then
    call GroupEnumUnitsOfPlayer(g, ai_player, null)
    set g = SelectNumberOfId(g, 100, old_id[race_manual_loading_mine])
  elseif not TownThreatened() then
    if race_uses_mine_expansion then
      call GroupEnumUnitsInRangeOfLoc(g, home_location, expansion_taken_radius, null)
      set g = SelectByPlayer(g, ai_player, true)
      set g = SelectById(g, old_id[racial_expansion], true)
    elseif fixmine != null and GetResourceAmount(fixmine) > 0 and (TownCountDone(racial_peon) > 8 or (GetUnitCount(old_id[hero[1]]) < 0 and not hero_built[1]) or GetGold() < 300) then  //human and orc
      call GroupAddUnit(g, fixmine)  // get fixmine on MultipleMinefix , prevention all peon harvest so only one mine
      if GetResourceAmount(fixmine) < 300 then
        set timeifx = 36
      endif
    endif
  endif
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null or (i >= 3 and not race_uses_mine_expansion and not race_manual_loading)
    if UnitAlive(u) and not IsUnitHidden(u) then
      set mines_not_full = PeonMineCheck(u) and mines_not_full
      set i = i + 1  //prevent all peon harvest mine , not peon harvest wood
    endif
    call GroupRemoveUnit(g, u)
  endloop
  call DestroyGroup(g)
  set g = null
  set u = null
  if mines_not_full then
    call TQAddJob(4, HARVEST_CHECK, 0)
  else
    call TQAddJob(40 - timeifx, HARVEST_CHECK, 0)
  endif
endfunction
#ENDIF