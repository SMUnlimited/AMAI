#IFDEF GLOBAL
#ELSE

function GetHealthFountainID takes nothing returns integer
  if nearest_neutral[NEUTRAL_POWER_FOUNTAIN] != null then
    return NEUTRAL_POWER_FOUNTAIN
  elseif nearest_neutral[NEUTRAL_HEAL_WARD] != null then
    return NEUTRAL_HEAL_WARD
  endif
  return NEUTRAL_HEALING_FOUNTAIN
endfunction

function GetHealthFountainUnit takes nothing returns unit
  if nearest_neutral[NEUTRAL_POWER_FOUNTAIN] != null and UnitAlive(nearest_neutral[NEUTRAL_POWER_FOUNTAIN]) then
    return nearest_neutral[NEUTRAL_POWER_FOUNTAIN]
  elseif nearest_neutral[NEUTRAL_HEAL_WARD] != null and UnitAlive(nearest_neutral[NEUTRAL_HEAL_WARD]) then
    return nearest_neutral[NEUTRAL_HEAL_WARD]
  elseif nearest_neutral[NEUTRAL_HEALING_FOUNTAIN] != null and UnitAlive(nearest_neutral[NEUTRAL_HEALING_FOUNTAIN]) then
    return nearest_neutral[NEUTRAL_HEALING_FOUNTAIN]
  endif
  return null
endfunction

function HealthFountainJob takes unit u, integer p returns nothing

local real distance = 0
local real unitstrengt = 0
local real x = 0
local real y = 0
local unit fountain = GetHealthFountainUnit()
local integer detectedenemies = 0
local integer enemiesatfountain = 0
local boolean creepsawake = false

  call DisplayToAllJobDebug("HEALTH_FOUNTAIN Start")

  if GetUnitState(u, UNIT_STATE_LIFE) <= 0 then
    call TQAddUnitJob(2, RESET_GUARD_POSITION, p, u)
    set fountain = null
    return
  elseif fountain == null then
    call TQAddUnitJob(2, SEND_HOME, GetRandomInt(0,Max(exist_town_num-1,0)), u)
    return
  endif
  call RemoveGuardPosition(u)
  set x = GetUnitX(fountain)
  set y = GetUnitY(fountain)
  set detectedenemies = GetLocationNonCreepStrength(GetUnitX(u), GetUnitY(u), 1000)
  set enemiesatfountain = GetLocationNonCreepStrength(x, y, 1000)
  set creepsawake = IsCreepsAwake(x, y, 1000)
  set unitstrengt = GetUnitStrength(u)
  set distance = DistanceBetweenUnits(u, fountain)
  if (distance <= 600 and (detectedenemies >= unitstrengt or creepsawake)) or (difficulty == HARD and (enemiesatfountain >= unitstrengt or creepsawake)) then
//    call DisplayToAll("Fountain Return Home")
    call IssuePointOrder(u, "move", GetLocationX(home_location), GetLocationY(home_location))
    call TQAddUnitJob(8, RESET_GUARD_POSITION, p, u)
  elseif GetUnitState(u, UNIT_STATE_LIFE) >= GetUnitState(u, UNIT_STATE_MAX_LIFE) * (I2R(p)/100) then
//    call DisplayToAll("Fountain Healed")
    call TQAddUnitJob(2, RESET_GUARD_POSITION, p, u)
  else
//    call DisplayToAll("Fountain Move to fountain")
    if distance >= buy_distance then
      if detectedenemies > 0 and (GetUnitAbilityLevel(u, 'AIhm') > 0 or GetUnitAbilityLevel(u, 'Ashm') > 0) and IssueImmediateOrder(u, "ambush") then  //no need do anything
      else
        call IssuePointOrder(u, "move", x, y)
      endif
    else
      if GetUnitAbilityLevel(u, 'AIhm') > 0 or GetUnitAbilityLevel(u, 'Ashm') > 0 and IssueImmediateOrder(u, "ambush") then  // order true no need do anything
      elseif not IsUnitType(u, UNIT_TYPE_FLYING) and GetUnitCurrentOrder(u) != OrderId("patrol") then
        call IssuePointOrder( u, "patrol", x + GetRandomReal(-100,100), y + GetRandomReal(-100,100))  //Prevent units from being blocked by other units coming from behind when leaving
      endif
    endif
    call TQAddUnitJob(8, HEALTH_FOUNTAIN, p, u)
  endif
  set fountain = null
endfunction
#ENDIF