#IFDEF GLOBAL
  integer item_exp_state = 0
  unit itemhero = null
  boolean item_expanding = false
  boolean item_job_running = false
  boolean expitem_buy = false
  location item_exp_guard_loc = null
  integer item_exp_attempts = 0
#ELSE

function ItemExpansionJob takes nothing returns nothing
  local unit u = null
  local unit temp = null
  local location loc = null
  local location item_exp_loc = null
  local item exp_item = null
  local integer time_next_try = 5
  if (itemhero != null and GetUnitState(itemhero, UNIT_STATE_LIFE) <= 0) or town_threatened or current_expansion == null then
    call RecycleGuardPosition(itemhero)
    call Trace("Expansion cannot or no Expansion")  // no call ChooseExpansion() , this is passive expansion , avoidance buy repeat item
    set item_job_running = false
    set item_expanding = false
    set expitem_buy = false
    set item_exp_state = 0
    set itemhero = null
    return
  endif
  set exp_time_count = ai_time
  set u = GetExpFoe(u)
  set item_job_running = true
  if item_exp_state == 0 then
    call Trace("Expansion Distance: " + Int2Str(R2I(GetExpansionDistance())))
    call Trace("Expansion state 0: Check Expansion Hero and Location")
    if itemhero == null then
      set exp_item = GetItemOfType(exp_item,race_item_expansion_item_id)
      if exp_item != null then
        set itemhero = GetItemHero(exp_item)
      endif
    endif
    if u == null then
      if itemhero == null and expitem_buy == false then
        if not shop_ordered and GetGold() < gold_price[race_item_expansion_item_id] or GetWood() < wood_price[race_item_expansion_item_id] then
          call Trace("Expansion no gold or wood") // Technically cost already approved and resourced by StartExpansion so unlikely to hit this
          set item_expanding = false
          set item_job_running = false
          set item_exp_state = 0
          set itemhero = null
          return
        endif
        if not shop_ordered and item_exp_attempts < 3 then
          set shop_ordered = true
          set item_exp_attempts = item_exp_attempts + 1
          call TQAddJob(5, BUY_ITEM, race_item_expansion_item_id)
        elseif item_exp_attempts >= 3 then
          call Trace("Item expansion buy failed too many times switch to normal expansion")
          set item_expanding = false
          set item_job_running = false
          set item_exp_state = 0
          return
        endif  
        call TQAddJob(20, ITEM_EXPANSION, 0)
        return
      endif
      call RemoveGuardPosition(itemhero) // Potentially a delay though and item can be lost before this happens
      call SetItemDroppable( GetItemOfTypeOnUnit(race_item_expansion_item_id, itemhero), false )  //prevent droppable
      call SetItemPawnable( GetItemOfTypeOnUnit(race_item_expansion_item_id, itemhero), false )  //can selling
      set expitem_buy = true
      set not_taken_expansion = current_expansion
      set temp = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[racial_expansion], GetUnitX(current_expansion), GetUnitY(current_expansion), 270.0)
      set item_exp_loc = GetUnitLoc(temp)
      call RemoveUnit(temp)
      set temp = null
      //set loc = GetUnitLoc(current_expansion)
      //call RemoveLocation(item_exp_guard_loc)
      //set item_exp_guard_loc = AIGetProjectedLoc(loc, item_exp_loc, 850, 0)  //Guarding position
      //call RemoveLocation(loc)
      //set loc = null
      if DistanceBetweenPoints_kd(item_exp_loc, GetUnitLoc(current_expansion)) > race_max_expa_mine_distance then
          set item_expanding = false
          set item_job_running = false
          set item_exp_state = 0
          set itemhero = null
          call RecycleGuardPosition(itemhero)
          call MarkBadExpansion(current_expansion)
          return
      else
        call UnitUseItemPoint(itemhero, GetItemOfTypeOnUnit(race_item_expansion_item_id, itemhero), GetLocationX(item_exp_loc), GetLocationY(item_exp_loc))
        set time_next_try = 1
        set item_exp_state = 3
      endif
      call RemoveLocation(item_exp_loc)
      set item_exp_loc = null
    endif
  elseif item_exp_state == 3 then
    call Trace("Expansion state 3: Check Use Item")
    if GetItemOfTypeOnUnit(race_item_expansion_item_id, itemhero) != null and not CheckExpansionTaken (current_expansion) then
      set temp = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[racial_expansion], GetUnitX(current_expansion), GetUnitY(current_expansion), 270.0)
      set item_exp_loc = GetUnitLoc(temp)
      call RemoveUnit(temp)
      set temp = null
      call RemoveGuardPosition(itemhero)
      call UnitUseItemPoint(itemhero, GetItemOfTypeOnUnit(race_item_expansion_item_id, itemhero), GetLocationX(item_exp_loc), GetLocationY(item_exp_loc))
      set time_next_try = 1
      call RemoveLocation(item_exp_loc)
      set item_exp_loc = null
    else
      call Trace("Expansion Done")
      call RecycleGuardPosition(itemhero)
      set item_expanding = false
      set item_job_running = false
      set expitem_buy = false
      set item_exp_state = 0
      set itemhero = null
      call SetItemDroppable( GetItemOfTypeOnUnit(race_item_expansion_item_id, itemhero), true )  //prevent droppable
      call SetItemPawnable( GetItemOfTypeOnUnit(race_item_expansion_item_id, itemhero), true )  //can selling
      set u = null
      return
    endif
  endif
  set u = null
  call TQAddJob(time_next_try, ITEM_EXPANSION, 0)
endfunction
#ENDIF
