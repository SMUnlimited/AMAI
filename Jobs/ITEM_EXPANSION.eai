#IFDEF GLOBAL
  integer item_exp_state = 0
  unit itemhero = null
  boolean item_attack_ruard = false
  boolean expitem_buy = false
  location item_exp_guard_loc = Location(0,0)
#ELSE

function UseExpansionItem takes unit u, item t returns location
  local unit tempu = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[racial_expansion], GetUnitX(current_expansion), GetUnitY(current_expansion), 270.0)
  local real x = GetUnitX(tempu)
  local real y = GetUnitY(tempu)
  call RemoveUnit(tempu)
  set tempu = null
  call RemoveGuardPosition(u)
  call UnitUseItemPoint(u, t, x, y)
  return Location(x, y)
endfunction

function ItemExpansionJob takes nothing returns nothing
  local unit u = null
  local unit temp = null
  local location l = null
  local location loc = null
  local item exp_item = null
  local integer time_next_try = 5
  call DisplayToAllJobDebug("ITEM_EXPANSION JOB STARTED")
  if (itemhero != null and GetUnitState(itemhero, UNIT_STATE_LIFE) <= 0) or town_threatened or current_expansion == null then
    call RecycleGuardPosition(itemhero)
    call Trace("Item expansion : Cannot Expansion or no expansion")  // no call ChooseExpansion() , this is passive expansion , avoidance buy repeat item
    set item_expanding = false
    set expitem_buy = false
    set item_exp_state = 0
    set not_taken_expansion = null
    set itemhero = null
    return
  endif
  set exp_time_count = ai_time
  set u = GetExpFoe(u)
  if item_exp_state == 0 then
    call Trace("Item expansion state 0 - Check Expansion Hero and Location , Distance :" + Int2Str(R2I(GetExpansionDistance())))
    set exp_item = GetItemOfType(exp_item,race_item_expansion_item_id)
    set itemhero = GetItemHero(exp_item)
    if (itemhero == null or exp_item == null) and expitem_buy == false then
      set exp_item = null
      set u = null
      if GetGold() < gold_price[race_item_expansion_item_id] + 120 or GetWood() < wood_price[race_item_expansion_item_id] + 80 then
        call Trace("Item expansion state 0 - No gold or wood")
        set item_expanding = false
        return
      endif
      set shop_ordered = true
      call TQAddJob(5, BUY_ITEM, race_item_expansion_item_id)
      call TQAddJob(20, ITEM_EXPANSION, 0)
      return
    endif
    call SetItemDroppable(exp_item, false)  //prevent droppable
    call SetItemPawnable(exp_item, false)  //prevent selling
    set expitem_buy = true
    set not_taken_expansion = current_expansion
    if u == null and (current_expansion == not_taken_expansion or not CheckExpansionTaken(current_expansion)) then
      call CreateDebugTag("Item expansion state 0 - use item", 10, itemhero, 3.00, 1.50)
      set loc = UseExpansionItem(itemhero, exp_item)
      set l = GetUnitLoc(current_expansion)
      call RemoveLocation(item_exp_guard_loc)
      set item_exp_guard_loc = AIGetProjectedLoc(l, loc, 850, 0)  //Guarding position
      set item_attack_ruard = true
      call RemoveLocation(loc)
      set loc = null
      call RemoveLocation(l)
      set l = null
      set item_exp_state = 2
    else
      set item_exp_state = 1
    endif
  elseif item_exp_state == 1 then
    call Trace("Item expansion state 1 - Rush Expansion")
    if u == null then
      set item_exp_state = 2
      set time_next_try = 1
    endif
  elseif item_exp_state == 2 then
    set exp_item = GetItemOfType(exp_item,race_item_expansion_item_id)
    set itemhero = GetItemHero(exp_item)  //prevent hero not item
    if itemhero != null and exp_item != null and (current_expansion == not_taken_expansion or not CheckExpansionTaken(current_expansion)) then
      call CreateDebugTag("Item expansion state 2 - use item", 10, itemhero, 3.00, 1.50)
      set loc = UseExpansionItem(itemhero, exp_item)
      set l = GetUnitLoc(current_expansion)
      call RemoveLocation(item_exp_guard_loc)
      set item_exp_guard_loc = AIGetProjectedLoc(l, loc, 850, 0)  //Guarding position
      set item_attack_ruard = true
      call RemoveLocation(loc)
      set loc = null
      call RemoveLocation(l)
      set l = null
      set time_next_try = 1
    else
      call Trace("Item expansion state 2 - End")
      call RecycleGuardPosition(itemhero)
      set item_expanding = false
      set expitem_buy = false
      set item_exp_state = 0
      set not_taken_expansion = null
      if exp_item != null then
        call Trace("Item expansion state 2 - End , not use item")
        call SetItemDroppable(exp_item, true)  //can droppable
        call SetItemPawnable(exp_item, true)  //can selling
        set exp_item = null
      endif
      set u = null
      return
    endif
  endif
  set exp_item = null
  set u = null
  call TQAddJob(time_next_try, ITEM_EXPANSION, 0)
endfunction
#ENDIF