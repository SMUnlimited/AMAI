#IFDEF GLOBAL
  integer item_exp_state = 0
  integer item_exp_count = 0
  unit itemhero = null
  boolean item_attack_ruard = false
  boolean expitem_buy = false
  location item_exp_guard_loc = Location(0,0)
#ELSE

function ItemExpansionReset takes nothing returns nothing
  if itemhero != null then
    call RecycleGuardPosition(itemhero)
  endif
  set item_expanding = false
  set expitem_buy = false
  set item_exp_state = 0
  set item_exp_count = 0
  set not_taken_expansion = null
  set itemhero = null
endfunction

function UseExpansionItem takes item t returns nothing
  local unit u = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[racial_expansion], GetUnitX(current_expansion), GetUnitY(current_expansion), 270.0)
  local location loc = GetUnitLoc(current_expansion)
  local location l = GetUnitLoc(u)
  local location rl = AIGetProjectedLoc(loc, l, 850, 0)  //Guarding position
  call RemoveUnit(u)
  call UnitUseItemPoint(itemhero, t, GetLocationX(l), GetLocationY(l))
  call MoveLocation(item_exp_guard_loc, GetLocationX(rl), GetLocationY(rl))
  set item_attack_ruard = true
  call RemoveLocation(loc)
  set loc = null
  call RemoveLocation(rl)
  set rl = null
  call RemoveLocation(l)
  set l = null
  set u = null
endfunction

function ItemExpansionJob takes nothing returns nothing
  local unit u = null
  local item exp_item = null
  local integer time_next_try = 5
  call DisplayToAllJobDebug("ITEM_EXPANSION JOB STARTED")
  if (itemhero != null and GetUnitState(itemhero, UNIT_STATE_LIFE) <= 0) or town_threatened or current_expansion == null then
    call Trace("Item expansion : Cannot Expansion or no expansion")  // no call ChooseExpansion() , this is passive expansion , avoidance buy repeat item
    call ItemExpansionReset()
    return
  endif
  set exp_time_count = ai_time
  set u = GetExpFoe(u)
  if item_exp_state == 0 then
    call Trace("Item expansion state 0 - Check Expansion Hero and Location , Distance :" + Int2Str(R2I(GetExpansionDistance())))
    set exp_item = GetItemOfType(exp_item,race_item_expansion_item_id)
    if exp_item == null then
      set u = null
      if expitem_buy == false then
        if shop_ordered or GetGold() < gold_price[race_item_expansion_item_id] + 120 or GetWood() < wood_price[race_item_expansion_item_id] + 80 then
          call Trace("Item expansion state 0 - No gold or wood or buy item job is runing for other item")
          set item_expanding = false
          set item_exp_count = 0
          set not_taken_expansion = null
          return
        endif
        set shop_unit = GetOneOfId(shop_unit, ai_player, old_id[racial_shop])
        if shop_unit != null then
          set shop_wanted = 1
          set shop_distance_limit = raceshop_distance_limit
          set shop_ordered = true 
          set expitem_buy = true
          call TQAddJob(5, BUY_ITEM, race_item_expansion_item_id)
        else
          call Trace("Item expansion state 0 - not get shop , end job")
          set item_expanding = false
          set item_exp_count = 0
          set not_taken_expansion = null
          return
        endif
      elseif item_exp_count > 3 or not shop_ordered then
        call Trace("Item expansion state 0 - but item timeout")
        set item_expanding = false
        set expitem_buy = false
        set item_exp_count = 0
        set not_taken_expansion = null
        return
      else
        set item_exp_count = item_exp_count + 1
      endif
      call TQAddJob(20, ITEM_EXPANSION, 0)
      return
    endif
    set itemhero = GetItemHero(exp_item)
    call SetItemDroppable(exp_item, false)  //prevent droppable
    call SetItemPawnable(exp_item, false)  //prevent selling
    set not_taken_expansion = current_expansion
    set item_exp_count = 0
    set time_next_try = 1
    set exp_item = null
    if u == null then
      call CreateDebugTag("Item expansion state 0 to 2", 10, itemhero, 3.00, 1.50)
      set item_exp_state = 2
    else
      call CreateDebugTag("Item expansion state 0 to 1", 10, itemhero, 3.00, 1.50)
      set item_exp_state = 1
    endif
  elseif item_exp_state == 1 then
    if u == null then
      call Trace("Item expansion state 2 - Rush Expansion done")
      set item_exp_state = 2
      set time_next_try = 1
    elseif item_exp_count > 3 then
      call Trace("Item expansion state 1 - Wait rush end , not rush")
      call ItemExpansionReset()
      set u = null
      return
    else
      call Trace("Item expansion state 1 - Wait rush Expansion")
      set item_exp_count = item_exp_count + 1
      set time_next_try = 40
    endif
  elseif item_exp_state == 2 then
    set exp_item = GetItemOfType(exp_item,race_item_expansion_item_id)
    set itemhero = GetItemHero(exp_item)  //prevent hero not item
    if itemhero != null and exp_item != null and not CheckExpansionTaken(current_expansion) then
      call RemoveGuardPosition(itemhero)
      set time_next_try = 2
      if DistanceBetweenUnits(itemhero, current_expansion) > 800 then
        call CreateDebugTag("Item expansion state 2 - move to expansion", 10, itemhero, 3.00, 1.50)
        call IssuePointOrder(itemhero, "move", GetUnitX(current_expansion), GetUnitY(current_expansion))
      else
        call CreateDebugTag("Item expansion state 2 - use item", 10, itemhero, 3.00, 1.50)
        call UseExpansionItem(exp_item)
      endif
    else
      call Trace("Item expansion state 2 - End")
      call ItemExpansionReset()
      if exp_item != null then
        call Trace("Item expansion state 2 - End , not use item")
        call SetItemDroppable(exp_item, true)  //can droppable
        call SetItemPawnable(exp_item, true)  //can selling
        set exp_item = null
      endif
      set u = null
      return
    endif
  endif
  set exp_item = null
  set u = null
  call TQAddJob(time_next_try, ITEM_EXPANSION, 0)
endfunction
#ENDIF