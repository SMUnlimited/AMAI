#IFDEF GLOBAL

#ELSE
function ItemTypeCheck takes unit u , item it , integer t returns boolean
  local integer i = GetItemTypeId(it)
  local integer c = 0
  local integer h = GetUnitTypeId(u)
  local integer sum = 0
  if it == null or GetItemType(it) == ITEM_TYPE_CHARGED or GetItemType(it) == ITEM_TYPE_POWERUP then
    return false
  endif
  loop
    exitwhen t > 5
    set t = t + 1  // just need check remaining Slot
    set c = GetItemTypeId(UnitItemInSlot(u, t))
    if c == i then
      set sum = sum + 1
    endif
    exitwhen sum > 0  // if sum is 0 , then no repeat item , check same ability item
    if false then
    #INCLUDETABLE <$VER$\ItemCheck.txt> #EFR #COND '%2' ne '0'
    elseif i == '%1' and (c == '%2' or c == '%3') then
      set sum = 1
    #ENDINCLUDE
    endif
    exitwhen sum > 0
  endloop
  if false then  // check hero ability
  #INCLUDETABLE <$VER$\ItemCheck.txt> #EFR #COND '%4' ne '0'
  elseif i == '%1' and (sum > 0 or GetUnitAbilityLevel(u,'%4') > 0 or GetUnitAbilityLevel(u,'%5') > 0 or GetUnitAbilityLevel(u,'%6') > 0 or GetUnitAbilityLevel(u,'%7') > 0 or GetUnitAbilityLevel(u,'%8') > 0 or GetUnitAbilityLevel(u,'%9') > 0) then
    return true
  #ENDINCLUDE
  endif
  return false
endfunction

function CheckItem takes nothing returns nothing
  local integer i = 1
  local integer c = 0
  local integer t = 0
  local group g = CreateGroup()
  local unit u = null
  local unit array tempu
  call GroupEnumUnitsOfPlayer(g, ai_player, null)
  set g = SelectUnittype(g, UNIT_TYPE_HERO, true)  // Prevent hero more than 3 , no use hero_unit[i]
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    if UnitAlive(u) and not IsUnitInGroup(u, unit_buying_merc) and not IsUnitInGroup(u, unit_healing) and GetSlotsFreeOnUnit(u) < 6 then
      set tempu[i] = u
      set i = i + 1
    endif
    call GroupRemoveUnit(g,u)
  endloop
  call DestroyGroup(g)
  set g = null
  set c = i - 1
  set i = 1
  loop
    set u = tempu[i]
    exitwhen u == null
    set t = 0
    loop
      exitwhen t > 5
      if ItemTypeCheck(u,UnitItemInSlot(u,t),t) then
        set tempu[0] = tempu[GetRandomInt(1,c)]
        if tempu[0] != u and GetSlotsFreeOnUnit(tempu[0]) > 0 and DistanceBetweenUnits(tempu[0],u) <= 1600 then
          call IssueTargetOrder(tempu[0], "move", u)  // two hero near
          call UnitDropItemTarget(u,UnitItemInSlot(u,t),tempu[0])
          set t = 5  // once move one item
        endif
      endif
      set t = t + 1
    endloop
    set i = i + 1
  endloop
  loop
    set i = i - 1
    exitwhen i < 0
    set tempu[i] = null
  endloop
endfunction

function ItemRepeatCheck takes nothing returns nothing
  if not town_threatened and not TownThreatened() and not CaptainRetreating() and not CaptainInCombat(true) and not attack_running then
    call CheckItem()  // Prevent the map set unit die will drop item , so no judge CreepsOnMap
  endif
  call TQAddJob(160, ITEM_REPEAT_CHECK, 0)
endfunction
#ENDIF