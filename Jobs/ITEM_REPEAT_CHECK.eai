#IFDEF GLOBAL

#ELSE
function ItemTypeCheck takes unit u , item it , integer t returns boolean
  local integer i = GetItemTypeId(it)
  local integer c = 0
  if it == null or GetItemType(it) == ITEM_TYPE_CHARGED or GetItemType(it) == ITEM_TYPE_POWERUP then
    return false
  endif
  loop
    exitwhen t > 4 or c < 0
    set t = t + 1  // just need check remaining Slot
    set c = GetItemTypeId(UnitItemInSlot(u, t))
    if c != 0 then  // check same item and same ability's item
      if c == i then
        set c = -1
      #INCLUDETABLE <$VER$\ItemCheck.txt> #EFR #COND '%2' ne '0' and '%3' ne '0'
      elseif i == '%1' and ( c == '%2' or c == '%3' ) then
        return true
      #ENDINCLUDE
      #INCLUDETABLE <$VER$\ItemCheck.txt> #EFR #COND '%2' ne '0' and '%3' eq '0'
      elseif i == '%1' and c == '%2' then
        return true
      #ENDINCLUDE
      #INCLUDETABLE <$VER$\ItemCheck.txt> #EFR #COND '%2' eq '0' and '%3' ne '0'
      elseif i == '%1' and c == '%3' then
        return true
      #ENDINCLUDE
      endif
    endif
  endloop
  if false then  // check repeat ability
  #INCLUDETABLE <$VER$\ItemCheck.txt> #EFR #COND '%8' ne '0'
  elseif i == '%1' and (c < 0 or GetUnitAbilityLevel(u,'%4') > 0 or GetUnitAbilityLevel(u,'%5') > 0 or GetUnitAbilityLevel(u,'%6') > 0 or GetUnitAbilityLevel(u,'%7') > 0 or GetUnitAbilityLevel(u,'%8') > 0) then
    return true
  #ENDINCLUDE
  #INCLUDETABLE <$VER$\ItemCheck.txt> #EFR #COND '%7' ne '0' and '%8' eq '0'
  elseif i == '%1' and (c < 0 or GetUnitAbilityLevel(u,'%4') > 0 or GetUnitAbilityLevel(u,'%5') > 0 or GetUnitAbilityLevel(u,'%6') > 0 or GetUnitAbilityLevel(u,'%7') > 0) then
    return true
  #ENDINCLUDE
  #INCLUDETABLE <$VER$\ItemCheck.txt> #EFR #COND '%6' ne '0' and '%7' eq '0'
  elseif i == '%1' and (c < 0 or GetUnitAbilityLevel(u,'%4') > 0 or GetUnitAbilityLevel(u,'%5') > 0 or GetUnitAbilityLevel(u,'%6') > 0) then
    return true
  #ENDINCLUDE
  #INCLUDETABLE <$VER$\ItemCheck.txt> #EFR #COND '%5' ne '0' and '%6' eq '0'
  elseif i == '%1' and (c < 0 or GetUnitAbilityLevel(u,'%4') > 0 or GetUnitAbilityLevel(u,'%5') > 0) then
    return true
  #ENDINCLUDE
  #INCLUDETABLE <$VER$\ItemCheck.txt> #EFR #COND '%4' ne '0' and '%5' eq '0'
  elseif i == '%1' and (c < 0 or GetUnitAbilityLevel(u,'%4') > 0) then
    return true
  #ENDINCLUDE
  elseif c < 0 then
    if false then  // check repeat item
    #INCLUDETABLE <$VER$\ItemCheck.txt> #EFR #COND '%4' eq '0'
    elseif i == '%1' then
      return true
    #ENDINCLUDE
    endif
  endif
  return false
endfunction

function CheckItem takes nothing returns nothing
  local integer i = 1
  local integer c = 0
  local integer t = 0
  local group g = CreateGroup()
  local unit u = null
  local unit array tempu
  call GroupEnumUnitsOfPlayer(g, ai_player, null)
  set g = SelectUnittype(g, UNIT_TYPE_HERO, true)  // Prevent hero more than 3 , no use hero_unit[i]
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    if UnitAlive(u) and not IsUnitBuying(u) and IsStandardUnit(u) then
      set tempu[i] = u  // no attack , no need check IsUnitInvisible
      set i = i + 1
    endif
    call GroupRemoveUnit(g,u)
  endloop
  call DestroyGroup(g)
  set g = null
  if i > 2 then  // if no or just one hero , cannot move item
    set tempu[i] = null  // Prevent cannot exit loop
    set c = i - 1
    set i = 1
    loop
      set u = tempu[i]
      exitwhen u == null
      if GetSlotsFreeOnUnit(u) < 6 then  // have item
        set t = 0
        loop
          exitwhen t >= 5  // check 0~4 include 5
          if ItemTypeCheck(u,UnitItemInSlot(u,t),t) then
            set tempu[0] = tempu[GetRandomInt(1,c)]
            if tempu[0] != u and GetSlotsFreeOnUnit(tempu[0]) > 0 and DistanceBetweenUnits(tempu[0],u) <= 1600 then
              call RemoveGuardPosition(u)
              call RemoveGuardPosition(tempu[0])
              call IssueTargetOrder(tempu[0], "move", u)  // two hero near
              call UnitDropItemTarget(u,UnitItemInSlot(u,t),tempu[0])
              call TQAddUnitJob(10, RESET_GUARD_POSITION, 80, tempu[0])
              call TQAddUnitJob(10, RESET_GUARD_POSITION, 80, u)
              set t = 5  // once move one item
            endif
          endif
          set t = t + 1
        endloop
      endif
      set i = i + 1
    endloop
  endif
  loop
    set i = i - 1
    exitwhen i < 0
    set tempu[i] = null
  endloop
endfunction

function ItemRepeatCheck takes nothing returns nothing
  call DisplayToAllJobDebug("CHECK ITEM JOB STARTED")
  if not town_threatened and not CaptainRetreating() and not CaptainInCombat(true) then
    call CheckItem()  // Prevent the map set unit die will drop item , so no judge CreepsOnMap
  endif
  if not pathing_done then
    call TQAddJob(160, ITEM_REPEAT_CHECK, 0)
  endif
endfunction
#ENDIF