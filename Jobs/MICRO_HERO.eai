#IFDEF GLOBAL
  constant integer ACTION_DO_NOTHING = 0
  constant integer ACTION_GO_HOME = 1
  constant integer ACTION_FOUNTAIN = 2
  constant integer ACTION_TP = 3
  constant integer ACTION_ZEPPELIN_HOME = 4
  constant integer ACTION_ZEPPELIN_FOUNTAIN = 5
  constant integer ACTION_HEALER = 6
  constant integer ACTION_MANA_FOUNTAIN = 7
  constant integer ACTION_MOONWELLS = 8

  location array hero_loc
  location array hero_dir
  real array hero_hp
  real array hero_hp_loss
  real array hero_enemy_density
  location array hero_enemy_loc
  real array hero_ally_density
  location array hero_ally_loc
  boolean array hero_singetping

#ELSE


//==========================================================================
// (AMAI)	GetDensities
//==========================================================================
function GetDensities takes location l, player p, real r returns nothing
  local group g = CreateGroup()
  local unit u = null
  local real ur = 0
  local real xe = 0
  local real ye = 0
  local real xa = 0
  local real ya = 0
  local location ul = null
  local location loc = null
  local player up = null
  local integer i = 0
  set enemy_density = 0
  set ally_density = 0
  call GroupEnumUnitsInRangeOfLoc(g, l, r, null)
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    if (ModuloInteger(i, 200) == 0) then
      call TQSleep(0.1)
    endif
    if UnitAlive(u) and not IsUnitHidden(u) then
      set ul = GetUnitLoc(u)
      set ur = 1 - (DistanceBetweenPoints(ul, l) / r)
      set loc = GetMultipleLoc(ul, ur)
      //if ur == 1 then
      set up = GetOwningPlayer(u)
      if IsPlayerEnemy(p, up) then
        set enemy_density = enemy_density + ur
        set xe = xe + GetLocationX(loc)  // Prevent loc exceeding map range
        set ye = ye + GetLocationY(loc)
        //set enemy_density_loc = GetSumLoc_dd(enemy_density_loc, GetMultipleLoc(ul, ur))
      elseif IsPlayerAlly(p, up) then
        set ally_density = ally_density + ur
        set xa = xa + GetLocationX(loc)
        set ya = ya + GetLocationY(loc)
        //set ally_density_loc = GetSumLoc_dd(ally_density_loc, GetMultipleLoc(ul, ur))
      endif
      call RemoveLocation(loc)
      call RemoveLocation(ul)
    endif
    call GroupRemoveUnit(g, u)
    set i = i + 1
  endloop

  if ally_density > 0 then
    set xa = xa / ally_density
    set ya = ya / ally_density
    //set ally_density_loc = GetDivisionLoc_d(ally_density_loc, ally_density)
  else
    set xa = 0
    set ya = 0
  endif
  call RemoveLocation(ally_density_loc)
  set ally_density_loc = Location(xa,ya)
  if enemy_density > 0 then
    set xe = xe / enemy_density
    set ye = ye / enemy_density
    //set enemy_density_loc = GetDivisionLoc_d(enemy_density_loc, enemy_density)
  else
    set xe = 0
    set ye = 0
  endif
  call RemoveLocation(enemy_density_loc)
  set enemy_density_loc = Location(xe,ye)
  call DestroyGroup(g)
  set g = null
  set ul = null
  set loc = null
  set up = null
endfunction

function TeleportHome takes integer hn returns nothing
// Team Game improvements Added by Strategy Master 12/03/05
  local integer i = 0
  local player p = null
  local real distance = 9999999
  local location teleport_loc = null
  //local real dx = 0
  //local real dy = 0
  local real actual_diff = 0
  local location start_loc = null
  //set hero_loc = GetUnitLoc(hero_unit[hn])
  if c_ally_total > 0 and major_hero != null and UnitAlive(major_hero) then
    loop
      exitwhen i >= GetBJMaxPlayers()
      set p = Player(i)
      if GetPlayerSlotState(p) == PLAYER_SLOT_STATE_PLAYING and IsPlayerAlly(p, ai_player) then
        set start_loc = GetPlayerStartLocationLoc(p)
        //set dx = GetLocationX(start_loc) - GetLocationX(hero_loc)
        //set dy = GetLocationY(start_loc) - GetLocationY(hero_loc)
        //set actual_diff = SquareRoot(dx * dx + dy * dy)
        set actual_diff = DistanceBetweenPoints(start_loc, hero_loc[hn])
        if actual_diff < distance then
          set distance = actual_diff
          if teleport_loc != null then
            call RemoveLocation(teleport_loc)
          endif
          set teleport_loc = Location(GetLocationX(start_loc),GetLocationY(start_loc))
        endif
        call RemoveLocation(start_loc)
      endif
      set i = i + 1
    endloop
  endif
  call ClearCaptainTargets()
  if teleport_loc != null then
    call CaptainAttack(GetLocationX(teleport_loc), GetLocationY(teleport_loc))
  else
    call CaptainGoHome()
    set teleport_loc = Location(GetLocationX(home_location),GetLocationY(home_location))
  endif
  call UnitUseItemPoint(hero_unit[hn], GetItemOfTypeOnUnit(tp_item, hero_unit[hn]), GetLocationX(teleport_loc) + GetRandomInt(500,700) * ISign(), GetLocationY(teleport_loc) + GetRandomInt(500,700) * ISign())
  set break_attack = attack_running
  set attack_running = false
  set teleporting = true
  if teleportloc != null then
    call RemoveLocation(teleportloc)
  endif
  set teleportloc = Location(GetLocationX(teleport_loc),GetLocationY(teleport_loc))  //  set new teleport job move loc
  call RemoveLocation(teleport_loc)
  set teleport_loc = null
  call TQAddUnitJob(0.5, TELEPORT, 0, hero_unit[hn])
  set start_loc = null
  set p = null
  //if hero_loc != null then
  //call RemoveLocation( hero_loc )
  //set hero_loc = null
  //endif
endfunction

function GetTeleportLandPoint takes location l returns location
  local group g = CreateGroup()
  local unit u = null
  local real x = GetLocationX(l)
  local real y = GetLocationY(l)
  call GroupEnumUnitsInRangeOfLoc(g, l, 900, null)
  if teleportloc != null then
    call RemoveLocation(teleportloc)  // set new loc , cannot return teleportloc
  endif
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null  //Find a enemy unit , land to unit loc , prevent army land to hall , cannot expand
    if IsPlayerEnemy(ai_player,GetOwningPlayer(u)) and not IsUnitType(u, UNIT_TYPE_FLYING) and not IsUnitInvisible(u, ai_player) and UnitAlive(u) and not IsUnitHidden(u) then
      set teleportloc = GetUnitLoc(u)
      call DestroyGroup(g)
      set g = null
      set u = null
      return GetUnitLoc(u)
    endif
    call GroupRemoveUnit(g, u)
  endloop
  set teleportloc = Location(x,y)
  call DestroyGroup(g)
  set g = null
  return Location(x,y)
endfunction

function TeleportToLoc takes unit u, location l returns nothing
  local location loc = GetTeleportLandPoint(l)
  set break_attack = attack_running
  set attack_running = false
  set teleporting = true
  call ClearCaptainTargets()
  call CaptainAttack(GetLocationX(loc), GetLocationY(loc))
  call UnitUseItemPoint(u, GetItemOfTypeOnUnit(tp_item, u), GetLocationX(loc), GetLocationY(loc))
  call TQAddUnitJob(0.5, TELEPORT, 0, u)
  call RemoveLocation(loc)
  set loc = null
endfunction

function ItemSingetp takes integer hn, boolean retreat returns boolean
  local boolean b = false
  local unit u = null
  local location l = null
  local group g = null
  local item t = null
  if GetUnitAbilityLevel(hero_unit[hn], 'AImt') > 0 then  // check life preventing control skills
    set t = GetItemOfTypeOnUnit(singe_tp_item,hero_unit[hn])
    if t != null then
      if retreat == false then  // teleport to battle , hope don't interrupt casting
        set l = GetUnitLoc(major_hero)
      elseif GetUnitState(hero_unit[hn], UNIT_STATE_LIFE) / GetUnitState(hero_unit[hn], UNIT_STATE_MAX_LIFE) >= 0.4 then
        set l = Location(GetLocationX(home_location), GetLocationY(home_location))
      endif
      if l != null then
        set g = CreateGroup()
        call GroupEnumUnitsInRangeOfLoc(g, l, 300, null)
        set g = SelectByPlayer(g, ai_player, true)
        loop
          set u = FirstOfGroup(g)
          exitwhen u == null or (not IsUnitLoaded(u) and not IsUnitType(u, UNIT_TYPE_PEON) and not IsUnitType(u, UNIT_TYPE_FLYING) and not IsUnitType(u, UNIT_TYPE_HERO) and not UnitInvis(u) and GetUnitState(u, UNIT_STATE_MAX_LIFE) > 0 and GetUnitState(u, UNIT_STATE_LIFE) / GetUnitState(u, UNIT_STATE_MAX_LIFE) >= 0.5 and not IsUnitHidden(u))
          call GroupRemoveUnit(g, u)
        endloop
        call DestroyGroup(g)
        set g = null
        if u != null then
          call CreateDebugTag("use singe tp item", 10, hero_unit[hn], 3.00, 1.50)
          call RemoveGuardPosition(hero_unit[hn])
          set b = UnitUseItemTarget(hero_unit[hn],t, u)
          set u = null
          set hero_singetping[hn] = true
        endif
        call RemoveLocation(l)
        set l = null
      endif
      set t = null
    endif
  endif
  return b
endfunction

// function ItemSaveHero takes integer hn returns boolean
//   local integer i = 1  // only supports three heroes
//   local integer id = 0
//   local item t = null
//   loop
//     exitwhen i > 3
//     if i != hn and CheckUseSaveItem(i) and DistanceBetweenUnits(hero_unit[i],hero_unit[hn]) < 700 then
//       set id = GetUnitCurrentOrder(hero_unit[i])
//       if id == 0 or id == OrderId("attack") or id == OrderId("stop") or id == OrderId("move") then
//         if GetUnitAbilityLevel(hero_unit[i], 'ANsa') > 0 and GetPlayerStructureCount(ai_player, true) > 0 then
//           set t = GetItemOfTypeOnUnit(sanctuary_item,hero_unit[i])
//           if t != null and UnitUseItemTarget(hero_unit[i],t,hero_unit[hn]) then
//             call CreateDebugTag("sanctuary item save", 10, hero_unit[hn], 3.00, 1.50)
//             set t = null
//             return true
//           endif
//         elseif GetUnitAbilityLevel(hero_unit[i], 'ANpr') > 0 and TownCountDone(racial_hall[1]) > 0 then
//           set t = GetItemOfTypeOnUnit(preservation_item,hero_unit[i])
//           if t != null and UnitUseItemTarget(hero_unit[i],t,hero_unit[hn]) then
//             call CreateDebugTag("preservation item save", 10, hero_unit[hn], 3.00, 1.50)
//             set t = null
//             return true
//           endif
//         endif
//       endif
//     endif
//     set i = i + 1
//   endloop
//   set t = null
//   return ItemSingetp(hn,true)
// endfunction

function ExecuteSaveHero takes integer hn, integer a, unit healer returns nothing
  local location l = null
  local group g = null
  local location unitloc = null
  local location loc = null
  local integer hFountainID = GetHealthFountainID()
  local integer mFountainID = GetManaFountainID()

  if a != ACTION_DO_NOTHING and a != ACTION_TP then
    if GetUnitAbilityLevel(hero_unit[hn], 'AOwk') > 0 or GetUnitAbilityLevel(hero_unit[hn], 'ANwk') > 0 then
      if not IsUnitInvisible(hero_unit[hn], Player(PLAYER_NEUTRAL_AGGRESSIVE)) then
        call IssueImmediateOrder(hero_unit[hn], "windwalk")
      endif
    endif
    if a != ACTION_HEALER and hero_unit[hn] == major_hero then
      set major_hero = null
    endif
  endif
  if a == ACTION_HEALER and healer != null and GetUnitState(hero_unit[hn], UNIT_STATE_LIFE) >= 0.40 * GetUnitState(hero_unit[hn], UNIT_STATE_MAX_LIFE) then
    call GroupAddUnit(unit_healing, hero_unit[hn])
    call CreateDebugTag("HERO: Going to Healer", 10, hero_unit[hn], 4.00, 2.00)
    set unitloc = GetUnitLoc(healer)
    call RemoveGuardPosition(hero_unit[hn])
    if GetLocationX(last_enemy_loc) != 0 and GetLocationY(last_enemy_loc) != 0 then
      set loc = GetSubtractionLoc(unitloc, last_enemy_loc)
      set l = GetProjectedLoc(unitloc, loc, 350)
      call TQAddUnitJob(GetTimeToReachLoc(hero_unit[hn], l) + 1, RESET_GUARD_POSITION, 0, hero_unit[hn])
      call IssuePointOrderLoc(hero_unit[hn], "move", l)
      call RemoveLocation(l)
      set l = null
      call RemoveLocation(loc)
      set loc = null
    else
      call TQAddUnitJob(GetTimeToReachLoc(hero_unit[hn], unitloc) + 1, RESET_GUARD_POSITION, 0, hero_unit[hn])
      call IssuePointOrderLoc(hero_unit[hn], "move", unitloc)
    endif
    call RemoveLocation(unitloc)
    set unitloc = null
    call HealUnit(healer, hero_unit[hn], false)
    return
  endif
  // if a != ACTION_TP and not teleporting and not town_threatened and not UnitInvis(hero_unit[hn]) and ItemSaveHero(hn) then  //No mandatory control required
  //   call GroupAddUnit(unit_healing,hero_unit[hn])
  //   if a == ACTION_MOONWELLS then
  //     call CreateDebugTag("HERO: Item Save Going to Moonwells", 10, hero_unit[hn], 4.00, 2.00)
  //     call TQAddUnitJob(0, MOON_WELL_CONTROL, 80, hero_unit[hn])
  //   else
  //     call RemoveGuardPosition(hero_unit[hn])
  //     call TQAddUnitJob(16, RESET_HEALTH, 80, hero_unit[hn])
  //   endif
  //   return
  // endif
  if a == ACTION_FOUNTAIN then
    call CreateDebugTag("HERO: Going to Fountain", 10, hero_unit[hn], 4.00, 2.00)
    call GroupAddUnit(unit_healing, hero_unit[hn])
    call TQAddUnitJob(0, HEALTH_FOUNTAIN, 80, hero_unit[hn])
    call IssuePointOrder(hero_unit[hn], "move", GetUnitX(nearest_neutral[hFountainID]), GetUnitY(nearest_neutral[hFountainID]))
  elseif a == ACTION_MANA_FOUNTAIN then
    call CreateDebugTag("HERO: Going to Mana Fountain", 10, hero_unit[hn], 4.00, 2.00)
    call GroupAddUnit(unit_healing, hero_unit[hn])
    call TQAddUnitJob(0, MANA_FOUNTAIN, 80, hero_unit[hn])
    call IssuePointOrder(hero_unit[hn], "move", GetUnitX(nearest_neutral[mFountainID]), GetUnitY(nearest_neutral[mFountainID]))
  elseif a == ACTION_GO_HOME then
    //call UnitGoHome(hero_unit[hn])
    call CreateDebugTag("HERO: Going Home", 10, hero_unit[hn], 4.00, 2.00)
    call GroupAddUnit(unit_healing, hero_unit[hn])
    call TQAddUnitJob(0, SEND_HOME, 0, hero_unit[hn])
  elseif a == ACTION_ZEPPELIN_HOME then
    call CreateDebugTag("HERO: Zeppelin Home", 10, hero_unit[hn], 4.00, 2.00)
    call GroupAddUnit(unit_rescueing, follow_zeppelin)
    set g = CreateGroup()
    call GroupAddUnit(g, follow_zeppelin)
    call GroupAddUnit(unit_healing, hero_unit[hn])
    call TQAddGroupJob(0, ZEPPELIN_MOVE, ZTARGET_HOME, hero_unit[hn], g)
    set g = null
  elseif a == ACTION_ZEPPELIN_FOUNTAIN then
    call CreateDebugTag("HERO: Zepplin to fountain", 10, hero_unit[hn], 4.00, 2.00)
    call GroupAddUnit(unit_rescueing, follow_zeppelin)
    set g = CreateGroup()
    call GroupAddUnit(g, follow_zeppelin)
    call GroupAddUnit(unit_healing, hero_unit[hn])
    call TQAddGroupJob(0, ZEPPELIN_MOVE, ZTARGET_FOUNTAIN, hero_unit[hn], g)
    set g = null
  elseif a == ACTION_TP then
    call CreateDebugTag("Hero: Town Portalling", 10, hero_unit[hn], 4.00, 2.00)
    call TeleportHome(hn)
  elseif a == ACTION_MOONWELLS then
    call CreateDebugTag("HERO: Going to Moonwells", 10, hero_unit[hn], 4.00, 2.00)
    call GroupAddUnit(unit_healing,hero_unit[hn])
    call TQAddUnitJob(0, MOON_WELL_CONTROL, 80, hero_unit[hn])
  endif
endfunction

function SaveHero takes integer hn , integer armyOfHero returns nothing
  local real daytime = GetFloatGameState(GAME_STATE_TIME_OF_DAY)
  local unit healer = null
  local integer hFountainID = GetHealthFountainID()
  local integer mFountainID = GetManaFountainID()
  local location l = GetUnitLoc(hero_unit[hn])
  set healer = GetHealer(healer)
  call ActionListInit(9)
#INCLUDE <HeroFleeRules.ai>
  call ExecuteSaveHero(hn, ActionListEval(), healer)
  call RemoveLocation(l)
  set l = null
  set healer = null
endfunction

function HeroBugFixHealthCheck takes integer hn returns boolean
  return GetUnitState(hero_unit[hn], UNIT_STATE_LIFE) >= 0.80 * GetUnitState(hero_unit[hn], UNIT_STATE_MAX_LIFE) and IsUnitInGroup(hero_unit[hn], unit_healing)
endfunction

function MicroHeroJob takes integer hn returns nothing
  local real new_hp = 0.0
  local real teleportdistance = 0.0
  local integer current_order = 0
  local integer armyOfHero = 0
  local boolean healing = false
  call DisplayToAllJobDebug("MICRO_HERO Job start")
  if UnitAlive(hero_unit[hn]) then
    set armyOfHero = GetArmyOfUnit(hero_unit[hn])
    // A Temporary Bug fix
    if HeroBugFixHealthCheck(hn) then
      call CreateDebugTag("HERO: Healing Bug Fix Job", 10, hero_unit[hn], 4.00, 2.00)
      call RecycleGuardPosition(hero_unit[hn])
      call GroupRemoveUnit(unit_healing, hero_unit[hn])
      if not retreat_controlled and not town_threatened and not teleporting and major_hero != null and UnitAlive(major_hero) and DistanceBetweenUnits(major_hero,hero_unit[hn]) > 2000 then
        call ItemSingetp(hn,false)
      endif
    else
      set healing = IsUnitInGroup(hero_unit[hn], unit_healing)
    endif
    set new_hp = GetUnitState(hero_unit[hn], UNIT_STATE_LIFE) / GetUnitState(hero_unit[hn], UNIT_STATE_MAX_LIFE)
    set current_order = GetUnitCurrentOrder(hero_unit[hn])

    //  call DisplayToAll("MicroH Start")
    call RemoveLocation(hero_dir[hn])
    //set hero_dir[hn] = GetSubtractionLoc_dd(GetUnitLoc(hero_unit[hn]), hero_loc[hn])
    set hero_dir[hn] = hero_loc[hn]
    set hero_loc[hn] = GetUnitLoc(hero_unit[hn])
    set hero_hp_loss[hn] = hero_hp[hn] - new_hp
    set hero_hp[hn] = new_hp
    call GetDensities(hero_loc[hn], ai_player, hero_radius)
    set hero_enemy_density[hn] = enemy_density
    call MoveLocation(hero_enemy_loc[hn], GetLocationX(enemy_density_loc), GetLocationY(enemy_density_loc))
    set hero_ally_density[hn] = ally_density
    call MoveLocation(hero_ally_loc[hn], GetLocationX(ally_density_loc), GetLocationY(ally_density_loc))
    if not teleporting and ((current_order >= 852008 and current_order <= 852013) or current_order == 852093) then
      if GetItemTypeId(UnitItemInSlot(hero_unit[hn], current_order - 852008)) == old_id[tp_item] or current_order == 852093 then
        if current_order == 852093 and teleportloc != null then
          call RemoveLocation(teleportloc)
          set teleportloc = null
        endif
        set teleporting = true
        call TQAddUnitJob(0.5, TELEPORT, 0, hero_unit[hn])
      endif
    endif

    //  call DisplayToAll("Hero Order: "+Int2Str(GetUnitCurrentOrder(hero_unit[hn])))

    //  call DisplayToAll(Int2Str(R2I(hero_enemy_density[hn] * 10)))
    //  call DisplayToAll(Int2Str(R2I(DistanceBetweenPoints(hero_enemy_loc[hn],GetUnitLoc(hero_unit[hn])))))

    if not teleporting and GetUnitState(hero_unit[hn], UNIT_STATE_LIFE) < RMax(flee_health_percentage * GetUnitState(hero_unit[hn], UNIT_STATE_MAX_LIFE), flee_minimum_health) and healing == false then
      call SaveHero(hn,armyOfHero)
      set healing = IsUnitInGroup(hero_unit[hn], unit_healing)
    endif
    if hero_singetping[hn] then
      call TQAddJob(2.5, MICRO_HERO, hn)
      set hero_singetping[hn] = false
      return
    endif

    if armyOfHero >= 0 and not teleporting and GetItemNumberOnUnit(tp_item, hero_unit[hn]) > 0 then
      if most_threatened_town >= 0 then
        set teleportdistance = DistanceBetweenPoints(hero_loc[hn], town_loc[most_threatened_town])
      endif
      if town_threatened and town_threat[most_threatened_town] >= teleport_min_threat and teleportdistance > LinearInterpolation(teleport_low_threat, teleport_high_threat, teleport_low_threat_distance, teleport_high_threat_distance, town_threat[most_threatened_town]) and town_can_tp[most_threatened_town] and army_strength[armyOfHero] >= teleport_army_min_strength then
        if LinearInterpolation(ver_low_aggression,ver_high_aggression,ver_flee_multiple1,ver_flee_multiple2,attacking_aggression)*own_strength > army_strength[town_threat_army[most_threatened_town]] * 0.75 then
          //call PingMinimap(GetLocationX(hero_loc[hn]), GetLocationY(hero_loc[hn]), 10.00)
          //call PingMinimap(GetLocationX(town_loc[most_threatened_town]), GetLocationY(town_loc[most_threatened_town]), 10.00)
          //call DisplayToAll("Protection Town Portal: " + Int2Str(R2I(teleportdistance)))
          call CreateDebugTag("HERO: Teleporting to threatened town", 10, hero_unit[hn], 4.00, 2.00)
          call TeleportToLoc(hero_unit[hn], town_loc[most_threatened_town])
          call TQAddJob(1, MICRO_HERO, hn)
          return
        elseif DistanceBetweenPoints(hero_loc[hn], home_location) > 2500 then
          call CreateDebugTag("HERO: Teleporting home town too threatened", 10, hero_unit[hn], 4.00, 2.00)
          call TeleportHome(hn)
          call TQAddJob(1, MICRO_HERO, hn)
          return
        endif
      // Next statement detects when the main army is outmatched and so teleports back to home base
      elseif attack_running and main_army >= 0 and army_loc[main_army] != null and IsUnitInRangeXY(hero_unit[hn],GetLocationX(army_loc[main_army]), GetLocationY(army_loc[main_army]), battle_radius) and army_strength[armyOfHero] >= teleport_army_min_strength and CaptainRetreating() and isfleeing and DistanceBetweenPoints(hero_loc[hn], home_location) > teleport_low_threat_distance then
        call CreateDebugTag("HERO: Army outmatched-teleporting home", 10, hero_unit[hn], 4.00, 2.00)
        call TeleportHome(hn)
        call TQAddJob(1, MICRO_HERO, hn)
        return
      endif
    endif
    if not teleporting and hero_enemy_density[hn] > 5 and healing and DistanceBetweenPoints(hero_dir[hn],hero_loc[hn]) <= 225 then
      call CreateDebugTag("HERO: Surround Detection Teleportation", 10, hero_unit[hn], 4.00, 2.00)
      call TeleportHome(hn)
    endif
  endif
  call TQAddJob(1 * sleep_multiplier, MICRO_HERO, hn)
  //call DisplayToAll("MICRO_HERO END")
endfunction
#ENDIF