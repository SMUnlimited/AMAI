#IFDEF GLOBAL

boolean militia_expanding = false
//boolean militia = true
boolean militia_exp_attack = false
integer militia_time_out = 0
integer militia_state = 0
unit militia_builder = null
//unit militia_fastb1 = null
//unit militia_fastb2 = null
//unit militia_fastb3 = null
//unit militia_expansion = null
//location militia_loc = null

#ELSE

function BuildMilitiaExpansion takes nothing returns boolean
  local unit u = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[racial_expansion], GetUnitX(not_taken_expansion), GetUnitY(not_taken_expansion), 270.0)
  local real x = GetUnitX(u)
  local real y = GetUnitY(u)
  local boolean b = false
  call RemoveUnit(u)
  set u = null
  if DistanceBetweenPoints_dd(Location(x, y), GetUnitLoc(not_taken_expansion)) <= race_max_expa_mine_distance then
    call CreateDebugTag("Militia Expansion - build", 10, militia_builder, 3.00, 1.50)
    call IssuePointOrderById(militia_builder, old_id[racial_expansion], x, y)
    call TQAddUnitJob(200, RESET_GUARD_POSITION_ONLY, 80, militia_builder)
    if race_has_power_build then
      call FastBuildCheck()
    endif
    set b = true
  else
    call RecycleGuardPosition(militia_builder)
  endif
  return b
endfunction

function MilitiaExpansionReset takes nothing returns nothing
	if militia_state < 3 then
		set gold_buffer = 0
		set wood_buffer = 0
	endif
	set militia_time_out = 0
	set militia_state = 0
	set militia_expanding = false
	set not_taken_expansion = null
	set militia_builder = null
endfunction

function TryMilitiaExpansionFoot takes nothing returns nothing
local group g = null
//local unit u = null
local integer ownstrength = 0
local integer expansionstrength = 0
local location l = null

	call DisplayToAllJobDebug("MILITIA_EXPAND JOB START")

	if not_taken_expansion == null or towerrush or GetMinesOwned() >= 2 or (not militia_expanding and not_taken_expansion != null) then  // have mine , peon need harvest
		call MilitiaExpansionReset()
		call Trace("MILITIA_EXPAND : not take expansion")
		return
	endif
	set ownstrength = GetOwnStrength()
	if militia_state < 3 then
		set gold_buffer = GetUnitGoldCost2(racial_expansion)
		set wood_buffer = GetUnitWoodCost2(racial_expansion)
	endif
	if militia_state == 0 then
		call Trace("Expansion Distance: " + Int2Str(R2I(GetExpansionDistance())))
		set expansionstrength = GetLocationCreepStrength(GetUnitX(not_taken_expansion), GetUnitY(not_taken_expansion), expansion_radius)
		if GetExpansionDistance() <= 5800 and expansionstrength < 20 then
			call Trace("Our Strength:" + Int2Str(ownstrength) + " Expansion Strength:" + Int2Str(expansionstrength))
			if TownCountDone(hero[1]) + TownCountDone(hero[2]) + TownCountDone(hero[3]) > 0 and not town_threatened and ownstrength >= minimum_attack_strength and TownCountDone(racial_peon) > 6 and ownstrength + 6 >= expansionstrength then
				if GetGold() >= gold_buffer and GetWood() >= wood_buffer then
					if expansionstrength <= 0 then
						set militia_state = 2
						call Trace("Militia state 0 to 2")
					else
						call Trace("Militia state 0 to 1")
						set militia_state = 1
						set militia_exp_attack = true
					endif
					set militia_expanding = true
					call TQAddJob(1, MILITIA_EXPAND, 0)
					return
				endif
			endif
		endif
		call MilitiaExpansionReset()
		call Trace("Militia state 0 Ended , expansion too far or own too weak")
	elseif militia_state == 1 then
		call Trace("Militia state 1 rush expansion")
		set expansionstrength = GetLocationCreepStrength(GetUnitX(not_taken_expansion), GetUnitY(not_taken_expansion), expansion_radius)
		if ownstrength + 6 < expansionstrength then
			call MilitiaExpansionReset()
			call Trace("Militia state 1 creep too powerful or cannot rush , stop trying to expand")
		elseif expansionstrength <= 0 then
			call Trace("Militia state 1 to 2")
			set militia_state = 2
			call TQAddJob(1, MILITIA_EXPAND, 0)
			return
		else
			call Trace("Militia state 1 wait rush creep")
			call TQAddJob(12, MILITIA_EXPAND, 0)
			return
		endif
	elseif militia_state == 2 then
		call Trace("Militia state 2 get militia")
		set g = CreateGroup()
		call GroupEnumUnitsOfPlayer(g, ai_player,null)
		set g = SelectNumberOfId(g, 50, old_id[racial_militia])
		set g = SelectByHidden(g, false)
		set g = SelectByAlive(g, true)
		if FirstOfGroup(g) == null then
			call GroupClear(g)
			call GroupEnumUnitsOfPlayer(g, ai_player, null)
			set g = SelectNumberOfId(g, 50, old_id[racial_peon])
			set g = SelectByHidden(g, false)
			set g = SelectByAlive(g, true)
		endif
		set l = GetUnitLoc(not_taken_expansion)
		set g = GetNearestSubGroupOfGroup(g, l, 4)
		call RemoveLocation(l)
		set l = null
		set militia_builder = FirstOfGroup(g)
		//call GroupRemoveUnit(g, militia_builder)
		//set militia_fastb1 = FirstOfGroup(g)
		//call GroupRemoveUnit(g, militia_fastb1)
		//set militia_fastb2 = FirstOfGroup(g)
		//call GroupRemoveUnit(g, militia_fastb2)
		//set militia_fastb3 = FirstOfGroup(g)
		call DestroyGroup(g)
		set g = null
		if militia_builder != null then
			call CreateDebugTag("Militia Expansion - take peon", 10, militia_builder, 3.00, 1.50)
			set militia_state = 3
			call Trace("Militia state 2 to 3")
			call TQAddJob(1, MILITIA_EXPAND, 0)
			return
		elseif militia_time_out < 1 then
			call Trace("Militia state 2 wait get peon")
			set militia_time_out = militia_time_out + 1
			call TQAddJob(8, MILITIA_EXPAND, 0)
			return
		endif
		call MilitiaExpansionReset()
		call Trace("Militia state 2 Ended , not peon")
	elseif militia_state == 3 then
		if militia_builder == null or GetUnitState(militia_builder, UNIT_STATE_LIFE) <= 0 or CheckExpansionTaken(not_taken_expansion) or GetGold() < GetUnitGoldCost2(racial_expansion) or GetWood() < GetUnitWoodCost2(racial_expansion) then
			call Trace("Militia state 3 Ended , lost peon or expansion by other player take or no gold or no wood")
		else
			call RemoveGuardPosition(militia_builder)  // has to be here or peasant comes off construction
			if DistanceBetweenUnits(militia_builder, not_taken_expansion) < 800 then
				//call IssueTargetOrder(militia_fastb1, "stop", not_taken_expansion)
				//call IssueTargetOrder(militia_fastb2, "stop", not_taken_expansion)
				if BuildMilitiaExpansion() then
					call Trace("Militia state 3 , Build Success")
				else
					call Trace("Militia state 3 , Build Fail")
					call CreateDebugTag("Militia Expansion -  Build Fail", 10, militia_builder, 3.00, 1.50)
				endif
			else
				call Trace("Militia state 3 , Moving expansion")
				call CreateDebugTag("Militia Expansion - peon move to loc", 10, militia_builder, 3.00, 1.50)
				call IssueTargetOrder(militia_builder, "move", not_taken_expansion)
				call TQAddJob(2, MILITIA_EXPAND, 0)
				return
			endif
		endif
		call MilitiaExpansionReset()
	endif
endfunction

#ENDIF