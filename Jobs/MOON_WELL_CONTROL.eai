#IFDEF GLOBAL
  unit nearest_moon_well = null

#ELSE

function GetMoonWell takes unit u, player p, integer id returns unit
  local group g = CreateGroup()
  call GroupEnumUnitsOfPlayer(g, p, null)
  set g = SelectByAlive(g, true)
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    if GetUnitTypeId(u) == id and GetUnitState(u, UNIT_STATE_MANA) >= 30 then
      call DestroyGroup(g)
      set g = null
      return u
    endif
    call GroupRemoveUnit(g, u)
  endloop
  call DestroyGroup(g)
  set g = null
  return u

endfunction

function HeadtoMoonWell takes unit u, integer p returns nothing
  local real dist = 0
  local real enemystrength = 0
  set dist = DistanceBetweenUnits(u, nearest_moon_well)
  set enemystrength = GetLocationNonCreepStrength(GetUnitX(u), GetUnitY(u), 800)
  if dist >= 1000 and enemystrength > 0 and CheckBlinkAbility(u) then
    call IssuePointOrder(u, "blink", GetUnitX(nearest_moon_well), GetUnitY(nearest_moon_well))
    call IssuePointOrder(u, "smart", GetUnitX(nearest_moon_well), GetUnitY(nearest_moon_well))
  elseif dist >= 1000 and GetUnitAbilityLevel(u, 'Ashm') > 0 and enemystrength > 0 and (GetFloatGameState(GAME_STATE_TIME_OF_DAY) >= 18 or GetFloatGameState(GAME_STATE_TIME_OF_DAY) < 6) and not IsUnitType(u, UNIT_TYPE_HERO) then
    call IssueImmediateOrder(u, "ambush")
  else
    call IssuePointOrder(u, "smart", GetUnitX(nearest_moon_well), GetUnitY(nearest_moon_well))
  endif
  call TQAddUnitJob(4, MOON_WELL_CONTROL, p, u)
endfunction

function MoonWellJob takes unit u, integer p returns nothing
  local real dist = 0
  local real enemystrength = 0

  call DisplayToAllJobDebug("MOON_WELL_CONTROL JOB START")
  call RemoveGuardPosition(u)
  call CreateDebugTag("moon well job", 10, u, 3.00, 1.50)
  if GetUnitState(u, UNIT_STATE_LIFE) <= 0 or u == null then
    if u != null then
      call TQAddUnitJob(2, RESET_GUARD_POSITION, p, u)
    endif
    return
  elseif GetUnitState(u, UNIT_STATE_LIFE) >= GetUnitState(u, UNIT_STATE_MAX_LIFE) * (I2R(p)/100) then  // or IsUnitType(u, UNIT_TYPE_SUMMONED)
    call TQAddUnitJob(2, RESET_GUARD_POSITION, p, u)
    return
  elseif nearest_moon_well == null or not UnitAlive(nearest_moon_well) or GetUnitState(nearest_moon_well, UNIT_STATE_MANA) < 30 then
    set nearest_moon_well = GetMoonWell(nearest_moon_well, ai_player, old_id[racial_farm])
    if nearest_moon_well == null then
      set dist = DistanceBetweenPoints_dk(GetUnitLoc(u), home_location)
      set enemystrength = GetLocationNonCreepStrength(GetUnitX(u), GetUnitY(u), 800)
      if dist >= 1000 and enemystrength > 0 and CheckBlinkAbility(u) then
        call IssuePointOrder(u, "blink", GetLocationX(home_location), GetLocationY(home_location))
        call IssuePointOrder(u, "move", GetLocationX(home_location), GetLocationY(home_location))
        call TQAddUnitJob(2, SEND_HOME, 0, u)  // 0 is home_location
      elseif dist >= 1000 and enemystrength > 0 and GetUnitAbilityLevel(u, 'Ashm') > 0 and (GetFloatGameState(GAME_STATE_TIME_OF_DAY) >= 18 or GetFloatGameState(GAME_STATE_TIME_OF_DAY) < 6) and not IsUnitType(u, UNIT_TYPE_HERO) then
        call IssueImmediateOrder(u, "ambush")
        call TQAddUnitJob(4, MOON_WELL_CONTROL, p, u)
      elseif dist >= 1000 then
        call IssuePointOrder(u, "move", GetLocationX(home_location), GetLocationY(home_location))
        call TQAddUnitJob(4, MOON_WELL_CONTROL, p, u)
      elseif IsUnitType(u,UNIT_TYPE_HERO) then
        call TQAddUnitJob(2, SEND_HOME, 0, u)  // 0 is home_location
      else
        call TQAddUnitJob(2, RESET_GUARD_POSITION, p, u)
      endif
    else
      call HeadtoMoonWell(u, p)
    endif
  else
    call HeadtoMoonWell(u, p)
  endif

endfunction

#ENDIF