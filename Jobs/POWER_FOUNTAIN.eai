#IFDEF GLOBAL
#ELSE
function PowerFountainJob takes unit u, integer p returns nothing

local real distance = 0
local integer nn = NEUTRAL_POWER_FOUNTAIN
local unit fountain = nearest_neutral[nn]
local integer detectedenemies = GetLocationNonCreepStrength(GetUnitX(u), GetUnitY(u), 1000) 
local integer enemiesatfountain = GetLocationNonCreepStrength(GetUnitX(fountain), GetUnitY(fountain), 1000)
local boolean creepsawake = IsCreepsAwake(GetUnitX(fountain), GetUnitY(fountain), 1000)


  call DisplayToAllJobDebug("POWER_FOUNTAIN Start")

  call RemoveGuardPosition(u)
  if GetUnitState(u, UNIT_STATE_LIFE) <= 0 then
    call TQAddUnitJob(2, RESET_GUARD_POSITION, p, u)
	return
  elseif fountain == null then
    call TQAddUnitJob(2, SEND_HOME, p, u)
	return
  elseif (DistanceBetweenUnits(u, fountain) <= 600 and (detectedenemies >= GetUnitStrength(u) or creepsawake)) or (MeleeDifficulty() == MELEE_INSANE and (enemiesatfountain >= GetUnitStrength(u) or creepsawake)) then
//    call DisplayToAll("Fountain Return Home")
    call IssuePointOrder(u, "move", GetLocationX(home_location), GetLocationY(home_location))
	call GroupRemoveUnit(unit_healing, u)	
    call TQAddUnitJob(8, RESET_GUARD_POSITION, p, u)
  elseif GetUnitState(u, UNIT_STATE_LIFE) >= GetUnitState(u, UNIT_STATE_MAX_LIFE) * (I2R(p)/100) or GetUnitState(u, UNIT_STATE_MANA) >= GetUnitState(u, UNIT_STATE_MAX_MANA) * (I2R(p)/100)then
//    call DisplayToAll("Fountain Healed")
    call TQAddUnitJob(2, RESET_GUARD_POSITION, p, u)
	return
  else
//    call DisplayToAll("Fountain Move to fountain")
     set distance = DistanceBetweenUnits(u, fountain)
    if distance >= buy_distance then
	if GetUnitAbilityLevel(u, 'Ashm') > 0 and GetLocationNonCreepStrength(GetUnitX(u), GetUnitY(u), 800) > 0 and (GetFloatGameState(GAME_STATE_TIME_OF_DAY) >= 18 or GetFloatGameState(GAME_STATE_TIME_OF_DAY) <= 6) then
		call IssueImmediateOrder(u, "ambush")
    else
		if GetUnitCurrentOrder(u) != OrderId("move") then
			call IssuePointOrder(u, "move", GetUnitX(fountain), GetUnitY(fountain))
		endif
	endif
    elseif GetUnitAbilityLevel(u, 'Ashm') > 0 then
      call IssueImmediateOrder(u, "ambush")
    endif
    call TQAddUnitJob(8, POWER_FOUNTAIN, p, u)
  endif


endfunction
#ENDIF