#IFDEF GLOBAL
#ELSE

function countEnemyReal takes nothing returns nothing
  local integer i = 0
  local integer p = 0
  loop
    exitwhen i > RACE_NUMBER
    set c_enemy[i] = 0
    set i = i + 1
  endloop

  set i = 0
  loop
    exitwhen i >= c_enemy_total
    if GetPlayerSlotState(enemy_force[i]) == PLAYER_SLOT_STATE_PLAYING then
      set p = GetPlayerId(enemy_force[i])
      set c_enemy[player_race[p]] = c_enemy[player_race[p]] + 1
    endif
    set i = i + 1
  endloop
endfunction

function CheckAllianceChange takes nothing returns boolean
  local integer i = 0
  local integer c = Max(c_ally_total, c_enemy_total)
  local player p = null
  loop
    exitwhen i >= c
    if i < c_ally_total then
      set p = ally_force[i]
      if GetPlayerSlotState(p) == PLAYER_SLOT_STATE_PLAYING and not IsPlayerObserver(p) and not IsPlayerAlly(ai_player,p) then
        set i = c + 2
      endif
    endif
    if i < c_enemy_total then
      set p = enemy_force[i]
      if GetPlayerSlotState(p) == PLAYER_SLOT_STATE_PLAYING and not IsPlayerObserver(p) and not IsPlayerEnemy(ai_player,p) then
        set i = c + 2
      endif
    endif
    set i = i + 1
  endloop
  set p = null
  return i > c
endfunction

function RevealEnemy takes nothing returns nothing
  call DisplayToAllJobDebug("REVEAL_ENEMY JOB START")
  if ai_time > 160 then
    if CheckAllianceChange() then  // Prevent map change alliance settings
      call countAllyEnemy()
      call countEnemyReal()
    endif
  else
    call countEnemyReal()
  endif
  if not pathing_done then
    call TQAddJob(1200, REVEAL_ENEMY, 0)
  endif
endfunction
#ENDIF