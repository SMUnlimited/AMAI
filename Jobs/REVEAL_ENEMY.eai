#IFDEF GLOBAL
#ELSE
function UpdateAllyEnemy takes nothing returns nothing
  local integer i = 0
  local player p = null
  set force_number = 0
  set c_ally_total = 0
  set c_ally_user_total = 0
  set c_enemy_total = 0
  set c_enemy_user_total = 0
  set c_ai_total = 0
  loop
    exitwhen i >= PLAYER_NEUTRAL_AGGRESSIVE
    set p = Player(i)
    if GetPlayerSlotState(p) == PLAYER_SLOT_STATE_PLAYING and not IsPlayerObserver(p) then
      if GetPlayerController(p) == MAP_CONTROL_COMPUTER then
        set c_ai_total = c_ai_total + 1
      endif
      if IsPlayerAlly(ai_player,p) then
        if i == GetAiPlayer() then
          set own_force[force_number] = p
          set force_number = force_number + 1
        else
          if GetPlayerController(p) == MAP_CONTROL_COMPUTER then
            set own_force[force_number] = p
            set force_number = force_number + 1
          else
            set c_ally_user_total = c_ally_user_total + 1
          endif
          set c_ally[player_race[i]] = c_ally[player_race[i]] + 1
          set ally_force[c_ally_total] = p
          set c_ally_total = c_ally_total + 1
        endif
      elseif IsPlayerEnemy(ai_player,p) then
        if GetPlayerController(p) != MAP_CONTROL_COMPUTER then
          set c_enemy_user_total = c_enemy_user_total + 1
        endif
        set c_enemy[player_race[i]] = c_enemy[player_race[i]] + 1
        set enemy_force[c_enemy_total] = p
        set c_enemy_total = c_enemy_total + 1
      endif
    endif
    set i = i + 1
  endloop
  set p = null
endfunction

function countEnemyReal takes nothing returns nothing
  local integer i = 0
  local integer p = 0
  loop
    exitwhen i > RACE_NUMBER
    set c_enemy[i] = 0
    set i = i + 1 
  endloop

  set i = 0
  loop
    exitwhen i >= c_enemy_total
    if GetPlayerSlotState(enemy_force[i]) == PLAYER_SLOT_STATE_PLAYING then
      set p = GetPlayerId(enemy_force[i])
      set c_enemy[player_race[p]] = c_enemy[player_race[p]] + 1
    endif
    set i = i + 1
  endloop
endfunction

function RevealEnemy takes nothing returns nothing
  call DisplayToAllJobDebug("REVEAL_ENEMY JOB START")
  if ai_time <= 160 then
    call countEnemyReal()
  else
    call UpdateAllyEnemy()  // Prevent map change alliance settings
  endif
  if not pathing_done then
    call TQAddJob(360, REVEAL_ENEMY, 0)
  endif
endfunction
#ENDIF