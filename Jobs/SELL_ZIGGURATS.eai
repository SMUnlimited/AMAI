#IFDEF GLOBAL
  location TRrecovery = null
  unit sellbuild = null
  unit sellpeon = null
#ELSE

function RecoveryTRBuild takes group g returns nothing
  if sellbuild == null or not UnitAlive(sellbuild) then
    call GroupEnumUnitsInRange(g, GetLocationX(TRrecovery), GetLocationY(TRrecovery), 2000, null)
    set g = SelectByPlayer(g, ai_player, true)
    set g = SelectUnittype(g, UNIT_TYPE_STRUCTURE, true)
    set g = SelectByAlive(g, true)
    set g = SelectByHidden(g, false)
    set sellbuild = FirstOfGroup(g)
  endif
  if sellbuild == null then
    call RemoveLocation(TRrecovery)
    set TRrecovery = null
    set sellbuild = null
    set sellpeon = null
  else
    if sellpeon == null or not UnitAlive(sellpeon) then
      set sellpeon = GetExpansionPeon2(sellpeon)
    endif
    if sellpeon != null then
      call IssueTargetOrder(sellpeon, "unsummon", sellbuild)
    endif
  endif
endfunction

function SelectByZiggurat takes group rg returns group
  local unit u = null
  local integer i = 0
  local integer id = 0
  local group g = rg
  set rg = CreateGroup()
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    if UnitAlive(u) and not IsUnitHidden(u) then
      set id = GetUnitTypeId(u)
      loop
        exitwhen i >= ziggarray
        if id == racial_zigg[i] then
          call GroupAddUnit(rg, u)
        endif
        set i = i + 1
      endloop
      set i = 0
    endif
    call GroupRemoveUnit(g, u)
  endloop
  call DestroyGroup(g)
  set g = null
  return rg
endfunction

function CheckSellZiggurat takes unit u returns nothing
  local group g = CreateGroup()
  call GroupEnumUnitsInRange(g, GetUnitX(u), GetUnitY(u), race_ziggurat_keep_distance, null)
  call GroupRemoveUnit(g, u)
  set g = SelectByPlayer(g, ai_player, true)
  set g = SelectUnittype(g, UNIT_TYPE_STRUCTURE, true)
  set g = SelectByAlive(g, true)
  set g = SelectByHidden(g, false)
  if FirstOfGroup(g) == null then
    call Unsummon(u)
  endif
  call DestroyGroup(g)
  set g = null
endfunction

function CheckSellStructure takes group g returns nothing
  set g = SelectUnittype(g, UNIT_TYPE_STRUCTURE, true)
  set g = SelectUnittype(g, UNIT_TYPE_TOWNHALL, false)
  set g = SelectByIdOr(g, old_id[racial_lumber], old_id[racial_expansion], false)
  set g = SelectByIdOr(g, old_id[racial_altar], old_id[racial_shop], false)
  set g = SelectByAlive(g, true)
  set g = SelectByHidden(g, false)
  call Unsummon(FirstOfGroup(g))
endfunction

function SellZigguratJob takes nothing returns nothing
  local group g = CreateGroup()
  local unit u = null

  call DisplayToAllJobDebug("SELL_ZIGGURATS JOB START")
  if not towerrush then
    if TRrecovery == null then
      call GroupEnumUnitsOfPlayer(g, ai_player, null)
      if GetGoldOwned() + GetGold() < 800 then  // if insufficient resources, buy more buildings , else ensure there is sufficient food available
        call CheckSellStructure(g)
      elseif FoodCap() > ver_food_limit then
        set g = SelectByZiggurat(g)
        loop
          set u = FirstOfGroup(g)
          exitwhen u == null
          call CheckSellZiggurat(u)
          call GroupRemoveUnit(g,u)
        endloop
      endif
    else
      call RecoveryTRBuild(g)
      call DestroyGroup(g)
      set g = null
      call TQAddJob(6, SELL_ZIGGURATS, 0)
      return
    endif
  endif
  call DestroyGroup(g)
  set g = null
  if not pathing_done then
    call TQAddJob(30, SELL_ZIGGURATS, 0)
  endif
endfunction
#ENDIF