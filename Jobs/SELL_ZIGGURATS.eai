#IFDEF GLOBAL
  location TRrecovery = null
  unit sellbuild = null
  unit sellpeon = null
#ELSE

function RecoveryTRBuild takes group g returns nothing
  if sellbuild == null or not UnitAlive(sellbuild) then
    call GroupEnumUnitsInRange(g, GetLocationX(TRrecovery), GetLocationY(TRrecovery), 2000, null)
    set g = SelectByPlayer(g, ai_player, true)
    set g = SelectUnittype(g, UNIT_TYPE_STRUCTURE, true)
    set g = SelectByAlive(g, true)
    set g = SelectByHidden(g, false)
    set sellbuild = FirstOfGroup(g)
  endif
  if sellpeon == null or not UnitAlive(sellpeon) then
    set sellpeon = GetExpansionPeon2(sellpeon)
  endif
  if sellbuild != null and sellpeon != null then
    call IssueTargetOrder(sellpeon, "unsummon", sellbuild)
  elseif sellbuild == null then
    call RemoveLocation(TRrecovery)
    set TRrecovery = null
    set sellbuild = null
    set sellpeon = null
  endif
endfunction

function SelectByZiggurat takes group rg returns group
  local unit u = null
  local integer i = 0
  local group g = rg
  set rg = CreateGroup()
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    set i = 0
    loop
      exitwhen i > ziggarray
      if GetUnitTypeId(u) == racial_zigg[i] then
        call GroupAddUnit(rg, u)
      endif
      set i = i + 1
    endloop
    call GroupRemoveUnit(g, u)
  endloop
  call DestroyGroup(g)
  set g = null
  return rg
endfunction

function CheckSellZiggurat takes unit u returns nothing
  local group g = CreateGroup()
  call GroupEnumUnitsInRange(g, GetUnitX(u), GetUnitY(u), race_ziggurat_keep_distance, null)
  set g = SelectByPlayer(g, ai_player, true)
  set g = SelectUnittype(g, UNIT_TYPE_STRUCTURE, true)
  set g = SelectByAlive(g, true)
  set g = SelectByHidden(g, false)
  call GroupRemoveUnit(g, FirstOfGroup(g))
  if FirstOfGroup(g) == null then
    call Unsummon(u)
  endif
  call DestroyGroup(g)
  set g = null
endfunction

function SellZigguratJob takes nothing returns nothing
  local group g = CreateGroup()
  local unit u = null
  //local integer i = 0

  call DisplayToAllJobDebug("SELL_ZIGGURATS JOB START")
  if not towerrush then
    if TRrecovery == null then
      call GroupEnumUnitsOfPlayer(g, ai_player, null)
      set g = SelectByAlive(g, true)
      set g = SelectByHidden(g, false)
      set g = SelectByZiggurat(g)
      loop
        set u = FirstOfGroup(g)
        exitwhen u == null
        call CheckSellZiggurat(u)
        call GroupRemoveUnit(g,u)
      endloop
    else
      if sellbuild != null and UnitAlive(sellbuild) and sellpeon != null and UnitAlive(sellpeon) then
        call IssueTargetOrder(sellpeon, "unsummon", sellbuild)
      else
        call RecoveryTRBuild(g)
      endif
      call DestroyGroup(g)
      set g = null
      call TQAddJob(2, SELL_ZIGGURATS, 0)
      return
    endif
  endif
  call DestroyGroup(g)
  set g = null
  if not pathing_done then
    call TQAddJob(30, SELL_ZIGGURATS, 0)
  endif
endfunction
#ENDIF