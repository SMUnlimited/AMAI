#IFDEF GLOBAL
#ELSE

function SendHomeMoveUnitToLoc takes unit u, location l returns nothing
  local integer enemystrength = GetLocationNonCreepStrength(GetUnitX(u), GetUnitY(u), 800)
  if GetUnitAbilityLevel(u, 'AOwk') > 0 or GetUnitAbilityLevel(u, 'ANwk') > 0 then
    if enemystrength > 0 and not UnitInvis(u) then
      call IssueImmediateOrder(u, "windwalk")
    endif
  endif
  if enemystrength > 0 and CheckBlinkAbility(u) then
    call IssuePointOrder(u, "blink", GetLocationX(l), GetLocationY(l))
    call IssuePointOrder(u, "move", GetLocationX(l), GetLocationY(l))
  elseif GetUnitAbilityLevel(u, 'Ashm') > 0 and enemystrength > 0 and (GetFloatGameState(GAME_STATE_TIME_OF_DAY) >= 18 or GetFloatGameState(GAME_STATE_TIME_OF_DAY) < 6) and not IsUnitType(u, UNIT_TYPE_HERO) then
    call CreateDebugTag("SEND_HOME: Night Hide", 10, u, 3.00, 1.50)
    call IssueImmediateOrder(u, "ambush")
  else
    call CreateDebugTag("SEND_HOME: Moving to Location", 10, u, 3.00, 2.00)
    //call RemoveGuardPosition(u)
    call IssuePointOrder(u, "move", GetLocationX(l), GetLocationY(l))
  endif
endfunction

function SomeUnitHasHealingItem takes unit ru, unit u, integer rhi returns unit
  local group g = CreateGroup()
  call GroupEnumUnitsInRange(g, GetUnitX(u), GetUnitY(u), 400, null)
  set g = SelectByPlayer(g, ai_player, true)
  set g = SelectByAlive(g, true)
  loop
    set ru = FirstOfGroup(g)
    exitwhen ru == null
    if GetUnitAbilityLevel(ru, 'AInv') > 0 and GetItemNumberOnUnit(rhi, ru) > 0 then
      call DestroyGroup(g)
      set g = null
      return ru
    endif
    call GroupRemoveUnit(g, ru)
  endloop
  call DestroyGroup(g)
  set g = null
  return null
endfunction

function SendUnitHomeJob takes unit u, integer p returns nothing

  local location l = null
  local unit nearshopunit = null
  local group g = null
  local unit corpse = null
  local item it = null
  local integer racial_healing_item = 0
  local integer racial_mana_item = 0
  local integer enemystrength = 0
  local integer id = 0
  local unit otherhero = null

  call DisplayToAllJobDebug("SEND_HOME Job")

  if GetUnitState(u, UNIT_STATE_LIFE) <= 0 then
    call CreateDebugTag("SEND_HOME unit dead", 10, u, 3.00, 1.50)
    call TQAddUnitJob(2, RESET_GUARD_POSITION, p, u)
    return
  endif
  call DisplayToAllJobDebug("SEND_HOME Starting check")
  if IsUnitType(u, UNIT_TYPE_HERO) then
    set racial_healing_item = GetHeroHealingItem()
    set racial_mana_item = GetHeroManaItem()
    if racial_healing_item != 0 then
      set otherhero = SomeUnitHasHealingItem(otherhero, u, racial_healing_item)
    endif
    if buy_type[racial_healing_item] == BT_RACIAL_ITEM then
      //set shop_unit = GetUnitOfTypeNearUnit(old_id[racial_shop], u, shop_unit)
      set nearshopunit = GetUnitOfTypeNearUnit(old_id[racial_shop], u, nearshopunit)
      //	set shop_distance_limit = raceshop_distance_limit
    else
      //	set shop_unit = nearest_neutral[NEUTRAL_MERCHANT]
      if not (neutral_guarded[NEUTRAL_MERCHANT] and ((GetFloatGameState(GAME_STATE_TIME_OF_DAY) >= 5 and GetFloatGameState(GAME_STATE_TIME_OF_DAY) < 18) or not neutral_night_buy[NEUTRAL_MERCHANT])) then
        set nearshopunit = nearest_neutral[NEUTRAL_MERCHANT]
      endif
    endif
    //	set shop_distance_limit = merchant_distance_limit
    //set shop_sent = u
    //call TQAddJob(0, BUY_ITEM, racial_healing_item)
    //set nearshopunit = shop_unit
  endif
  call RemoveGuardPosition(u)
  if GetUnitState(u, UNIT_STATE_LIFE) >= GetUnitState(u, UNIT_STATE_MAX_LIFE) * (I2R(60)/100) then
    call CreateDebugTag("SEND_HOME Healed", 10, u, 3.00, 1.50)
    if GetUnitCurrentOrder(u) != OrderId("cannibalize") then
      call TQAddUnitJob(2, RESET_GUARD_POSITION, 0, u)
    else
      call TQAddUnitJob(6, RESET_GUARD_POSITION, 0, u)
    endif
  elseif racial_healing_item != 0 and GetItemNumberOnUnit(racial_healing_item, u) > 0 then
    if GetItemInstantType(racial_healing_item) == ITEMTYPE_CONTINUOUS and DistanceBetweenPoints_dk(GetUnitLoc(u), exist_town[p]) >= 1200 then
      call SendHomeMoveUnitToLoc(u,exist_town[p])  // This should be required for all regenerative type healing items
      call TQAddUnitJob(4, SEND_HOME, p, u)
      call CreateDebugTag("Hero: Use healing item", 10, u, 3.00, 2.00)
    else
      call CreateDebugTag("Hero: Use healing item", 10, u, 3.00, 1.50)
      set it = GetItemOfTypeOnUnit(racial_healing_item, u)
      call UnitUseItem(u, it)
      call UnitUseItemTarget(u, it, u)
      if racial_mana_item != 0 and GetUnitState(u, UNIT_STATE_MANA) < 100 and GetItemNumberOnUnit(racial_mana_item, u) > 0 then
        call UnitUseItem(u, GetItemOfTypeOnUnit(racial_mana_item, u))
      endif
      call TQAddUnitJob(Max(3,GetItemHealingTime(racial_healing_item)), SEND_HOME, p, u)  // Have to add delay as continous healing items run out
      set it = null
    endif
  elseif otherhero != null and IsUnitType(u, UNIT_TYPE_HERO) and GetSlotsFreeOnUnit(u) > 0 then  // prevent create on the ground
    call UnitAddItem(u, GetItemOfTypeOnUnit(racial_healing_item, otherhero))
    call TQAddUnitJob(2, RESET_GUARD_POSITION, 0, u)
    call CreateDebugTag("Hero: add healing item", 10, u, 3.00, 2.00)
  elseif nearshopunit != null and IsUnitType(u, UNIT_TYPE_HERO) and GetSlotsFreeOnUnit(u) > 0 and GetItemNumberOnUnit(racial_healing_item, u) <= 0 then
    if DistanceBetweenUnits(u, nearshopunit) >= buy_distance then
      set l = GetUnitLoc(nearshopunit)
      call SendHomeMoveUnitToLoc(u,l)
      call TQAddUnitJob(4, SEND_HOME, p, u)
      call RemoveLocation(l)
      set l = null
      call CreateDebugTag("Hero: move to shop", 10, u, 3.00, 2.00)
    else
      call CreateDebugTag("SEND_HOME Stop at Shop", 10, u, 3.00, 2.00)
      call IssueImmediateOrder(u, "stop")
      call IssueNeutralImmediateOrderById(ai_player, nearshopunit, old_id[racial_healing_item])
      if GetUnitState(u, UNIT_STATE_MANA) < 100 and racial_mana_item != 0 and GetItemNumberOnUnit(racial_mana_item, u) <= 0 then
        call IssueNeutralImmediateOrderById(ai_player, nearshopunit, old_id[racial_mana_item])
      endif
      call TQAddUnitJob(2, RESET_GUARD_POSITION, 0, u)  // end job
    endif
  else
    if DistanceBetweenPoints_dk(GetUnitLoc(u), exist_town[p]) >= 1200 then
      call SendHomeMoveUnitToLoc(u,exist_town[p])
      if follow_zeppelin != null and not IsUnitInGroup(follow_zeppelin, unit_rescueing) and not IsUnitType(u, UNIT_TYPE_FLYING) then
        call CreateDebugTag("Zepplin Home", 10, u, 3.00, 1.50)
        call GroupAddUnit(unit_rescueing, follow_zeppelin)
        set g = CreateGroup()
        call GroupAddUnit(g, follow_zeppelin)
        call GroupAddUnit(unit_healing, u)
        call TQAddGroupJob(0, ZEPPELIN_MOVE, ZTARGET_HOME, u, g)
        set nearshopunit = null
        set otherhero = null
        set g = null
        return  // switch job
      endif
    else
      if town_threatened and GetLocationNonCreepStrength(GetUnitX(u), GetUnitY(u), 1500) > 0 then  // only need to control the unit if it is attacked nearby, otherwise will be killed
        call CreateDebugTag("SEND_HOME town_threatened", 10, u, 3.00, 1.50)
        call RecycleGuardPosition(u)   // Gives unit control for only a little bit but dosn't remove from healing group
      // elseif GetUnitAbilityLevel(u, 'Ashm') > 0 and (GetFloatGameState(GAME_STATE_TIME_OF_DAY) >= 18 or GetFloatGameState(GAME_STATE_TIME_OF_DAY) < 6) and not IsUnitType(u, UNIT_TYPE_HERO) then
      //   call IssueImmediateOrder(u, "ambush")  // no need control
      //   call CreateDebugTag("SEND_HOME hide", 10, u, 3.00, 1.50)
      elseif GetUnitState(u, UNIT_STATE_MANA) > 200 then  //Note : this writing may not be compatible with custom maps
        if (GetUnitAbilityLevel(u, 'Ahwd') > 0 or GetUnitAbilityLevel(u, 'AChw') > 0) and not ward_cast then
          set ward_cast = IssuePointOrder(u, "healingward", GetUnitX(u), GetUnitY(u))
          call CreateDebugTag("SEND_HOME use healingward " + B2S(ward_cast), 10, u, 3.00, 1.50)
        elseif GetUnitAbilityLevel(u, 'AChv') > 0 or (IsUnitType(u, UNIT_TYPE_HERO) and (GetUnitAbilityLevel(u, 'AOhw') > 0 or GetUnitAbilityLevel(u, 'ANhw') > 0)) then
          call IssueTargetOrder(u, "healingwave", u)
          call CreateDebugTag("SEND_HOME heal wave", 10, u, 3.00, 1.50)
        elseif GetUnitAbilityLevel(u, 'ANhs') > 0 then
          call IssuePointOrder(u, "healingspray", GetUnitX(u), GetUnitY(u))
          call CreateDebugTag("SEND_HOME heal spray", 10, u, 3.00, 1.50)
        elseif GetUnitAbilityLevel(u, 'Arej') > 0 or GetUnitAbilityLevel(u, 'ACrj') > 0 or GetUnitAbilityLevel(u, 'ACr2') > 0 then
          if GetUnitAbilityLevel(u, 'Abrf') > 0 then
            call IssueImmediateOrder(u, "unbearform")
          endif
          call IssueTargetOrder(u, "rejuvination", u)
        endif
      else
        if GetUnitAbilityLevel(u, 'Abur') > 0 and not IsUnitInvisible(u, Player(PLAYER_NEUTRAL_AGGRESSIVE)) then
          call IssueImmediateOrder(u, "burrow")
          call CreateDebugTag("SEND_HOME burrow", 10, u, 3.00, 1.50)
        elseif GetUnitAbilityLevel(u, 'Astn') > 0 and GetUnitAbilityLevel(u, 'ACmi') == 0 then
          call IssueImmediateOrder(u, "stoneform")
          call CreateDebugTag("SEND_HOME stone", 10, u, 3.00, 1.50)
        elseif (GetUnitAbilityLevel(u, 'Acn2') > 0 or GetUnitAbilityLevel(u, 'Acan') > 0 or GetUnitAbilityLevel(u, 'ACcn') > 0) and GetUnitCurrentOrder(u) != OrderId("cannibalize") then
          if GetUpgradeLevel(old_id[UPG_CANNIBALIZE]) > 0 then
            set g = CreateGroup()
            call GroupEnumUnitsInRangeOfLoc(g, exist_town[p], 1000, null)
            loop
              set corpse = FirstOfGroup(g)
              exitwhen corpse == null
              exitwhen not UnitAlive(corpse) and IsUnitType(corpse, UNIT_TYPE_DEAD) and not IsUnitType(corpse, UNIT_TYPE_STRUCTURE)
              call GroupRemoveUnit(g, corpse)
            endloop
          endif
          if (GetUnitAbilityLevel(u, 'Ahrl') == 0 or (GetWood() > 500 and GetUnitCurrentOrder(u) != OrderId("cannibalize"))) and corpse != null then  // 'Ahrl' Ghoul Harvest lumber ability
            call IssueTargetOrder(u, "move" , corpse)
            call IssueImmediateOrder(u, "cannibalize")
            call CreateDebugTag("SEND_HOME cannibalize", 10, u, 3.00, 1.50)
          else
            call GroupClear(g)
            call GroupEnumUnitsInRangeOfLoc(g, exist_town[p], 1000, null)
            loop
              set corpse = FirstOfGroup(g) // Not a corpse, is it a town hall we can return wood too?
              exitwhen corpse == null
              exitwhen UnitAlive(corpse) and (IsUnitType(corpse, UNIT_TYPE_TOWNHALL) or GetUnitTypeId(corpse) == old_id[racial_lumber])
              call GroupRemoveUnit(g, corpse)
            endloop
            if corpse != null then
              if GetUnitCurrentOrder(u) == OrderId("harvest") or GetUnitCurrentOrder(u) == OrderId("resumeharvesting") then
              else
                call IssueImmediateOrder(u, "autoharvestlumber")
              endif
              call CreateDebugTag("SEND_HOME harvest lumber", 10, u, 3.00, 1.50)
            else
              call CreateDebugTag("SEND_HOME cannibalize", 10, u, 3.00, 1.50)
              call IssueImmediateOrder(u, "cannibalize")
            endif
          endif
          set corpse = null
          call DestroyGroup(g)
          set g = null
        endif
      endif
    endif
    call TQAddUnitJob(4, SEND_HOME, p, u)
    //call DisplayToAllJobDebug("SEND_HOME: JOB Finished!!!! ")
  endif
  set nearshopunit = null
  set otherhero = null
endfunction
#ENDIF