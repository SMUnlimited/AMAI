#IFDEF GLOBAL
    location rally_point = null
    boolean homebuild = true
    boolean minebuild = false
    boolean shredderbuild = false
#ELSE

function CheckBurrow takes group g , integer id returns nothing
  call GroupEnumUnitsOfPlayer(g, ai_player, null)
  set g = SelectById(g, id, true)
  set g = SelectByAlive(g, true)
  call GroupImmediateOrder( g, "standdown" )  //fix orc peon go battlestations , but can not standdown
  call GroupClear(g)
endfunction

function CheckTower takes group g returns nothing
  call GroupEnumUnitsOfPlayer(g, ai_player, null)
  set g = SelectById(g, old_id[race_tower_id], true)
  set g = SelectByAlive(g,true)
  if FirstOfGroup(g) != null then
    if race_tower_upgrade2 != 0 and GetRandomInt(0,4) == 4 then
      call IssueImmediateOrderById(FirstOfGroup(g), old_id[race_tower_upgrade2])
    endif
    call IssueImmediateOrderById(FirstOfGroup(g), old_id[race_tower_upgrade1])
  endif
  call GroupClear(g)
endfunction

function StatisticsUnitNum takes group g returns integer
  local integer i = 0
  local unit u = null
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    set i = i + 1
    call GroupRemoveUnit(g,u)
  endloop
  return i
endfunction

function CheckBuildLoc takes group g , boolean bool , location loc , real range , integer num returns nothing
  call GroupEnumUnitsInRangeOfLoc(g, loc, range, null)
  set g = SelectByPlayer(g, ai_player, true)
  set g = SelectUnittype(g, UNIT_TYPE_STRUCTURE, true)
  set g = SelectByAlive(g, true)
  if StatisticsUnitNum(g) >= num then
    set bool = false
  else
    set bool = true  //build may be dismantled after being attacked
  endif
  call GroupClear(g)
endfunction

function CheckShredderLoc takes group g returns nothing
  local location loc = null
  local unit u = null
  call GroupEnumUnitsOfPlayer(g, ai_player, null)
  set g = SelectById(g, neutral_shredder, true)
  set u = FirstOfGroup(g)
  set loc = Location(GetUnitX(u),GetUnitY(u))
  if u != null and GetUnitCurrentOrder(u) == OrderId("harvest") and DistanceBetweenPoints(home_location , loc) >= 1000 then
    call GroupEnumUnitsInRangeOfLoc(g, loc, 800, null)
    set g = SelectByPlayer(g, ai_player, true)
    set g = SelectUnittype(g, UNIT_TYPE_STRUCTURE, true)
    set g = SelectByAlive(g, true)
    if StatisticsUnitNum(g) >= 5 then
      set shredderbuild = false
    else
      set shredderbuild = true
    endif
  endif
  call RemoveLocation(loc)
  set loc = null
  call GroupClear(g)
  set u = null
endfunction

function CheckRallyPoint takes group g , location loc , real range returns nothing
  if rally_point == null and front_loc[0] != null then
    set rally_point = AIGetProjectedLoc(front_loc[0], home_location, -1200, 0)
  endif
  if loc == null then
    return
  endif
  call GroupEnumUnitsInRangeOfLoc(g, home_location, range, null)  // ohter town no need set
  set g = SelectByPlayer(g, ai_player, true)
  set g = SelectUnittype(g, UNIT_TYPE_TOWNHALL, false)  //avoid peon go to rallypoint
  set g = SelectUnittype(g, UNIT_TYPE_STRUCTURE, true)
  set g = SelectByAlive(g, true)
  call GroupPointOrderLoc(g, "setrally",loc)
  call GroupClear(g)
endfunction

function StructurecontrolJob takes nothing returns nothing
  local location loc = null
  local group g = CreateGroup()
  local integer i = 'ngol'
  if race_manual_loading then
    set i = old_id[race_manual_loading_mine]
  elseif race_uses_mine_expansion then
    set i = old_id[racial_expansion]
  endif
  call DisplayToAllJobDebug("STRUCTURE_CONTROL JOB START")
  call CheckRallyPoint(g,rally_point,1600)
  call CheckRallyPoint(g,home_location,1100)  //avoid being blocked by front_loc build
  if racial_lumber == 0 or race_no_wood_harvest then  //help fix ELF build blocking
    call CheckBuildLoc(g,homebuild,home_location,800,5)
    call GroupEnumUnitsInRangeOfLoc(g, home_location, 1000, null)
    if race_manual_loading or race_uses_mine_expansion then
      set g = SelectByPlayer(g, ai_player, true)
    endif
    set g = SelectById(g, i, true)
    if FirstOfGroup(g) == null or (FirstOfGroup(g) != null and GetResourceAmount(FirstOfGroup(g)) == 0) then
      set loc = GetUnitLoc(FirstOfGroup(g))
      call CheckBuildLoc(g,minebuild,loc,700,4)
      call RemoveLocation(loc)
      set loc = null
    endif
    if TownCountDone(neutral_shredder) > 0 then
      call CheckShredderLoc(g)  // harvest wood will have new loc can build
    endif
  endif
  if TownCountDone(BURROW) > 0 and not TownThreatened() then
    call CheckBurrow(g,old_id[BURROW])
  endif
  if race_tower_upgrade1 != 0 and TownCountDone(race_tower_id) != 0 and GetWood() >= 850 and GetGold() >= 1200 and not towerrush then
    call CheckTower(g)
  endif
  call DestroyGroup(g)
  set g = null
  call TQAddJob(80, STRUCTURE_CONTROL, 0)
endfunction
#ENDIF