#IFDEF GLOBAL
    location rally_point = null
    boolean homebuild = true
    boolean minebuild = false
    boolean shredderbuild = false
#ELSE

function CheckBurrow takes integer id returns nothing
  local group g = CreateGroup()
  call GroupEnumUnitsOfPlayer(g, ai_player, null)
  set g = SelectById(g, id, true)
  set g = SelectByAlive(g, true)
  call GroupImmediateOrder( g, "standdown" )  //fix orc peon go battlestations , but can not standdown
  call DestroyGroup(g)
  set g = null
endfunction

function CheckBuildLoc takes location loc, boolean bool, real range, integer num returns nothing
  local group g = CreateGroup()
  local integer i = 0
  call GroupEnumUnitsInRangeOfLoc(g, loc, range, null)
  set g = SelectByPlayer(g, ai_player, true)
  set g = SelectUnittype(g, UNIT_TYPE_STRUCTURE, true)
  set g = SelectByAlive(g, true)
  set i = BlzGroupGetSize(g)
  call DestroyGroup(g)
  set g = null
  set bool = (i < num)  //ture is build may be dismantled after being attacked
endfunction

function CheckShredderLoc takes nothing returns nothing
  local real d = 0
  local location loc = null
  local unit u = null
  local group g = CreateGroup()
  call GroupEnumUnitsOfPlayer(g, ai_player, null)
  set g = SelectById(g, neutral_shredder, true)
  set u = FirstOfGroup(g)
  if u != null and GetUnitCurrentOrder(u) == OrderId("harvest") then
    set loc = GetUnitLoc(u)
    set d = DistanceBetweenPoints(home_location , loc)
    if d >= 1000 and d <= 2000 then  //Ensure to be near home_location
      call GroupClear(g)
      call GroupEnumUnitsInRangeOfLoc(g, loc, 800, null)
      set g = SelectByPlayer(g, ai_player, true)
      set g = SelectUnittype(g, UNIT_TYPE_STRUCTURE, true)
      set g = SelectByAlive(g, true)
      set shredderbuild = BlzGroupGetSize(g) < 5
    endif
    call RemoveLocation(loc)
    set loc = null
  endif
  call DestroyGroup(g)
  set g = null
  set u = null
endfunction

function CheckRallyPoint takes location loc, real range returns nothing
  local group g = null
  if rally_point == null then
    if front_loc[0] != null and GetLocationX(front_loc[0]) !=0 and GetLocationY(front_loc[0]) !=0 then
      set rally_point = AIGetProjectedLoc(front_loc[0], home_location, -1200, 0)
    endif
  endif
  if loc == null then
    return
  endif
  set g = CreateGroup()
  call GroupEnumUnitsInRangeOfLoc(g, home_location, range, null)  // ohter town no need set
  set g = SelectByPlayer(g, ai_player, true)
  set g = SelectUnittype(g, UNIT_TYPE_TOWNHALL, false)  //avoid peon go to rallypoint
  set g = SelectUnittype(g, UNIT_TYPE_STRUCTURE, true)
  set g = SelectByAlive(g, true)
  call GroupPointOrderLoc(g, "setrally",loc)
  call DestroyGroup(g)
  set g = null
endfunction

function StructurecontrolJob takes nothing returns nothing
  call DisplayToAllJobDebug("STRUCTURE_CONTROL JOB START")
  call CheckRallyPoint(rally_point,1600)
  call CheckRallyPoint(home_location,1100)  //avoid being blocked by front_loc build
  if racial_lumber == 0 or race_no_wood_harvest then  //help fix ELF build blocking
    call CheckBuildLoc(home_location,homebuild,800,5)
    call CheckBuildLoc(mine_loc,minebuild,700,4)
    if TownCountDone(neutral_shredder) > 0 then
      call CheckShredderLoc()  // harvest wood will have new loc can build
    endif
  endif
  if TownCountDone(BURROW) > 0 and not town_threatened then
    call CheckBurrow(old_id[BURROW])
  endif
  if not pathing_done then
    call TQAddJob(sleep_multiplier * 5, STRUCTURE_CONTROL, 0)
  endif
endfunction
#ENDIF