#IFDEF GLOBAL
    boolean teleporting = false
    location teleportloc = Location(0,0)
#ELSE
function TeleportJob takes unit hu returns nothing
  local group g = null
  local unit u = null
  local integer current_order = GetUnitCurrentOrder(hu)

  call DisplayToAllJobDebug("TELEPORT JOB Start")
  if not UnitAlive(hu) or current_order < 852008 or current_order > 852013 then
    if not UnitAlive(hu) then
      call CaptainGoHome()
    elseif teleportloc != null then
      call TeleportCaptain(GetLocationX(teleportloc),GetLocationY(teleportloc))
      //call AttackMoveXY(R2I(GetLocationX(teleportloc)), R2I(GetLocationY(teleportloc)))
    endif
    set teleporting = false
    return
  endif
  set g = CreateGroup()
  call GroupEnumUnitsInRange(g, GetUnitX(hu), GetUnitY(hu), 2 * teleport_radius, null)
  call GroupRemoveUnit(g, hu)
  set g = SelectByPlayer(g, ai_player, true)
  set g = SelectUnittype(g, UNIT_TYPE_PEON, false)
  set g = SelectUnittype(g, UNIT_TYPE_STRUCTURE, false)
  set g = SelectByHidden(g, false)
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    if DistanceBetweenUnits(hu, u) > teleport_radius and UnitAlive(u) then
      call IssuePointOrder(u, "move", GetUnitX(hu), GetUnitY(hu))
    endif
    call GroupRemoveUnit(g, u)
  endloop
  call DestroyGroup(g)
  set g = null
  call TQAddUnitJob(1, TELEPORT, 0, hu)
endfunction
#ENDIF