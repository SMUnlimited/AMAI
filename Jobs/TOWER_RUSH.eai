#IFDEF GLOBAL

#ELSE

// ######################## Tower Rush ##############################
// #  Created by Strategy Master 13/07/05. Controls all part of the
// #  tower rush when tower rush check comes true.
// ##################################################################
function TowerRushHeroControl_k takes unit u, location t returns nothing
	local location l = GetUnitLoc(u)
	call RemoveGuardPosition(u)
	call ClearCaptainTargets()
	if not tower_peon_inplace then
		call IssuePointOrder(u, "move", GetUnitX(builder[1]), GetUnitY(builder[1]))
	elseif DistanceBetweenPoints(l, t) > 200 then
		call CreateDebugTag("TRush: hero moving", 10, u, 4.00, 2.00)
		call IssuePointOrderLoc(u, "move", t)
	endif
	if DistanceBetweenPoints(l, toweringplayerloc) <= DistanceBetweenPoints(t, toweringplayerloc) + 230 then
		call UnitUseItemPoint( u, GetItemOfTypeOnUnit(race_tower_item, u), GetLocationX(l), GetLocationY(l))
	endif
	call RemoveLocation(l)
	set l = null
endfunction

function TowerRushBuilderFirstControl_d takes unit u, unit hall returns nothing
	call RemoveGuardPosition(u)
	if hall != null and DistanceBetweenUnits(u, hall) < 1000 then
		if (GetUnitAbilityLevel(u, 'ANha') != 0 or GetUnitAbilityLevel(u, 'Ahar') != 0 or GetUnitAbilityLevel(u, 'Ahr3') != 0) then
			call IssueImmediateOrder( u, "returnresources" )
		endif
		if race_militia_available and DistanceBetweenPoints_kd(toweringplayerloc, GetUnitLoc(u)) > 6800 then
			call IssueImmediateOrder( u, race_militia_unitstring )
		endif
	endif
endfunction

function TowerRushBuilderControl_k takes unit u, location t returns nothing
	local real builder_x = 0
	local real builder_y = 0
	local integer id = GetUnitCurrentOrder(u)
	local unit loccheck = null
	local location unitloc = null
	local location loc = null
	local location l = GetUnitLoc(u)
	if DistanceBetweenPoints(l, t) >= 300 and (id != OrderId("move") or id == OrderId("stop")) then
		call CreateDebugTag("TRush: builder moving", 10, u, 4.00, 2.00)
		call IssuePointOrderLoc(u, "move", t)
	elseif DistanceBetweenPoints(l, t) <= 500 and (id == 0 or (id != OrderId("smart") and id != OrderId("repair") and id != old_id[race_tower_id] and id != old_id[racial_shop])) then  //Improve efficiency
		set tower_peon_inplace = true
		if towers_built >= 6 and race_tower_shop == 0 then  //Building shop
			set loc = GetSubtractionLoc(tower_target_loc, toweringplayerloc)
			set unitloc = GetProjectedLoc(tower_target_loc, loc, 800)
			call RemoveLocation(loc)
			if race_tower_id_replace == 0 then
				set loccheck = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[racial_shop], GetLocationX(unitloc), GetLocationY(unitloc), 270.0)
			else
				set loccheck = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[race_tower_id_replace], GetLocationX(unitloc), GetLocationY(unitloc), 270.0)
			endif
			set loc = GetUnitLoc(loccheck)
			set builder_x = GetLocationX(loc)
			set builder_y = GetLocationY(loc)
			call RemoveLocation(unitloc)
			call RemoveLocation(loc)
			call RemoveUnit(loccheck)
			if not IsTerrainPathable(builder_x, builder_y, PATHING_TYPE_BUILDABILITY) then
				if GetGold() > (GetUnitGoldCost2(racial_shop)) and GetWood() > (GetUnitWoodCost2(racial_shop)) then
					call IssueBuildOrderById(u, old_id[racial_shop], builder_x, builder_y)
					call CreateDebugTag("TRush: Shop Building", 10, u, 4.00, 2.00)
					set race_tower_shop = 1
				endif
			endif
			set loc = null
		else  //Building tower
			if GetUnitTypeId(u) == old_id[racial_militia] or (race_tower_bypeon == true and u == builder[4] and IsUnitType(builder[1], UNIT_TYPE_STRUCTURE) and IsUnitType(builder[2], UNIT_TYPE_STRUCTURE) and IsUnitType(builder[3], UNIT_TYPE_STRUCTURE)) then
				call RemoveLocation(l)
				set l = null
				call CreateDebugTag("TRush: cannot build tower", 10, u, 4.00, 2.00)
				return  // HUMAN militia cannot build , ELF no need all peon build but need try give one repair
			endif
			if towers_built < 3 or (towers_built >= 3 and towers_built <= 6 and GetGold() > (GetUnitGoldCost2(race_tower_id) + 100) and GetWood() > (GetUnitWoodCost2(race_tower_id) + 60)) or (towers_built > 6 and (GetGold() > GetUnitGoldCost2(race_tower_id) * 2) and (GetWood() > GetUnitWoodCost2(race_tower_id) * 2)) then
				if race_tower_id_replace == 0 then
					set loccheck = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[race_tower_id], GetLocationX(t), GetLocationY(t), 270.0)
				else
					set loccheck = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[race_tower_id_replace], GetLocationX(t), GetLocationY(t), 270.0)
				endif
				set unitloc = GetUnitLoc(loccheck)
				set builder_x = GetLocationX(unitloc)
				set builder_y = GetLocationY(unitloc)
				call RemoveLocation(unitloc)
				call RemoveUnit(loccheck)
				if not IsTerrainPathable(builder_x, builder_y, PATHING_TYPE_BUILDABILITY) then
					if IssueBuildOrderById(u, old_id[race_tower_id], builder_x, builder_y) then
						set towers_built_count = towers_built_count + 1
					endif
					if towers_built != 0 then
						call CreateDebugTag("TRush: Building", 10, u, 4.00, 2.00)
					else
						call CreateDebugTag("TRush: First Building", 10, u, 4.00, 2.00)
						set towers_built = 1
					endif
				endif
			endif
		endif
	endif
	call RemoveLocation(l)
	set l = null
	set loccheck = null
	set unitloc = null
endfunction

function TowerRushTowerMoveControl_k takes unit u, location t returns nothing  //reuse these too far towers
	local unit loccheck = null
	local location unitloc = null
	local location l = GetUnitLoc(u)
	local integer id = GetUnitCurrentOrder(u)
	if GetUnitState(u, UNIT_STATE_LIFE) >= GetUnitState(u, UNIT_STATE_MAX_LIFE) * 0.65 then  //exclude UNIT_STATE_LIFE too low , dangerous to stand up
		if DistanceBetweenPoints(l, toweringplayerloc) > DistanceBetweenPoints(t, toweringplayerloc) + 220 and id == 0 then
			set unitloc = GetProjectedLoc(t, l, 70)
			if race_tower_id_replace == 0 then
				set loccheck = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[race_tower_id], GetLocationX(unitloc), GetLocationY(unitloc), 270.0)
			else
				set loccheck = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[race_tower_id_replace], GetLocationX(unitloc), GetLocationY(unitloc), 270.0)
			endif
			call RemoveLocation(unitloc)
			set unitloc = GetUnitLoc(loccheck)
			call RemoveUnit(loccheck)
			if not IsTerrainPathable(GetLocationX(unitloc), GetLocationY(unitloc), PATHING_TYPE_BUILDABILITY) then
				call RemoveGuardPosition(u)
				call IssueImmediateOrder( u, race_tower_moveendstring)
				call IssuePointOrderLoc( u, race_tower_movestartstring, unitloc)  //move tree
			endif
			call RemoveLocation(unitloc)
			set id = GetUnitCurrentOrder(u)  // update order
		endif
	endif
	if DistanceBetweenPoints(l, toweringplayerloc) <= DistanceBetweenPoints(t, toweringplayerloc) or id != OrderId(race_tower_movestartstring) or GetUnitState(u, UNIT_STATE_LIFE) < GetUnitState(u, UNIT_STATE_MAX_LIFE) * 0.5 then
		call RemoveGuardPosition(u)
		call IssuePointOrderLoc( u, race_tower_movestartstring, l)
	endif
	call RemoveLocation(l)
	set l = null
	set loccheck = null
	set unitloc = null
endfunction

function TowerRushSetBuilder takes unit u returns boolean
	if IsUnitType(u, UNIT_TYPE_STRUCTURE) or UnitAlive(u) == false or IsUnitBuying(u) or IsUnitInGroup(u, unit_zepplin_move) then  // the peon harvest gold or build by peon become, like ELF
		return false
	endif
	if tower_attack_run == true then
		call RemoveGuardPosition(u)
	endif
	return true
endfunction

function TowerRushJob takes nothing returns nothing
	local group g = null
	local group tempg = null
	local unit u = null
	local location t = null
	local item it = null
	local integer i = 0
	local integer s = 0
	local integer towers_attck = 0
	local integer randomnum = 0
	local integer towermove = 30
	local integer buildtries = 10
	local integer id = 0
	local real time_next_try = 5
	local real EnemyStrength = 0
	local real OwnStrength = 0
	local boolean CloseJob = false
	call DisplayToAllJobDebug("TOWER_RUSH Job START")
	if not hero_built[1] and race_tower_item_must == true then
		call TQAddJob(5, TOWER_RUSH, 0)
		return
	endif
	if race_tower_move then
		set buildtries = 8  // tower can move
	endif
	set g = CreateGroup()
	call GroupEnumUnitsInRange(g, GetLocationX(tower_target_loc), GetLocationY(tower_target_loc), 1500, null)
	loop
		set u = FirstOfGroup(g)
		exitwhen u == null
		if GetOwningPlayer(u) == ai_player and GetUnitState(u, UNIT_STATE_LIFE) > 0 and IsUnitType(u, UNIT_TYPE_STRUCTURE) then
			set id = GetUnitTypeId(u)
			if id == old_id[race_tower_id] or (race_tower_upgrade1 != 0 and id == old_id[race_tower_upgrade1]) or ( race_tower_upgrade2 != 0 and id == old_id[race_tower_upgrade2]) then
				if id == old_id[race_tower_id] then
					if (randomnum == 4 or GetUnitCountDone(old_id[race_towerrush_requirement]) == 0) and race_tower_upgrade2 != 0 and TownCount(race_tower_upgrade2) < 2 then
						call IssueImmediateOrderById(u, old_id[race_tower_upgrade2])
					elseif race_tower_upgrade1 != 0 then
						call IssueImmediateOrderById(u, old_id[race_tower_upgrade1])
					endif
				endif
				if race_tower_move == true then
					set builder[towermove] = u
					set towermove = towermove + 1
				endif
				if IsUnitType(u, UNIT_TYPE_ATTACKS_GROUND) or IsUnitType(u, UNIT_TYPE_ATTACKS_FLYING) then
					set towers_attck = towers_attck + 1
				endif
				set i = i + 1
			elseif id == old_id[racial_shop] then
				set s = s + 1
			endif
			if GetUnitState(u, UNIT_STATE_LIFE) < GetUnitState(u, UNIT_STATE_MAX_LIFE) * 0.7 then
				set randomnum = GetRandomInt(1, 4)
				if builder[randomnum] != null and GetUnitCurrentOrder(builder[randomnum]) == 0 then
					call IssueTargetOrder( builder[randomnum], "repair", u )
					call CreateDebugTag("TRush: repair tower", 10, u, 4.00, 2.00)
				endif
			endif
		endif
		call GroupRemoveUnit(g,u)
	endloop
	set towers_built = i
	set race_tower_shop = s
	if tower_used_item == false then
		set tower_used_item = towers_built > 0 or (IsPointBlighted(GetLocationX(t), GetLocationY(t)) == IsPointBlighted(GetLocationX(home_location), GetLocationY(home_location)))
		//have woter , no need send hero used item or UD item used success
	endif
	if towers_built <= 3 then
		if GetLocationCreepStrength(GetLocationX(tower_target_loc), GetLocationY(tower_target_loc), 500) > 0 then
			set t = GetProjectedLoc(tower_target_loc, towerrushsubloc, 600)
		else
			set t = GetProjectedLoc(tower_target_loc, towerrushsubloc, 660)
		endif
		if first_tower_point_x == 0 then
			set first_tower_point_x = GetLocationX(t)
			set first_tower_point_y = GetLocationY(t)
		endif
	elseif towers_built <= 6 then
		set t = GetProjectedLoc(tower_target_loc, towerrushsubloc, 200)
		if second_attack_point_x == 0 then
			set second_attack_point_x = GetLocationX(t)
			set second_attack_point_y = GetLocationY(t)
		endif
	else
		set t = GetProjectedLoc(tower_target_loc, towerrushsubloc, -400)
	endif
	call CreateDebugTagLoc("build loc", 10, GetLocationX(t), GetLocationY(t), 60.00, 55.00)
	//call Trace("Getting ready to tower rush")
	if not (GetLocationX(t) == 0 and GetLocationY(t) == 0) and (TownCountDone(hero[1]) + TownCountDone(hero[2]) + TownCountDone(hero[3])) != 0 then
		set CloseJob = town_threatened and town_threat[most_threatened_town] >= teleport_low_threat * 1.5
		set CloseJob = CloseJob or (race_tower_item_must and not tower_used_item and hero_unit[2] != null and builder[0] == null)  //  too longer time not buy item , but item must
		if not CloseJob then
			set EnemyStrength = GetLocationNonCreepStrength(GetLocationX(t), GetLocationY(t), 1200)
			set OwnStrength = GetOwnStrength() + towers_attck + towers_built * 0.3  //  tower have strength
			set CloseJob = CloseJob or (EnemyStrength > OwnStrength * 2 and towers_built_count > 6 * race_tower_peon and towers_built < 2)  //  6 job cycles
			set CloseJob = CloseJob or (EnemyStrength > OwnStrength * 4)
		endif
	endif
	if GetPlayerSlotState(rushedplayer) != PLAYER_SLOT_STATE_PLAYING or GetLocationEnemyStrength(enemy_point_x, enemy_point_y, 1600, false) == 0 or CloseJob == true then  //or (builder[1] != null and (GetUnitState(builder[1], UNIT_STATE_LIFE) <= 0  and GetUnitState(builder[3], UNIT_STATE_LIFE) <= 0 and GetUnitState(builder[2], UNIT_STATE_LIFE) <= 0)) then
		//maybe enemy give up the Location,so temporary no need built
		call Trace("I have finished the tower rush")
		set gold_buffer = 0
		set wood_buffer = 0
		call CaptainGoHome()
		if tower_attack_run == true then
			if race_has_power_build then  // reduction
				call GroupEnumUnitsOfPlayer(g, ai_player, null)
				set g = SelectUnittype(g, UNIT_TYPE_PEON, true)
				set g = SelectByAlive(g, true)
				set g = SelectByHidden(g, false)
				set i = 41
				loop
					set u = FirstOfGroup(g)
					exitwhen u == null
					if not IsUnitBuying(u) then
						call TQAddUnitJob(3, RESET_GUARD_POSITION, 0, u)  //Prevent peon immediately go tower point repair tower
					endif
					if builder[i] != null then
						set builder[i] = null
						set i = i + 1
					endif
					call GroupRemoveUnit(g,u)
				endloop
				call DestroyGroup(tower_rush_power)
				call GroupClear(g)
			endif
			set i = 1
			loop
				exitwhen i > race_tower_peon
				call IssuePointOrderLoc(builder[i], "move", home_location)
				call RecycleGuardPosition(builder[i])
				set builder[i] = null
				set i = i + 1
			endloop
			call GroupEnumUnitsInRange(g, GetLocationX(towerrushsubloc), GetLocationY(towerrushsubloc), 2000, null)
			set g = SelectUnittype(g, UNIT_TYPE_STRUCTURE, true)
			loop
				set u = FirstOfGroup(g)
				exitwhen u == null
				if GetOwningPlayer(u) == ai_player then
					call IssueImmediateOrderById( u, 851976 )  //Cancel can recycling resources and prevent AI run FAST BUILD
				endif
				call GroupRemoveUnit(g,u)
			endloop
		endif
		set race_tower_alliancetarget = false
		set tower_used_item = false
		set tower_attack_run = false
		set tower_peon_run = false
		set tower_peon_inplace = false
		set towerrush = false
		call DisplayToAllies(chat_stop_towerrush)
		call DisplayToObserversImportant(chat_stop_towerrush)
		if builder[0] != null and GetItemOfTypeOnUnit(race_tower_item, builder[0]) != null then
			call SetItemPawnable( GetItemOfTypeOnUnit(race_tower_item, builder[0]), true )  //can selling
			call SetItemDroppable( GetItemOfTypeOnUnit(race_tower_item, builder[0]), true )  //droppable
		endif
		set builder[0] = null
		set builder[10] = null
		set rushedplayer = null
		call RemoveLocation(toweringplayerloc)
		call RemoveLocation(tower_target_loc)
		call RemoveLocation(towerrushsubloc)
		call RemoveLocation(t)
		set toweringplayerloc = null
		set tower_target_loc = null
		set towerrushsubloc = null
		set t = null
		call DestroyGroup(g)
		set g = null
		set towers_built = 0
		set towers_built_count = 0
		set race_tower_shop = 0
		set first_tower_point_x = 0
		set first_tower_point_y = 0
		set second_attack_point_x = 0
		set second_attack_point_y = 0
		set enemy_point_x = 0
		set enemy_point_y = 0
		//call SetCaptainHome(BOTH_CAPTAINS, GetLocationX(front_loc[0]), GetLocationY(front_loc[0]))
		call SetPeonsRepair(true)
		return
	endif

	// if tower_attack_run == true then
	// 	if towers_built < 4 and race_simple_melee > 2 then
	// 		if race_tower_upgrade1 == 0 then
	// 			set gold_buffer = GetUnitGoldCost2(race_tower_id) + 100
	// 			set wood_buffer = GetUnitWoodCost2(race_tower_id) + 110
	// 		else
	// 			set gold_buffer = GetUnitGoldCost2(race_tower_upgrade1) + 70
	// 			set wood_buffer = GetUnitWoodCost2(race_tower_upgrade1) + 80
	// 		endif
	// 	elseif towers_built < 8 and race_simple_melee > 3 then
	// 		set gold_buffer = GetUnitGoldCost2(race_tower_id) + 40
	// 		set wood_buffer = GetUnitWoodCost2(race_tower_id) + 50
	// 	else
	// 		set gold_buffer = 0
	// 		set wood_buffer = 0
	// 	endif
	// endif

	if builder[0] == null and race_tower_item != 0 then
		set builder[0] = GetItemHero(GetItemOfType(it,race_tower_item))
	endif
	if builder[0] != null then  //Avoid next cycle judgment
		call SetItemDroppable(it, false)  //prevent droppable
		call SetItemPawnable(it, false)  //can selling
	endif
	set it = null
	loop
		exitwhen towermove < 30
		call TowerRushTowerMoveControl_k(builder[towermove], t)
		set builder[towermove] = null
		set towermove = towermove - 1
	endloop

	call GroupClear(g)
	call GroupEnumUnitsOfPlayer(g, ai_player, null)
	set g = SelectUnittype(g, UNIT_TYPE_PEON, true)
	set g = SelectByAlive(g, true)
	set g = SelectByLoaded(g,false)
	if race_manual_loading then
		set g = SelectByOrderOr(g, OrderId("smart"), OrderId("harvest"),true)
	endif
	set g = SelectByHidden(g,false)
	set i = 1
	loop
		exitwhen i > race_tower_peon
		call GroupRemoveUnit(g,builder[i])
		set i = i + 1
	endloop
	set tempg = CopyGroup(g,tempg)
	set g = GetNearestSubGroupOfGroup(g, tower_target_loc, 10)
	set i = 1
	loop
		set u = FirstOfGroup(g)
		exitwhen i > race_tower_peon or u == null
		if builder[i] == null or not UnitAlive(builder[i]) or IsUnitType(builder[i], UNIT_TYPE_STRUCTURE) or IsUnitLoaded(builder[i]) then  // prevent WIPS build and harvest
			if TowerRushSetBuilder(u) then
				call CreateDebugTag("TRush: set peon", 10, u, 4.00, 2.00)
				set builder[i] = u
				call GroupRemoveUnit(tempg,u)  // Prevent cannot build tower
				call GroupRemoveUnit(tower_rush_power,u)
			endif
		endif
		set i = i + 1
		call GroupRemoveUnit(g,u)
	endloop
	if race_has_power_build and tower_attack_run == true then  // fix AI pull all peon to build tower
		set i = 41
		loop
			exitwhen i > 45  //prevent harvest gold peon more then 5 , so just control 3 peon
			if builder[i] == null or not UnitAlive(builder[i]) or not IsUnitInGroup(builder[i], tempg) then
				set builder[i] = FirstOfGroup(tempg)
				set id = GetUnitCurrentOrder(builder[i])
				if i > 42 then
					call RemoveGuardPosition(builder[i])
					call GroupAddUnit(tower_rush_power,builder[i])
					call IssueImmediateOrder(builder[i], "autoharvestgold")
				elseif id == 0 then
					call CreateDebugTag("TRush: free peon", 10, u, 4.00, 2.00)
					call RecycleGuardPosition(builder[i])
				endif
			endif
			if i > 42 then
				set id = GetUnitCurrentOrder(builder[i])
				call CreateDebugTag("TRush: harvest gold peon", 10, u, 4.00, 2.00)
				if id == OrderId("harvest") or id == OrderId("resumeharvesting") then  // no need do anything
				else
					call IssueImmediateOrder(builder[i], "autoharvestgold")
				endif
			endif
			call GroupRemoveUnit(tempg,builder[i])
			set i = i + 1
		endloop
		call GroupRemoveUnit(tower_rush_power,builder[41])  // give AI 2 peon build other construct
		call GroupRemoveUnit(tower_rush_power,builder[42])
		loop
			set u = FirstOfGroup(tempg)
			exitwhen u == null
			set id = GetUnitCurrentOrder(u)
			call CreateDebugTag("TRush: harvest lumber peon", 10, u, 4.00, 2.00)
			if id == OrderId("harvest") or id == OrderId("resumeharvesting") then  // no need do anything
			elseif not IsUnitInGroup(u, tower_rush_power) then
				call RemoveGuardPosition(u)
				call GroupAddUnit(tower_rush_power,u)
				call IssueImmediateOrder(u, "autoharvestlumber")
			endif
			call GroupRemoveUnit(tempg,u)
		endloop
	endif

	if tower_attack_run == true then
		if GetItemNumberOnUnit(race_tower_item, builder[0]) > 0 then
			call TowerRushHeroControl_k(builder[0], t)
		endif
		set i = 1
		if tower_peon_run != true then
			loop
				exitwhen i > race_tower_peon
				call GroupClear(g)
				call GroupEnumUnitsInRange(g, GetUnitX(builder[i]),GetUnitY(builder[i]), 1000 ,null)
				set g = SelectUnittype(g, UNIT_TYPE_TOWNHALL, true)
				call TowerRushBuilderFirstControl_d(builder[i], FirstOfGroup(g))
				set i = i + 1
			endloop
			set tower_peon_run = true
			set time_next_try = 4
		elseif towers_built < buildtries and towers_built - towers_attck < 2 then  // here towers_built no change , so AI will build more tower , even if tower can move , but not a big problem
			loop
				exitwhen i > race_tower_peon
				call TowerRushBuilderControl_k(builder[i], t)
				set i = i + 1
			endloop
			set time_next_try = 2
		endif
	endif

	call DestroyGroup(tempg)
	set tempg = null
	call DestroyGroup(g)
	set g = null
	call RemoveLocation(t)
	set t = null
	set u = null
	call TQAddJob(RMax(time_next_try,1), TOWER_RUSH, 0)
endfunction

#ENDIF