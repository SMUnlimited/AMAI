#IFDEF GLOBAL

#ELSE

function TowerRushRecoveryBuild takes nothing returns nothing
	local group g = CreateGroup()
	local unit u = null
	local integer i = 1
	local integer s = 0
	local integer c = 0
	call GroupEnumUnitsInRange(g, GetLocationX(towerrecoveryloc), GetLocationY(towerrecoveryloc), 2000, null)
	loop
		set u = FirstOfGroup(g)
		exitwhen u == null or i > race_tower_peon
		if GetOwningPlayer(u) == ai_player and IsUnitType(u, UNIT_TYPE_STRUCTURE) and UnitAlive(u) then
			if builder[i] != null and UnitAlive(builder[i]) then
				call IssueTargetOrder(builder[i], "unsummon", u)
				set s = 1
			endif
			set i = i + 1
			set c = c + 1
		endif
		call GroupRemoveUnit(g,u)
	endloop
	call DestroyGroup(g)
	set g = null
	set u = null
	// If we're recovering towers, clear any reserved buffers then requeue
	if c > 0 and s > 0 then
		set gold_buffer = 0
		set wood_buffer = 0
		call TQAddJob(4, TOWER_RUSH, 0)
		return
	endif
	set i = 1
	loop
		exitwhen i > race_tower_peon
		call IssuePointOrderLoc(builder[i], "move", home_location)
		call RecycleGuardPosition(builder[i])
		set builder[i] = null
		set i = i + 1
	endloop
	call RemoveLocation(towerrecoveryloc)
	set towerrecoveryloc = null
	// Ensure buffers cleared when recovery completes
	set gold_buffer = 0
	set wood_buffer = 0
endfunction

function TowerRushHeroControl_k takes unit u, location t returns nothing
	local location l = null
	local item it = null
	local unit loccheck = null
	local location unitloc = null
	set l = GetUnitLoc(u)
	if not tower_peon_inplace then
		call CreateDebugTag("TRush: hero moving", 10, u, 4.00, 2.00)
		call RemoveGuardPosition(u)
		call IssuePointOrder(u, "move", tower_peon_point_x, tower_peon_point_y)
	elseif DistanceBetweenPoints(l, t) > 200 then
		call CreateDebugTag("TRush: hero moving", 10, u, 4.00, 2.00)
		call RemoveGuardPosition(u)
		call IssuePointOrderLoc(u, "move", t)
	endif
	if DistanceBetweenPoints(l, toweringplayerloc) <= DistanceBetweenPoints(t, toweringplayerloc) + 230 then
		call CreateDebugTag("TRush: Use item", 10, u, 4.00, 2.00)
		set it = GetItemOfTypeOnUnit(race_tower_item, u)
		if it != null then
			if race_tower_id_replace == 0 then
				set loccheck = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[race_tower_id], GetLocationX(t), GetLocationY(t), 270.0)
			else
				set loccheck = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[race_tower_id_replace], GetLocationX(t), GetLocationY(t), 270.0)
			endif
			set unitloc = GetUnitLoc(loccheck)
			call RemoveUnit(loccheck)
			if unitloc != null then
				call RemoveGuardPosition(u)
				call UnitUseItemPoint(u, it, GetLocationX(unitloc), GetLocationY(unitloc))
				call RemoveLocation(unitloc)
			else
				call RemoveGuardPosition(u)
				call UnitUseItemPoint(u, it, GetLocationX(t), GetLocationY(t))
			endif
		endif
		set it = null
		set loccheck = null
	endif
	call RemoveLocation(l)
	set l = null
	call RemoveLocation(unitloc)
	set unitloc = null
endfunction

function TowerRushBuilderFirstControl takes unit u, unit hall returns integer
	if hall != null and DistanceBetweenUnits(u, hall) < 1000 then
		if GetUnitAbilityLevel(u, 'ANha') != 0 or GetUnitAbilityLevel(u, 'Ahar') != 0 or GetUnitAbilityLevel(u, 'Ahr3') != 0 then
			call IssueImmediateOrder( u, "returnresources" )
		endif
		if race_militia_available and DistanceBetweenPoints_kd(toweringplayerloc, GetUnitLoc(u)) > 6800 then
			call IssueImmediateOrder( u, race_militia_unitstring )
		endif
		return 1
	endif
	return 0
endfunction

function TowerRushBuilderControl_k takes unit u, location t returns nothing
	local real builder_x = 0
	local real builder_y = 0
	local integer id = GetUnitCurrentOrder(u)
	local unit loccheck = null
	local location unitloc = null
	local location loc = null
	local location l = GetUnitLoc(u)
	if DistanceBetweenPoints(l, t) >= 500 and (id == OrderId("stop") or id != OrderId("move")) then
		call CreateDebugTag("TRush: builder moving", 10, u, 4.00, 2.00)
		call IssuePointOrderLoc(u, "move", t)
	elseif DistanceBetweenPoints(l, t) <= 500 then
		set tower_peon_inplace = true
		if id == 0 or (id != OrderId("smart") and id != OrderId("repair") and id != OrderId("renew") and id != OrderId("restoration") and id != old_id[race_tower_id] and id != old_id[racial_shop]) then  //Improve efficiency
			if towers_built >= 6 and race_tower_shop == 0 then  //Building shop
				set loc = GetSubtractionLoc(tower_target_loc, toweringplayerloc)
				set unitloc = GetProjectedLoc(tower_target_loc, loc, 800)
				call RemoveLocation(loc)
				if race_tower_id_replace == 0 then
					set loccheck = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[racial_shop], GetLocationX(unitloc), GetLocationY(unitloc), 270.0)
				else
					set loccheck = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[race_tower_id_replace], GetLocationX(unitloc), GetLocationY(unitloc), 270.0)
				endif
				set builder_x = GetUnitX(loccheck)
				set builder_y = GetUnitY(loccheck)
				call RemoveLocation(unitloc)
				call RemoveUnit(loccheck)
				if not IsTerrainPathable(builder_x, builder_y, PATHING_TYPE_BUILDABILITY) then
					if GetGold() >= GetUnitGoldCost2(racial_shop) and GetWood() >= GetUnitWoodCost2(racial_shop) then
						call IssueBuildOrderById(u, old_id[racial_shop], builder_x, builder_y)
						call CreateDebugTag("TRush: Shop Building", 10, u, 4.00, 2.00)
						set race_tower_shop = 1
					endif
				endif
				set loc = null
			else  //Building tower
				if GetUnitTypeId(u) == old_id[racial_militia] or (race_tower_bypeon == true and u == builder[4] and IsUnitType(builder[1], UNIT_TYPE_STRUCTURE) and IsUnitType(builder[2], UNIT_TYPE_STRUCTURE) and IsUnitType(builder[3], UNIT_TYPE_STRUCTURE)) then
					call RemoveLocation(l)
					set l = null
					call CreateDebugTag("TRush: cannot build tower", 10, u, 4.00, 2.00)
					return  // HUMAN militia cannot build , ELF no need all peon build but need try give one repair
				endif
				if towers_built < 3 or (towers_built >= 3 and towers_built <= 6 and GetGold() >= (GetUnitGoldCost2(race_tower_id) + 100) and GetWood() >= (GetUnitWoodCost2(race_tower_id) + 60)) or (towers_built > 6 and (GetGold() >= GetUnitGoldCost2(race_tower_id) * 2) and (GetWood() >= GetUnitWoodCost2(race_tower_id) * 2)) then
					if race_tower_id_replace == 0 then
						set loccheck = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[race_tower_id], GetLocationX(t), GetLocationY(t), 270.0)
					else
						set loccheck = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[race_tower_id_replace], GetLocationX(t), GetLocationY(t), 270.0)
					endif
					set builder_x = GetUnitX(loccheck)
					set builder_y = GetUnitY(loccheck)
					call RemoveUnit(loccheck)
					if not IsTerrainPathable(builder_x, builder_y, PATHING_TYPE_BUILDABILITY) then
						// Ensure prerequisites for the tower are available before sending peons to build
						if not CanBuildUnit(race_tower_id) then
							call TQAddJob(4, TOWER_RUSH, 0)
							call RemoveLocation(l)
							set l = null
							set loccheck = null
							set unitloc = null
							return
						endif
						call IssueBuildOrderById(u, old_id[race_tower_id], builder_x, builder_y)
						if towers_built != 0 then
							call CreateDebugTag("TRush: Building", 10, u, 4.00, 2.00)
						else
							call CreateDebugTag("TRush: First Building", 10, u, 4.00, 2.00)
							set towers_built = 1
						endif
					endif
				endif
			endif
		endif
	endif
	call RemoveLocation(l)
	set l = null
	set loccheck = null
	set unitloc = null
endfunction

function TowerRushTowerMoveControl_k takes unit u, location t returns nothing  //reuse these too far towers
	local unit loccheck = null
	local location unitloc = null
	local location l = GetUnitLoc(u)
	local integer id = GetUnitCurrentOrder(u)
	local real s = GetUnitState(u, UNIT_STATE_LIFE)
	if s >= GetUnitState(u, UNIT_STATE_MAX_LIFE) * 0.65 then  //exclude UNIT_STATE_LIFE too low , dangerous to stand up
		if DistanceBetweenPoints(l, toweringplayerloc) > DistanceBetweenPoints(t, toweringplayerloc) + 220 and id == 0 then
			set unitloc = GetProjectedLoc(t, l, 70)
			if race_tower_id_replace == 0 then
				set loccheck = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[race_tower_id], GetLocationX(unitloc), GetLocationY(unitloc), 270.0)
			else
				set loccheck = CreateUnit(Player(PLAYER_NEUTRAL_PASSIVE), old_id[race_tower_id_replace], GetLocationX(unitloc), GetLocationY(unitloc), 270.0)
			endif
			call RemoveLocation(unitloc)
			set unitloc = GetUnitLoc(loccheck)
			call RemoveUnit(loccheck)
			if not IsTerrainPathable(GetLocationX(unitloc), GetLocationY(unitloc), PATHING_TYPE_BUILDABILITY) then
				call RemoveGuardPosition(u)
				call IssueImmediateOrder( u, race_tower_moveendstring)
				call IssuePointOrderLoc( u, race_tower_movestartstring, unitloc)  //move tree
			endif
			call RemoveLocation(unitloc)
			set id = GetUnitCurrentOrder(u)  // update order
		endif
	endif
	if s >= 1 and (DistanceBetweenPoints(l, toweringplayerloc) <= DistanceBetweenPoints(t, toweringplayerloc) or id != OrderId(race_tower_movestartstring) or s < GetUnitState(u, UNIT_STATE_MAX_LIFE) * 0.5) then
		call RemoveGuardPosition(u)
		call IssuePointOrderLoc( u, race_tower_movestartstring, l)
	endif
	call RemoveLocation(l)
	set l = null
	set loccheck = null
	set unitloc = null
endfunction

function TowerRushSetBuilder takes unit u returns boolean
	if IsUnitType(u, UNIT_TYPE_STRUCTURE) or UnitAlive(u) == false or IsUnitBuying(u) or IsUnitInGroup(u, unit_zepplin_move) then  // the peon harvest gold or build by peon become, like ELF
		return false
	endif
	if tower_attack_run == true then
		call RemoveGuardPosition(u)
	endif
	return true
endfunction

function TowerRushJob takes nothing returns nothing
	local group g = null
	local group tempg = null
	local unit u = null
	local location t = null
	local item it = null
	local integer i = 0
	local integer s = 0
	local integer towers_attck = 0 // Attacking towers built
	local integer randomnum = GetRandomInt(1, 4)
	local integer towermove = 30
	local integer buildtries = 10
	local integer id = 0
    local integer assigned_count = 0
    local integer ii = 0
    local integer tower_unit_id = 0
    local integer gold_per = 0
    local integer wood_per = 0
    local integer reserve_gold = 0
    local integer reserve_wood = 0
    local integer extra_gold = 0
    local integer extra_wood = 0
    local integer base_gold = 0
    local integer base_wood = 0
	local real bt = 0 // Non attacking towers built
	local real time_next_try = 4
	local real EnemyStrength = 0
	local real OwnStrength = 0
	local real x = 0
	local real y = 0
	local boolean CloseJob = false

	call DisplayToAllJobDebug("TOWER_RUSH Job START")
	if towerrecoveryloc != null then
		call TowerRushRecoveryBuild()
		return
	endif
	if not hero_built[1] and race_tower_item_must == true then
		call TQAddJob(5, TOWER_RUSH, 0)
		return
	endif
	if race_tower_move then
		set buildtries = 8  // tower can move
	endif
	set g = CreateGroup()
	call GroupEnumUnitsInRange(g, GetLocationX(tower_target_loc), GetLocationY(tower_target_loc), 1500, null)
	loop
		set u = FirstOfGroup(g)
		exitwhen u == null
		if GetOwningPlayer(u) == ai_player and GetUnitState(u, UNIT_STATE_LIFE) >= 1 and IsUnitType(u, UNIT_TYPE_STRUCTURE) then
			set id = GetUnitTypeId(u)
			if id == old_id[race_tower_id] or id == old_id[race_tower_upgrade] or id == old_id[race_tower_upgrade2] then
				if id == old_id[race_tower_id] then
						// Prefer higher upgrade when appropriate and when requirements for doing so are met
						if (randomnum == 4 or not CanBuildUnit(race_tower_id)) and race_tower_upgrade2 != 0 and TownCount(race_tower_upgrade2) < 2 then
							if CanBuildUnit(race_tower_upgrade2) then
								call IssueImmediateOrderById(u, old_id[race_tower_upgrade2])
							else
								call TQAddJob(4, TOWER_RUSH, 0)
								return
							endif
						elseif race_tower_upgrade != 0 then
							if CanBuildUnit(race_tower_upgrade) then
								call IssueImmediateOrderById(u, old_id[race_tower_upgrade])
							else
								call TQAddJob(4, TOWER_RUSH, 0)
								return
							endif
						endif
					endif
				if race_tower_move == true then
					set builder[towermove] = u
					set towermove = towermove + 1
				endif
				if IsUnitType(u, UNIT_TYPE_ATTACKS_GROUND) or IsUnitType(u, UNIT_TYPE_ATTACKS_FLYING) then
					set towers_attck = towers_attck + 1
				else
					set bt = bt + 0.3
				endif
				set i = i + 1
			elseif id == old_id[racial_shop] then
				set s = s + 1
			endif
			if GetUnitState(u, UNIT_STATE_LIFE) < GetUnitState(u, UNIT_STATE_MAX_LIFE) * 0.7 then
				set randomnum = GetRandomInt(1, 4)
				if builder[randomnum] != null and GetUnitCurrentOrder(builder[randomnum]) == 0 then
					call IssueTargetOrder(builder[randomnum], "repair", u )
					call IssueTargetOrder(builder[randomnum], "renew", u )
					call IssueTargetOrder(builder[randomnum], "restoration", u )
					call CreateDebugTag("TRush: repair tower", 10, builder[randomnum], 4.00, 2.00)
				endif
			endif
		endif
		call GroupRemoveUnit(g,u)
	endloop
	set towers_built = i
	set race_tower_shop = s
	if tower_used_item == false then
		set tower_used_item = towers_built > 0 or (IsPointBlighted(GetLocationX(t), GetLocationY(t)) == IsPointBlighted(GetLocationX(home_location), GetLocationY(home_location)))
		//have tower , no need send hero used item or UD item used success
	endif
	if towers_built <= 3 then
		if GetLocationCreepStrength(GetLocationX(tower_target_loc), GetLocationY(tower_target_loc), 500) > 0 then
			set t = GetProjectedLoc(tower_target_loc, towerrushsubloc, 600)
		else
			set t = GetProjectedLoc(tower_target_loc, towerrushsubloc, 660)
		endif
		if first_tower_point_x == 0 or first_tower_point_y == 0 then
			set first_tower_point_x = GetLocationX(t)
			set first_tower_point_y = GetLocationY(t)
		endif
	elseif towers_built <= 6 then
		set t = GetProjectedLoc(tower_target_loc, towerrushsubloc, 200)
		if second_attack_point_x == 0 or second_attack_point_y == 0 then
			set second_attack_point_x = GetLocationX(t)
			set second_attack_point_y = GetLocationY(t)
		endif
	else
		if race_tower_move and builder[10] != null and UnitAlive(builder[10]) then
			set t = GetUnitLoc(builder[10])
		else
			set t = GetProjectedLoc(tower_target_loc, towerrushsubloc, -400)
		endif
	endif
	call CreateDebugTagLoc("build tower loc", 10, GetLocationX(t), GetLocationY(t), 60.00, 55.00)
	//call Trace("Getting ready to tower rush")
	if GetLocationX(t) != 0 and GetLocationY(t) != 0 and (hero_built[1] or hero_built[2] or hero_built[3]) then
		set CloseJob = town_threatened and town_threat[most_threatened_town] >= teleport_low_threat * 1.5
		set CloseJob = CloseJob or (race_tower_item_must and not tower_used_item and hero_unit[2] != null and builder[0] == null)  //  too longer time not buy item , but item must
		if not CloseJob then
			// Delegate strength-based decision to common.eai helper
			set CloseJob = ShouldEndTowerRushStrength(GetLocationX(t), GetLocationY(t), towers_built, towers_built_count, towers_attck, bt)
			// If strength doesn't mandate ending, keep the old visibility fallback
			if not CloseJob and not console_tr_command and not tower_peon_inplace then
				if EnemyStrongerThan(GetLocationX(t), GetLocationY(t), 2.0, towers_attck, bt) then
					set i = 1
					loop
						exitwhen i > race_tower_peon or CloseJob
						set CloseJob = IsUnitVisible(builder[i], rushedplayer)
						set i = i + 1
					endloop
				endif
			endif
		endif
		call CreateDebugTagLoc("TR Close state : " + B2S(CloseJob), 10, GetLocationX(t), GetLocationY(t), 60.00, 55.00)
	endif
	// Only finish the tower rush early if the rushed player is invalid, the enemy point is known and has no strength,
	// or CloseJob is set. This prevents the job from ending immediately when `rushedplayer` or `enemy_point_*` are unset (0/null).
	if (rushedplayer == null or GetPlayerSlotState(rushedplayer) != PLAYER_SLOT_STATE_PLAYING) or ((enemy_point_x != 0 and enemy_point_y != 0) and GetLocationEnemyStrength(enemy_point_x, enemy_point_y, 1600, false) == 0) or CloseJob == true then  //or (builder[1] != null and (GetUnitState(builder[1], UNIT_STATE_LIFE) <= 0  and GetUnitState(builder[3], UNIT_STATE_LIFE) <= 0 and GetUnitState(builder[2], UNIT_STATE_LIFE) <= 0)) then
		call Trace("I have finished the tower rush")  //maybe enemy give up the Location,so temporary no need built
		set gold_buffer = 0
		set wood_buffer = 0
		if tower_attack_run == true then
			if race_has_power_build then  // reduction
				call GroupEnumUnitsOfPlayer(g, ai_player, null)
				call GroupRemoveUnit(g, expansion_peon)
				call GroupRemoveUnit(g, build_zeppelin)
				set i = 41
				loop
					set u = FirstOfGroup(g)
					exitwhen u == null
					if IsUnitType(u, UNIT_TYPE_PEON) and not IsUnitBuying(u) and UnitAlive(u) and not IsUnitHidden(u) then
						call TQAddUnitJob(3, RESET_GUARD_POSITION, 0, u)  //Prevent peon immediately go tower point repair tower
					endif
					if builder[i] != null then
						set builder[i] = null
						set i = i + 1
					endif
					call GroupRemoveUnit(g,u)
				endloop
				call GroupClear(g)
			endif
			set i = 1
			call GroupEnumUnitsInRange(g, GetLocationX(towerrushsubloc), GetLocationY(towerrushsubloc), 2000, null)
			loop
				set u = FirstOfGroup(g)
				exitwhen u == null
				if GetOwningPlayer(u) == ai_player and IsUnitType(u, UNIT_TYPE_STRUCTURE) and UnitAlive(u) then
					call IssueImmediateOrderById( u, order_cancel)  //Cancel can recycling resources and prevent AI run FAST BUILD
					if race_sell_ziggurats then
						call Unsummon(u)  // if build done, sell
						if builder[i] != null and UnitAlive(builder[i]) then
							call IssueTargetOrder(builder[i], "unsummon", u)
							set i = i + 1
						endif
					endif
				endif
				call GroupRemoveUnit(g,u)
			endloop
			if race_sell_ziggurats and not CloseJob then  // if enemy fail, sell
				set towerrecoveryloc = Location(GetLocationX(towerrushsubloc), GetLocationY(towerrushsubloc))
				call TQAddJob(2, TOWER_RUSH, 0)
			else
				set i = 1
				loop
					exitwhen i > race_tower_peon
					if builder[i] != null and UnitAlive(builder[i]) then
						call IssuePointOrderLoc(builder[i], "move", home_location)
						call RecycleGuardPosition(builder[i])
					endif
					set builder[i] = null
					set i = i + 1
				endloop
			endif
		endif
		set race_tower_alliancetarget = false
		set tower_used_item = false
		set tower_attack_run = false
		set tower_peon_run = false
		set tower_peon_inplace = false
		set towerrush = false
		set console_tr_command = false
		if chatting then
			call DisplayToAllies(chat_stop_towerrush)
			call DisplayToObserversImportant(chat_stop_towerrush)
		endif
		if builder[0] != null and GetItemOfTypeOnUnit(race_tower_item, builder[0]) != null then
			call SetItemPawnable(GetItemOfTypeOnUnit(race_tower_item, builder[0]), true)  //can selling
			call SetItemDroppable(GetItemOfTypeOnUnit(race_tower_item, builder[0]), true)  //droppable
		endif
		call RecycleGuardPosition(builder[0])
		set builder[0] = null
		set builder[10] = null
		set rushedplayer = null
		call RemoveLocation(toweringplayerloc)
		call RemoveLocation(tower_target_loc)
		call RemoveLocation(towerrushsubloc)
		call RemoveLocation(t)
		set toweringplayerloc = null
		set tower_target_loc = null
		set towerrushsubloc = null
		set t = null
		call DestroyGroup(tower_rush_power)
		set tower_rush_power = null
		call DestroyGroup(g)
		set g = null
		set towers_built = 0
		set towers_built_count = 0
		set race_tower_shop = 0
		set first_tower_point_x = 0
		set first_tower_point_y = 0
		set second_attack_point_x = 0
		set second_attack_point_y = 0
		set enemy_point_x = 0
		set enemy_point_y = 0
		set tower_peon_point_x = 0
		set tower_peon_point_y = 0
		call SetPeonsRepair(true)
		return
	endif

	if builder[0] != null and UnitAlive(builder[0]) and GetItemOfTypeOnUnit(race_item_expansion_item_id, builder[0]) == null then
		call RecycleGuardPosition(builder[0])  //prevent item change hero
	endif
	if builder[0] == null and race_tower_item != 0 then
		set it = GetItemOfType(it,race_tower_item)
		set builder[0] = GetItemHero(it)
	endif
	if builder[0] != null and it != null then  //Avoid next cycle judgment
		call SetItemDroppable(it, false)  //prevent droppable
		call SetItemPawnable(it, false)  //prevent selling
	endif
	set it = null
	loop
		exitwhen towermove < 30
		call TowerRushTowerMoveControl_k(builder[towermove], t)
		set builder[towermove] = null
		set towermove = towermove - 1
	endloop

	call GroupClear(g)
	call GroupEnumUnitsOfPlayer(g, ai_player, null)
	call GroupRemoveUnit(g, expansion_peon)
	set g = SelectUnittype(g, UNIT_TYPE_PEON, true)
	set g = SelectByAlive(g, true)
	set g = SelectByLoaded(g,false)
	if race_manual_loading then
		set g = SelectByOrderOr(g, OrderId("smart"), OrderId("harvest"),true)
	endif
	set g = SelectByHidden(g,false)
	set i = 1
	loop
		exitwhen i > race_tower_peon
		call GroupRemoveUnit(g,builder[i])
		set i = i + 1
	endloop
	set tempg = CopyGroup(g)
	set g = GetNearestSubGroupOfGroup(g, tower_target_loc, 10)
	set i = 1
	loop
		set u = FirstOfGroup(g)
		exitwhen i > race_tower_peon or u == null
		if builder[i] == null or not UnitAlive(builder[i]) or IsUnitType(builder[i], UNIT_TYPE_STRUCTURE) or IsUnitLoaded(builder[i]) then  // prevent WIPS build and harvest
			if TowerRushSetBuilder(u) then
				call CreateDebugTag("TRush: set peon", 10, u, 4.00, 2.00)
				set builder[i] = u
				call GroupRemoveUnit(tempg,u)  // Prevent cannot build tower
				call GroupRemoveUnit(tower_rush_power,u)
			endif
		endif
		set i = i + 1
		call GroupRemoveUnit(g,u)
	endloop

	// Reserve resources for towers/upgrades when peons assigned so other build logic
	// doesn't spend them during this build loop. Also keep a small extra for repairs.
	if gold_buffer == 0 and wood_buffer == 0 then

		loop
			exitwhen ii > race_tower_peon
			if builder[ii] != null and UnitAlive(builder[ii]) and not IsUnitType(builder[ii], UNIT_TYPE_STRUCTURE) then
				set assigned_count = assigned_count + 1
			endif
			set ii = ii + 1
		endloop

		// Only set reservation buffers when an attack-style tower run is active.
		if tower_attack_run == true then
			// If we have assigned builders prefer reserving per-builder cost
			// Choose the most expensive buildable tower variant (tower or upgrades)
			set tower_unit_id = race_tower_id
			if race_tower_upgrade != 0 then
				if GetUnitGoldCost2(race_tower_upgrade) > GetUnitGoldCost2(tower_unit_id) then
					set tower_unit_id = race_tower_upgrade
				endif
			endif
			if race_tower_upgrade2 != 0 then
				if GetUnitGoldCost2(race_tower_upgrade2) > GetUnitGoldCost2(tower_unit_id) then
					set tower_unit_id = race_tower_upgrade2
				endif
			endif
			if assigned_count > 0 then
				set gold_per = GetUnitGoldCost2(tower_unit_id)
				set wood_per = GetUnitWoodCost2(tower_unit_id)
				set reserve_gold = assigned_count * gold_per
				set reserve_wood = assigned_count * wood_per
				set extra_gold = reserve_gold / 7
				if extra_gold < 1 then
					set extra_gold = 1
				endif
				set extra_wood = reserve_wood / 7
				if extra_wood < 1 then
					set extra_wood = 1
				endif
				set gold_buffer = reserve_gold + extra_gold
				set wood_buffer = reserve_wood + extra_wood
			else
				// No peons assigned — fall back to conservative fixed buffers (commented rules)
				set base_gold = 0
				set base_wood = 0
				if towers_built < 4 and race_simple_melee > 2 then
					set base_gold = GetUnitGoldCost2(tower_unit_id) + 100
					set base_wood = GetUnitWoodCost2(tower_unit_id) + 100
				elseif towers_built < 8 and race_simple_melee > 3 then
					set base_gold = GetUnitGoldCost2(tower_unit_id) + 50
					set base_wood = GetUnitWoodCost2(tower_unit_id) + 50
				else
					set base_gold = 0
					set base_wood = 0
				endif
				if base_gold > 0 or base_wood > 0 then
					set extra_gold = base_gold / 7
					if extra_gold < 1 then
						set extra_gold = 1
					endif
					set extra_wood = base_wood / 7
					if extra_wood < 1 then
						set extra_wood = 1
					endif
					set gold_buffer = base_gold + extra_gold
					set wood_buffer = base_wood + extra_wood
				else
					set gold_buffer = 0
					set wood_buffer = 0
				endif
			endif
		else
			// Not an attack run — ensure no buffers are set
			set gold_buffer = 0
			set wood_buffer = 0
		endif
		call Trace("Tower Rush: Resource Buffers set to Gold: " + Int2Str(gold_buffer) + " Wood: " + Int2Str(wood_buffer))
	endif
	if race_has_power_build and tower_attack_run == true then  // fix AI pull all peon to build tower
		set i = 41
		loop
			exitwhen i > 46 or FirstOfGroup(tempg) == null  //prevent harvest gold peon more then 5 , so just control 3 peon
			if builder[i] == null or not UnitAlive(builder[i]) or not IsUnitInGroup(builder[i], tempg) then
				set builder[i] = FirstOfGroup(tempg)
				if i > 43 then
					call RemoveGuardPosition(builder[i])
					call GroupAddUnit(tower_rush_power,builder[i])
					call IssueImmediateOrder(builder[i], "autoharvestgold")
				endif
			elseif i > 43 then
				if GetUnitCurrentOrder(builder[i]) != OrderId("resumeharvesting") then
					call CreateDebugTag("TRush: harvest gold peon", 10, u, 4.00, 2.00)
					call IssueImmediateOrder(builder[i], "autoharvestgold")
				endif
			endif
			call GroupRemoveUnit(tempg,builder[i])
			set i = i + 1
		endloop
		call GroupRemoveUnit(tower_rush_power,builder[41])  // give AI 2 peon build other construct
		call GroupRemoveUnit(tower_rush_power,builder[42])
		call GroupRemoveUnit(tower_rush_power,builder[43])
		call RecycleGuardPosition(builder[41])
		call RecycleGuardPosition(builder[42])
		call RecycleGuardPosition(builder[43])
		loop
			set u = FirstOfGroup(tempg)
			exitwhen u == null
			if GetUnitCurrentOrder(u) != OrderId("resumeharvesting") and not IsUnitInGroup(u, tower_rush_power) then
				call CreateDebugTag("TRush: harvest lumber peon", 10, u, 4.00, 2.00)
				call RemoveGuardPosition(u)
				call GroupAddUnit(tower_rush_power,u)
				call IssueImmediateOrder(u, "autoharvestlumber")
			endif
			call GroupRemoveUnit(tempg,u)
		endloop
	endif

	if tower_attack_run == true then
		if builder[0] != null and GetItemNumberOnUnit(race_tower_item, builder[0]) > 0 and GetUnitState(builder[0], UNIT_STATE_LIFE) >= 1 then
			call Trace("Tower Rush: Spread Blight")
			if GetItemNumberOnUnit(race_tower_item, builder[0]) == 0 or GetUnitState(builder[0], UNIT_STATE_LIFE) < GetUnitState(builder[0], UNIT_STATE_MAX_LIFE) * 0.5 or teleporting or town_threatened then
				call RecycleGuardPosition(builder[0])
			else 
				call TowerRushHeroControl_k(builder[0], t)
			endif
		endif
		set i = 1
		if tower_peon_run != true then
			call GroupClear(g)
			call GroupEnumUnitsOfPlayer(g, ai_player, null)
			set g = SelectUnittype(g, UNIT_TYPE_TOWNHALL, true)
			set g = SelectByAlive(g, true)
			loop
				exitwhen i > race_tower_peon
				call RemoveGuardPosition(builder[i])
				call DestroyGroup(tempg)
				set tempg = CopyGroup(g)
				loop
					set u = FirstOfGroup(tempg)
					exitwhen u == null
					exitwhen TowerRushBuilderFirstControl(builder[i], u) > 0
					call GroupRemoveUnit(tempg,u)
				endloop
				set i = i + 1
			endloop
			set tower_peon_run = true
			set time_next_try = 2
		elseif towers_built < buildtries and towers_built - towers_attck < 2 then  // here towers_built no change , so AI will build more tower , even if tower can move , but not a big problem
			loop
				exitwhen i > race_tower_peon
				call TowerRushBuilderControl_k(builder[i], t)
				set x = GetUnitX(builder[i])
				set y = GetUnitY(builder[i])
				set i = i + 1
			endloop
			if tower_peon_inplace then
				set towers_built_count = towers_built_count + 1
			endif
			set tower_peon_point_x = x / Max(1, i - 1)
			set tower_peon_point_y = y / Max(1, i - 1)
			set time_next_try = 1
		endif
	endif

	call DestroyGroup(tempg)
	set tempg = null
	call DestroyGroup(g)
	set g = null
	call RemoveLocation(t)
	set t = null
	set u = null
	call TQAddJob(RMax(time_next_try,1), TOWER_RUSH, 0)
endfunction


#ENDIF


