#IFDEF GLOBAL
    boolean towerrush = false
    boolean race_tower_alliancetarget = false  // alliance have tower rush target?
    boolean tower_used_item = false  // tower rush item used done?
    boolean tower_attack_run = false  // tower rush attack team has left home?
    boolean tower_peon_run = false  // tower rush peon team has left home? just first left
    boolean tower_peon_inplace = false  // tower rush peon in place? use to attack
    boolean console_tr_command = false  // tower rush Order by Player from Console?
    unit array builder  // hero and peon go to enemy home
    location tower_target_loc = null
    real first_tower_point_x = 0
    real first_tower_point_y = 0
    real second_attack_point_x = 0
    real second_attack_point_y = 0
    real enemy_point_x = 0
    real enemy_point_y = 0
    real tower_peon_point_x = 0
    real tower_peon_point_y = 0
    integer towers_built = 0
    integer towers_built_count = 0
    integer race_tower_shop = 0
    player rushedplayer = null
    group tower_rush_power = null
    location toweringplayerloc = null
    location towerrushsubloc = null
	location towerrecoveryloc = null
#ELSE

// ######################## Tower Rush Check ##############################
// #  Created by Strategy Master 13/07/05. The conditions that check if 
// #  tower rush can start.
// #######################################################################

function StartTowerRush takes integer delay returns nothing
	set towerrush = true
	set tower_rush_power = CreateGroup()
	call SetPeonsRepair(false)
	call DisplayToAllies(chat_towerrush)
	call DisplayToObserversImportant(chat_towerrush)
	set towers_built = 0
	set towers_built_count = 0
	call Trace("Starting the tower rush")
	call TQAddJob(RMax(delay,5), TOWER_RUSH, 0)
endfunction

function TowerRushCheck takes nothing returns nothing
	local integer i = GetRandomInt(1,100)
	local unit u = null
	local player p = null
	local real distance = GetNearestEnemyDistance()
	local boolean mapinappropriate = game_is_ffa or (c_ally_total > 0) // Cannot use in FFA or team games due to attack override bug
	call DisplayToAllJobDebug("TOWER_RUSH_CHECK JOB START")
	//if rushedplayer == null or (not IsLocationFoggedToPlayer(home_location, rushedplayer) and not console_tr_command) or distance > 14000 or distance < front_base_distance * 2 or builder[10] == null or (mapinappropriate and not console_tr_command) or (race_tower_item_must and racial_shop == 0 and not IsPointBlighted(GetUnitX(builder[10]), GetUnitY(builder[10])) and IsPointBlighted(GetLocationX(home_location), GetLocationY(home_location))) then  //Distance to too far or player too many or ffa , ROC not shop
	//	call Trace("the map unsuited tower rush")
	//	if console_tr_command then
	//		call DisplayToAllies(chat_no_orc)
	//	endif
	//	set builder[10] = null
	//	return
	//endif

	if i > race_towerrush_probability and not console_tr_command and debugging <= 0 then
		call Trace("Not tower rushing this time due to probability check")
		return
	endif	

	if mapinappropriate and not console_tr_command then
		call Trace("Not tower rushing this time due to a inappropiate map or player setup")
    	return
  	endif  

	if not console_tr_command then
		set i = GetRandomInt(1,100)
		// Apply probability check based on distance before starting tower rush
		// Check distance-based probability
 		if i > 75 and distance >= 11000 then
			// Fail 25% of cases at 11000+ distance
			call RemoveLocation(tower_target_loc)
			set tower_target_loc = null
			set builder[10] = null
			//call TQAddJob(90, TOWER_RUSH_CHECK, 0)
			call Trace("At this distance chance to rush failed")
			return
		elseif i > 50 and distance >= 14000 then
			// Fail 50% of cases at 14000+ distance
			call RemoveLocation(tower_target_loc)
			set tower_target_loc = null
			set builder[10] = null
			//call TQAddJob(90, TOWER_RUSH_CHECK, 0)
			call Trace("At this distance chance to rush failed")
			return
		elseif i > 20 and (distance >= 16000 or mapSize > 3) then
			// Fail 80% of cases at 16000+ distance
			call RemoveLocation(tower_target_loc)
			set tower_target_loc = null
			set builder[10] = null
			//call TQAddJob(90, TOWER_RUSH_CHECK, 0)
			call Trace("At this distance chance to rush failed")
			return
		elseif distance >= 20000 then
			call RemoveLocation(tower_target_loc)
			set tower_target_loc = null
			set builder[10] = null
			//call TQAddJob(90, TOWER_RUSH_CHECK, 0)
			call Trace("At this distance chance to rush failed")
			return
		endif
	endif

	if debugging > 0 and Player(debug_player) != ai_player then
		call Trace("Skipping TowerRushCheck: debugging enabled for another player")
		return
	endif

	//set rushedplayer = GetNearestEnemy()  // no longer needed as is set from the GetNearestEnemyDistance function
	if toweringplayerloc == null then
		if race_tower_alliancetarget == false and ai_time <= 96 then  // Priority collaboration with allies
			set u = GetAllianceTarget()
			if u == null or not UnitAlive(u) then
				set u = I2U(GetStoredInteger(amaiCache, Int2Str(ALLIANCE_TARGET), "0"))
			endif
		endif
		if u != null then  //coordinate towertush with allies , 480 second
			set p = GetOwningPlayer(u)
			if IsPlayerEnemy(p, ai_player) and p != Player(PLAYER_NEUTRAL_AGGRESSIVE) then
				set rushedplayer = p
				set toweringplayerloc = GetStartLocationLoc(GetPlayerStartLocation(rushedplayer))
				set race_tower_alliancetarget = true
				set builder[10] = null
			endif
			set p = null
			set u = null
		endif
		if u == null and builder[10] == null then
			call Trace("TowerRushCheck: no alliance target and no builder unit found; will wait")
			call Trace("TowerRushCheck: distance=" + Int2Str(R2I(distance)) + " c_enemy_total=" + Int2Str(c_enemy_total) + " c_enemy_user_total=" + Int2Str(c_enemy_user_total) + " ai_time=" + Int2Str(ai_time))
			// Consider checking stored alliance target or recent nearest enemy
			if HaveStoredInteger(amaiCache, Int2Str(ALLIANCE_TARGET), "0") then
				call Trace("TowerRushCheck: stored ALLIANCE_TARGET exists: " + Int2Str(GetStoredInteger(amaiCache, Int2Str(ALLIANCE_TARGET), "0")))
			else
				call Trace("TowerRushCheck: no stored ALLIANCE_TARGET")
			endif
		endif
		// Fallback: if no builder unit (e.g. not a team game), try nearest enemy player start
		if u == null and builder[10] == null then
			set p = GetNearestEnemy()
			if p != null then
				set rushedplayer = p
				set toweringplayerloc = GetStartLocationLoc(GetPlayerStartLocation(rushedplayer))
				call Trace("TowerRushCheck: fallback - using nearest enemy start location for rushedplayer=" + Int2Str(GetPlayerId(rushedplayer)))
			endif
		endif
		if toweringplayerloc == null and builder[10] != null then
			set toweringplayerloc = GetUnitLoc(builder[10])  // builder[10] from the GetNearestEnemyDistance
		endif
	endif
	if toweringplayerloc != null then
		if GetPlayerRace(rushedplayer) == RACE_NIGHTELF and GetPlayerStructureCount(rushedplayer, true) < 6 then
			set tower_target_loc = GetLocationInDistanceFromLoc(toweringplayerloc, 780, true)  // ELF can be closer
		else
			set tower_target_loc = GetLocationInDistanceFromLoc(toweringplayerloc, front_base_distance, true)  //DistanceBetweenPoints the toweringplayerloc and first_tower_point , front_base_distance now is change , so need set new
		endif
		if tower_target_loc != null then
			set racial_rushcreep = 0  // TowerRush BR , waste time
			set enemy_point_x = GetLocationX(toweringplayerloc)
			set enemy_point_y = GetLocationY(toweringplayerloc)
			set towerrushsubloc = GetSubtractionLoc(tower_target_loc, toweringplayerloc)
			call StartTowerRush(1)
			call Trace("Tower rush starting")
			return
		endif
		call Trace("TowerRushCheck: could not determine a valid tower target location; clearing toweringplayerloc")
		call RemoveLocation(toweringplayerloc)
		set toweringplayerloc = null
	endif
	if console_tr_command then
		call DisplayToAllies(chat_no_orc)
	endif
	set enemy_point_x = 0
	set enemy_point_y = 0
	set rushedplayer = null
	set builder[10] = null
	set race_tower_alliancetarget = false
	call Trace("TowerRushCheck ended: conditions not met or reset; reschedule if needed")
	//call TQAddJob(90, TOWER_RUSH_CHECK, 0)  // Reschedule check
endfunction


#ENDIF

