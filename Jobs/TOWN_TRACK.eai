#IFDEF GLOBAL
    boolean town_threatened = false
    integer array town_owner
    location array town_loc
    real array town_value
    real array town_threat
    integer array town_count
    integer array town_threat_army
    boolean array town_can_tp
    integer most_threatened_town = -1
    integer most_threatened_enemy_town = -1
    integer max_value_town = -1
    integer home_town = -1
    integer town_num = 0
    real town_radius = 2500
    group in_town_group = CreateGroup()
#ELSE
function CopyTown takes integer from, integer to returns nothing
  set town_owner[to] = town_owner[from]
  set town_loc[to] = town_loc[from]
  set town_value[to] = town_value[from]
  set town_threat[to] = town_threat[from]
  set town_count[to] = town_count[from]
  set town_can_tp[to] = town_can_tp[from]
  if town_num == from then
    set town_num = to
  endif  
  // Not below are temporary to keep usage of it accurate as army track will run after this and re-evaluate.
  if most_threatened_town == from then
    set most_threatened_town = to
  endif
  if most_threatened_enemy_town == from then
    set most_threatened_enemy_town = to
  endif  
endfunction

function TrackTown takes integer num returns boolean
  local group g = CreateGroup()
  local unit u = null
  local location l = Location(0,0)
  
  set town_value[num] = 0
  set town_count[num] = 0
  set town_can_tp[num] = false
  call GroupEnumUnitsInRangeOfLoc(g, town_loc[num], town_radius, null)
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    if UnitAlive(u) and GetUnitState(u, UNIT_STATE_LIFE) > 0 and GetOwningPlayer(u) == Player(town_owner[num]) and IsUnitType(u, UNIT_TYPE_STRUCTURE) and not IsUnitInGroup(u, in_town_group) then
      call GroupAddUnit(in_town_group, u)
      set l = GetSumLoc_dd(l, GetUnitLoc(u))
      set town_count[num] = town_count[num] + 1
      set town_value[num] = town_value[num] + 1
      if IsUnitType(u, UNIT_TYPE_TOWNHALL) then
        set town_can_tp[num] = true
      endif
    endif
    call GroupRemoveUnit(g, u)
  endloop
  call DestroyGroup(g)
  set g = null
  set l = GetDivisionLoc_d(l, Max(town_count[num], 1))
  call RemoveLocation(town_loc[num])
  set town_loc[num] = l
//  if town_count[num] != 0 then
//    call PingMinimapEx(GetLocationX(l), GetLocationY(l), 9, 0, 0, 255, false)
//  endif
  set l = null
  if town_count[num] == 0 then
    call RemoveLocation(town_loc[num])
    set town_loc[num] = null
    return false
  endif
  if (Player(town_owner[num]) == ai_player) then 
    call CreateDebugTagLoc("Tracked Town: " + Int2Str(num) + " structures:" + Int2Str(town_count[num]), 16, GetLocationX(l), GetLocationY(l), 8.00, 3.00)
    if town_value[num] > max_value_town then
      set max_value_town = num
    endif
    if DistanceBetweenPoints(town_loc[num], home_location) <= town_radius then
      set home_town = num
    endif
  endif  
  return true
endfunction

function TrackExistingTowns takes nothing returns nothing
  local integer i = 0
  local integer validTownNum = 0
  loop
    exitwhen i >= town_num
    if TrackTown(i) then
      if (i != validTownNum) then
        call CopyTown(i, validTownNum)
      endif
      set validTownNum = validTownNum + 1
    endif
    set i = i + 1
  endloop
  set town_num = validTownNum
endfunction

function SeedNewTownAtLoc takes location l, integer p returns nothing
  set town_owner[town_num] = p
  set town_loc[town_num] = l
  set town_threat[town_num] = 0
  if TrackTown(town_num) then
    set town_num = town_num + 1
  endif  
endfunction

function SeedNewTownsForPlayer takes integer p returns nothing
  local group g = CreateGroup()
  local unit u = null
  local location l = null
  call GroupEnumUnitsOfPlayer(g, Player(p), null)
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    if (IsUnitType(u, UNIT_TYPE_TOWNHALL) or IsUnitGoldMine(u)) and UnitAlive(u) and not IsUnitInGroup(u, in_town_group) then
      call SeedNewTownAtLoc(GetUnitLoc(u), p)
    endif
    call GroupRemoveUnit(g, u)
  endloop
  call GroupClear(g)
  call GroupEnumUnitsOfPlayer(g, Player(p), null)
  loop
    set u = FirstOfGroup(g)
    exitwhen u == null
    if IsUnitType(u, UNIT_TYPE_STRUCTURE) and UnitAlive(u) and not IsUnitInGroup(u, in_town_group) then
      call SeedNewTownAtLoc(GetUnitLoc(u), p)
    endif
    call GroupRemoveUnit(g, u)
  endloop
  call DestroyGroup(g)
  set g = null
  set l = null
endfunction

function SeedNewTowns takes nothing returns nothing
  local integer p = 0
  loop
    exitwhen p >= GetBJMaxPlayers()
    if GetPlayerSlotState(Player(p)) == PLAYER_SLOT_STATE_PLAYING then
      call SeedNewTownsForPlayer(p)
    endif
    set p = p + 1
  endloop
  if home_town == -1 then
    set home_town = max_value_town // most valuable town is likely the home town
  endif
endfunction

function TownTrackJob takes nothing returns nothing
  call DisplayToAllJobDebug("TOWN_TRACK Job Start")
  if not town_threatened then
    call GroupClear(in_town_group)
    set home_town = -1
    call TrackExistingTowns()
    call SeedNewTowns()
  endif
  //call TQAddJob(15, TOWN_TRACK, 0)
  call TQAddJob(1, ARMY_TRACK, 0) // Always runs army track after updating towns as otherwise most threatened town will not be correct
endfunction
#ENDIF